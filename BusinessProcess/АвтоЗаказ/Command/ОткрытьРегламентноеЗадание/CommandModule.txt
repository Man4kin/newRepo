&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "БизнесПроцесс.АвтоЗаказ.Форма.ФормаБизнесПроцесса" Тогда
		Если ПараметрыВыполненияКоманды.Источник.РасписаниеНаФорме = Неопределено Тогда
			СтруктураПараметров = ПолучитьПараметрыЗадания(ПараметрКоманды);
			Если СтруктураПараметров = Неопределено Тогда
				Расписание = Новый РасписаниеРегламентногоЗадания;
			Иначе
				Расписание = СтруктураПараметров.Расписание;
			КонецЕсли;	
		Иначе
			//при копировании
			Расписание = ПараметрыВыполненияКоманды.Источник.РасписаниеНаФорме;
		КонецЕсли;
	Иначе
		СтруктураПараметров = ПолучитьПараметрыЗадания(ПараметрКоманды);
		Если СтруктураПараметров = Неопределено Тогда
			Расписание = Новый РасписаниеРегламентногоЗадания;
			ТекущееРегламентноеЗадание = Неопределено;
		Иначе
			Расписание = СтруктураПараметров.Расписание;
			ТекущееРегламентноеЗадание = СтруктураПараметров.РегламентноеЗадание;
		КонецЕсли;	
	КонецЕсли;	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Если Диалог.ОткрытьМодально() Тогда
		Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "БизнесПроцесс.АвтоЗаказ.Форма.ФормаБизнесПроцесса" Тогда
			Оповестить("ИзмененоРасписаниеАвтоЗаказа",Диалог.Расписание,ПараметрКоманды);
		Иначе
			Если ТекущееРегламентноеЗадание = Неопределено Тогда
				СтруктураПараметров = СоздатьНовоеЗадание(ПараметрКоманды);
			КонецЕсли;
			РегламентныеЗаданияСервер.УстановитьРасписаниеРегламентногоЗадания(СтруктураПараметров.РегламентноеЗадание, Диалог.Расписание);
			Представление = Строка(Диалог.Расписание);
			ЗаписатьПредставлениеРасписания(ПараметрКоманды,Представление);
			ОповеститьОбИзменении(ПараметрКоманды);
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыЗадания(ПараметрКоманды)
	Если НЕ ПустаяСтрока(ПараметрКоманды.РегламентноеЗадание) Тогда
		УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор(ПараметрКоманды.РегламентноеЗадание);
		ТекущееРегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(УникальныйИдентификаторЗадания);
		Если ТекущееРегламентноеЗадание = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;	
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РегламентноеЗадание", Строка(ТекущееРегламентноеЗадание.УникальныйИдентификатор));
	ПараметрыФормы.Вставить("Расписание", ТекущееРегламентноеЗадание.Расписание);
	Возврат ПараметрыФормы;
КонецФункции

&НаСервере
Функция СоздатьНовоеЗадание(ПараметрКоманды)
	ТекущееРегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание("АвтоЗаказ");
	ТекущееРегламентноеЗадание.Использование = Истина;
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(ПараметрКоманды);
	ТекущееРегламентноеЗадание.Параметры = МассивПараметров;
	ТекущееРегламентноеЗадание.Наименование = "Авто заказ "+ПараметрКоманды.Поставщик;
	ТекущееРегламентноеЗадание.Записать();
	Объект = ПараметрКоманды.ПолучитьОбъект();
	Объект.РегламентноеЗадание = Строка(ТекущееРегламентноеЗадание.УникальныйИдентификатор);
	Объект.Записать();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РегламентноеЗадание", Строка(ТекущееРегламентноеЗадание.УникальныйИдентификатор));
	ПараметрыФормы.Вставить("Расписание", ТекущееРегламентноеЗадание.Расписание);
	Возврат ПараметрыФормы;
КонецФункции


&НаСервере
Функция ЗаписатьПредставлениеРасписания(ПараметрКоманды,Расписание)
	Объект = ПараметрКоманды.ПолучитьОбъект();
	Объект.ПредставлениеРасписания = Расписание;
	Объект.Записать();
КонецФункции	