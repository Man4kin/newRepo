
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ТочкаМаршрута", БизнесПроцессы.иж_ПриемкаТоваров.ТочкиМаршрута.КорректировкаАкта);
	УстановитьОтборПоНоменклатуре(Список, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	УстановитьОтборПоНоменклатуре(Список, Номенклатура);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоНоменклатуре(Список, Номенклатура)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Номенклатура", Номенклатура,,, ЗначениеЗаполнено(Номенклатура));
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Источник = "BarCodeScaner" И Событие = "BarCodeValue" Тогда
		Если Не ВводДоступен() Тогда
			КомпонентаСканераШК.СобытиеОбработано();
			Возврат;   
		КонецЕсли;
		ТекстСообщения = "";
		Результат = РаботаСоСканеромШКСервер.ПолучитьНоменклатуру(Данные, Неопределено, ТекстСообщения);
		Если Результат <> Неопределено И Результат.мНоменклатура.Количество() > 0 Тогда
			Номенклатура = Результат.мНоменклатура[0];
			УстановитьОтборПоНоменклатуре(Список, Номенклатура);
		КонецЕсли;
		//РаботаСоСканеромШККлиент.ОбработкаШтрихКодаВСпискеНоменклатуры(Данные,Список);
		КомпонентаСканераШК.СобытиеОбработано();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.Список.ДанныеСтроки(Значение);
	
	Результат = Новый Структура("Основание, Задача", Значение, ТекущаяСтрока.Задача);
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры
