
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дерево = ЗаполнитьДеревоПараметров();
	ЗначениеВДанныеФормы(Дерево,ДеревоПараметров);

	Если ПараметрыСеанса.ЭтоЦентр = Ложь Тогда
		Для Каждого Элемент Из Элементы Цикл
			Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
				Продолжить;
			КонецЕсли;	
			Элемент.Доступность = Ложь;
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДеревоПараметров() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СлужебныеЗначения.Ссылка КАК Свойство,
	               |	Параметры.Значение,
	               |	ВЫБОР
	               |		КОГДА СлужебныеЗначения.ЭтоГруппа
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Картинка,
	               |	СлужебныеЗначения.ТипЗначения КАК ТипЗначенияСвойства
	               |ИЗ
	               |	ПланВидовХарактеристик.СлужебныеЗначения КАК СлужебныеЗначения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеЗначения КАК Параметры
	               |		ПО СлужебныеЗначения.Ссылка = Параметры.Свойство
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СлужебныеЗначения.Код ИЕРАРХИЯ";
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Возврат Дерево;
	
КонецФункции

&НаКлиенте
Процедура СохранитьЗначения(Команда)
	
	СохранитьЗначенияСервер();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначенияСервер()
	
	ПеренестиЗначенияСвойств(РеквизитФормыВЗначение("ДеревоПараметров"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПараметровПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные.Картинка = 1 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоПараметровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Элемент.ПодчиненныеЭлементы.ДеревоПараметровЗначение.ОграничениеТипа = Элемент.ТекущиеДанные.ТипЗначенияСвойства;

КонецПроцедуры

&НаСервере
Процедура ПеренестиЗначенияСвойств(ДеревоПараметров) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст=
	"ВЫБРАТЬ Свойство,Значение Поместить НаборЗаписей из &НаборЗаписей КАК НЗ";
	НаборЗаписей = РегистрыСведений.СлужебныеЗначения.СоздатьНаборЗаписей();
	ЗаполнитьДополнительныеРеквизиты(НаборЗаписей, ДеревоПараметров);
	Запрос.УстановитьПараметр("НаборЗаписей",НаборЗаписей);
	Запрос.Выполнить();	
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СлужебныеЗначения.Свойство КАК СвойствоСтарое,
		|	СлужебныеЗначения.Значение КАК ЗначениеСтарое,
		|	НаборЗаписей.Свойство,
		|	НаборЗаписей.Значение
		|ИЗ
		|	НаборЗаписей КАК НаборЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СлужебныеЗначения КАК СлужебныеЗначения
		|		ПО (НаборЗаписей.Свойство = СлужебныеЗначения.Свойство)";
		
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если (ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Значение) ИЛИ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ЗначениеСтарое))
			И (ВыборкаДетальныеЗаписи.Значение <> ВыборкаДетальныеЗаписи.ЗначениеСтарое) Тогда
			Запись = РегистрыСведений.СлужебныеЗначения.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Запись,ВыборкаДетальныеЗаписи);
			Запись.Записать();
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизиты(НаборЗаписей, ДеревоПараметров)
	
	Для Каждого Стр Из ДеревоПараметров.Строки Цикл
		Если Стр.Картинка = 1 Тогда
        	ЗаполнитьДополнительныеРеквизиты(НаборЗаписей, Стр)
        Иначе
			новСтр = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(новСтр,Стр);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры





