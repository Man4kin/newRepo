&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если ПараметрКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураПараметров = ПолучитьПараметрыЗадания(ПараметрКоманды);
	ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентноеЗадание", СтруктураПараметров, , ПараметрКоманды,ПараметрыВыполненияКоманды.Окно);
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыЗадания(ПараметрКоманды)
	Если НЕ ПустаяСтрока(ПараметрКоманды.РегламентноеЗаданиеОтправки) Тогда
		УникальныйИдентификаторЗадания = Новый УникальныйИдентификатор(ПараметрКоманды.РегламентноеЗаданиеОтправки);
		ТекущееРегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(УникальныйИдентификаторЗадания);
	Иначе
		ТекущееРегламентноеЗадание = Неопределено;
	КонецЕсли;
	
	Если ТекущееРегламентноеЗадание = Неопределено Тогда
		ТекущееРегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание("ОтправкаДанныхSMS");
		ТекущееРегламентноеЗадание.Использование = Истина;
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ПараметрКоманды);
		ТекущееРегламентноеЗадание.Параметры = МассивПараметров;
		ТекущееРегламентноеЗадание.Записать();
		Объект = ПараметрКоманды.ПолучитьОбъект();
		Объект.РегламентноеЗаданиеОтправки = Строка(ТекущееРегламентноеЗадание.УникальныйИдентификатор);
		Объект.Записать();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Действие", "Изменить");
	ПараметрыФормы.Вставить("Идентификатор", ТекущееРегламентноеЗадание.УникальныйИдентификатор);
	Возврат ПараметрыФормы;
КонецФункции	