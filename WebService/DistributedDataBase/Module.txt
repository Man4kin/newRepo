Функция DataExchange(Package)
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		СтруктураЗаписи = Новый Структура;
		стрПутьФайла = КаталогВременныхФайлов()+Строка(Новый УникальныйИдентификатор()); 
		СоздатьКаталог(стрПутьФайла); 
		стрИмяФайлаZIP = ОбменДаннымиРБДСервер.ПолучитьИмяФайла(стрПутьФайла, "MSG_"+СокрЛП(ПланыОбмена.ОбменРБД.ЭтотУзел().Код)+".zip");
		стрИмяФайла = ОбменДаннымиРБДСервер.ПолучитьИмяФайла(стрПутьФайла, "MSG_"+СокрЛП(ПланыОбмена.ОбменРБД.ЭтотУзел().Код)+".xml");
		Данные = Base64Значение(Package);
		Данные.Записать(стрИмяФайлаZIP);
		ЧтениеZIP = Новый ЧтениеZipФайла(стрИмяФайлаZIP);
		ЧтениеZIP.ИзвлечьВсе(стрПутьФайла, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
		ЧтениеZIP.Закрыть();
		
		УстановитьПривилегированныйРежим(Истина);
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(стрИмяФайла);
		ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
		ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
		ПланыОбмена.ПрочитатьИзменения(ЧтениеСообщения);
		
		СтруктураЗаписи.Вставить("УзелИнформационнойБазы",    ЧтениеСообщения.Отправитель);
		СтруктураЗаписи.Вставить("РезультатВыполненияОбмена", Перечисления.РезультатыВыполненияОбмена.Выполнено);
		СтруктураЗаписи.Вставить("ДатаНачала",                ТекущаяДата());
		СтруктураЗаписи.Вставить("ДатаОкончания",             ТекущаяДата());
		Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
			//запись информации о успешной выгрузке с изменением конфигурации
			СтруктураЗаписи.Вставить("ДействиеПриОбмене",         Перечисления.ДействияПриОбмене.ВыгрузкаДанных);
		Иначе
			СтруктураЗаписи.Вставить("ДействиеПриОбмене",         Перечисления.ДействияПриОбмене.ЗагрузкаДанных);
		КонецЕсли;	
		ЧтениеСообщения.ЗакончитьЧтение();
		ЧтениеXML.Закрыть();
		Результат = Истина;
		Если НЕ ПараметрыСеанса.ЭтоЦентр Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Отправка в центр подтверждения о приеме данных'"), УровеньЖурналаРегистрации.Информация, , ,);
			Package = ПланыОбмена.ГлавныйУзел();
			Результат = ОбменДаннымиРБДСервер.ВыгрузкаДанных(Package);
		КонецЕсли;	
		РегистрыСведений.СостояниеОбменовДанными.ДобавитьЗапись(СтруктураЗаписи);
	Исключение
		Если ЧтениеСообщения <> Неопределено Тогда
			СтруктураЗаписи = Новый Структура;
			СтруктураЗаписи.Вставить("УзелИнформационнойБазы",    ЧтениеСообщения.Отправитель);
			СтруктураЗаписи.Вставить("РезультатВыполненияОбмена", Перечисления.РезультатыВыполненияОбмена.Ошибка);
			СтруктураЗаписи.Вставить("ДатаНачала",                ТекущаяДата());
			СтруктураЗаписи.Вставить("ДатаОкончания",             ТекущаяДата());
			Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
				СтруктураЗаписи.Вставить("ДействиеПриОбмене",         Перечисления.ДействияПриОбмене.ВыгрузкаДанных);
			Иначе
				СтруктураЗаписи.Вставить("ДействиеПриОбмене",         Перечисления.ДействияПриОбмене.ЗагрузкаДанных);
			КонецЕсли;	
			ЧтениеСообщения.ПрерватьЧтение(); 
			ЧтениеXML.Закрыть();
		КонецЕсли;	
		Если КонфигурацияИзменена() Тогда
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Конфигурация изменена'"), УровеньЖурналаРегистрации.Информация, , ,);
			Константы.ХранилищеПакетаРБД.Установить(Новый ХранилищеЗначения(Package, Новый СжатиеДанных()));
			
			ОбменДаннымиРБДСервер.ОбновлениеКонфигурации();
			//ФоновыеЗадания.Выполнить("ОбменДаннымиРБДСервер.ОбновлениеКонфигурации");
			
			//СтруктураЗаписи.Вставить("ОписаниеОшибки","Ожидание обновления конфигурации.");
			//РегистрыСведений.СостояниеОбменовДанными.ДобавитьЗапись(СтруктураЗаписи);
			Результат = "ОжиданиеОбновления";
		Иначе	
			Package = ОбменДаннымиRIBСервер.ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке());
			Результат = Ложь;
			СтруктураЗаписи.Вставить("ОписаниеОшибки",Package);
			РегистрыСведений.СостояниеОбменовДанными.ДобавитьЗапись(СтруктураЗаписи);
		КонецЕсли;	
	КонецПопытки;
	Попытка
		УдалитьФайлы(стрПутьФайла);
	Исключение
		ОписаниеОшибки = ОбменДаннымиRIBСервер.ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("Обмен данными", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки);
	КонецПопытки;	
	Возврат Результат; 
КонецФункции

