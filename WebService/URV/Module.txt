
Функция ПолучитьПриказы(МесяцПриказа, КодСтруктурнойЕдиницы, СообщениеОшибки = "", Результат = "")
			
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	|	Izh_ПриказОРаспределенииНадбавок.Ссылка КАК ДокументПриказ,
	|	Izh_ПриказОРаспределенииНадбавок.ПериодПерераспределения,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.КодПодразделения,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.КодСотрудника,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.ФИОСотрудника,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.Подразделение,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.Должность,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.ВидЗанятости,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.ВидРаспределения,
	|	ВЫБОР
	|		КОГДА Izh_ПриказОРаспределенииНадбавокРаспределение.ВидРаспределения = ЗНАЧЕНИЕ(Перечисление.Izh_ВидыПерераспределенияНадбавок.Лишение)
	|			ТОГДА 1
	|		КОГДА Izh_ПриказОРаспределенииНадбавокРаспределение.ВидРаспределения = ЗНАЧЕНИЕ(Перечисление.Izh_ВидыПерераспределенияНадбавок.Перераспределение)
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВидРаспределенияЧислом,
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.ПроцентРаспределения
	|ИЗ
	|	Документ.Izh_ПриказОРаспределенииНадбавок.Распределение КАК Izh_ПриказОРаспределенииНадбавокРаспределение
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Izh_ПриказОРаспределенииНадбавок КАК Izh_ПриказОРаспределенииНадбавок
	|		ПО Izh_ПриказОРаспределенииНадбавокРаспределение.Ссылка = Izh_ПриказОРаспределенииНадбавок.Ссылка
	|ГДЕ
	|	Izh_ПриказОРаспределенииНадбавокРаспределение.КодПодразделения = &КодПодразделения
	|	И Izh_ПриказОРаспределенииНадбавок.ПериодПерераспределения = &ПериодПерераспределения
	|	И Izh_ПриказОРаспределенииНадбавок.Ссылка.ПометкаУдаления = ЛОЖЬ";
	//Запрос.УстановитьПараметр("КодПодразделения"		, ?(КодСтруктурнойЕдиницы = "00", "с", "м") + КодСтруктурнойЕдиницы);
	Запрос.УстановитьПараметр("КодПодразделения"		, КодСтруктурнойЕдиницы);
	Запрос.УстановитьПараметр("ПериодПерераспределения"	, НачалоМесяца(МесяцПриказа));
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СообщениеОшибки = "Не найдено приказов на перераспределение за " + Формат(МесяцПриказа, "ДФ='ММММ гггг'");
		Возврат Ложь;
	КонецЕсли;
		
	Результат = XMLСтрока(Новый ХранилищеЗначения(РезультатЗапроса.Выгрузить(), Новый СжатиеДанных(9)));
	
	Возврат Истина;	
	
КонецФункции

Функция DataExchange(Package)
	
	Попытка
		Если ПараметрыСеанса.Отладка Тогда
			ЗаписьЖурналаРегистрации("WebService - ЗУП", УровеньЖурналаРегистрации.Информация,,,Package);
		КонецЕсли;	
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(Package);
		ЧтениеXML.ПервоеОбъявление();
		ЧтениеXML.Прочитать();
		ИмяТипаПакета	= ЧтениеXML.Имя;		
		ТипПакета		= ФабрикаXDTO.Тип(ЧтениеXML.URIПространстваИмен, ИмяТипаПакета);		
		
		XDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ТипПакета);
		
		ОбъектОбработан = Ложь;
		
		Если ИмяТипаПакета = "DocumentObject.ТабельУчетаРабочегоВремени" Тогда
			ОбъектОбработан = ОбменДаннымиУРВСервер.ЗагрузитьDocumentObjectТабельУчетаРабочегоВремени(XDTO);			
		ИначеЕсли ИмяТипаПакета = "DocumentObject.ПриказОРаспределенииНадбавок" Тогда		
			ОбъектОбработан = ОбменДаннымиУРВСервер.ЗагрузитьDocumentObjectПриказОРаспределенииНадбавок(XDTO);
		ИначеЕсли ИмяТипаПакета = "CatalogObject.Должности" Тогда		
			ОбъектОбработан = ОбменДаннымиУРВСервер.ЗагрузитьCatalogObjectДолжности(XDTO);
		ИначеЕсли ИмяТипаПакета = "CatalogObject.КлассификаторИспользованияРабочегоВремени" Тогда		
			ОбъектОбработан = ОбменДаннымиУРВСервер.ЗагрузитьCatalogObjectКлассификаторИспользованияРабочегоВремени(XDTO);			
		КонецЕсли;
				
		Результат	= ОбъектОбработан;
	Исключение
		Package		= ОбменДаннымиRIBСервер.ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке());
		Результат	= Ложь;
	КонецПопытки;
	
	Возврат Результат; 
	
КонецФункции

Функция ПолучитьТабелиЗаПериод(НачалоПериода, КонецПериода, КодСтруктурнойЕдиницы, Результат)
	
	ОбработкаВыгрузки = Обработки.Izh_ВыгрузкаТабелейУчетаРабочегоВремени.Создать();
	ОбработкаВыгрузки.НачалоПериода	= НачалоПериода;
	ОбработкаВыгрузки.КонецПериода	= КонецПериода;
	ОбработкаВыгрузки.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.НайтиПоРеквизиту("Наименование3", СокрЛП(КодСтруктурнойЕдиницы));
		
	МассивДокументов = ОбработкаВыгрузки.ПолучитьТабелиЗаПериод();
	
	Возврат ОбработкаВыгрузки.ПолучитьТекстВыгрузки(МассивДокументов, Результат);
	
КонецФункции
