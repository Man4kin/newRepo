
// Создать и заполнить дерево значений свойств для редактирования в форме объекта
Функция ЗаполнитьДеревоРеквизитов(Ссылка, Справочник, ПВХ, Все = Истина) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПВХДополнительныеРеквизиты.Ссылка КАК Свойство,
	               |	ДополнительныеРеквизиты.Значение,
	               |	ВЫБОР
	               |		КОГДА ПВХДополнительныеРеквизиты.ЭтоГруппа
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Картинка,
	               |	ПВХДополнительныеРеквизиты.ТипЗначения КАК ТипЗначенияСвойства
	               |ИЗ
	               |	ПланВидовХарактеристик."+ПВХ+" КАК ПВХДополнительныеРеквизиты";
				   Если Все Тогда
					Запрос.Текст = Запрос.Текст+"
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник."+Справочник+".ДополнительныеРеквизиты КАК ДополнительныеРеквизиты";
				   Иначе
					Запрос.Текст = Запрос.Текст+"
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник."+Справочник+".ДополнительныеРеквизиты КАК ДополнительныеРеквизиты";
				   КонецЕсли;
					Запрос.Текст = Запрос.Текст+"
				   |		ПО ПВХДополнительныеРеквизиты.Ссылка = ДополнительныеРеквизиты.Свойство
	               |			И (ДополнительныеРеквизиты.Ссылка = &Ссылка)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПВХДополнительныеРеквизиты.Ссылка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Возврат Дерево;
КонецФункции

// Из дерева значений свойств на форме заполнить табличную часть объекта значений свойств
Процедура ПеренестиЗначенияСвойств(ДополнительныеРеквизиты, ДеревоСвойств) Экспорт
	ДополнительныеРеквизиты.Очистить();
   	ЗаполнитьДополнительныеРеквизиты(ДополнительныеРеквизиты, ДеревоСвойств)
КонецПроцедуры

Процедура ЗаполнитьДополнительныеРеквизиты(ДополнительныеРеквизиты, ДеревоСвойств)
	Для Каждого Стр Из ДеревоСвойств.Строки Цикл
		Если Стр.Картинка = 1 Тогда
        	ЗаполнитьДополнительныеРеквизиты(ДополнительныеРеквизиты, Стр)
        Иначе
			Если ЗначениеЗаполнено(Стр.Значение) И (Стр.Значение <> Ложь) Тогда
				новСтр = ДополнительныеРеквизиты.Добавить();
				ЗаполнитьЗначенияСвойств(новСтр,Стр);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьСвязиПараметровВыбора(Элемент) Экспорт
	Элемент.ТекущийЭлемент.СвязиПараметровВыбора = Новый ФиксированнаяСтруктура("Владелец", "Объект.Ссылка");
КонецПроцедуры

Функция ЗаполнитьДеревоСертификатов(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПВХСертификаты.Ссылка КАК Сертификат,
	               |	ДанныеСертификатов.Значение,
	               |	ВЫБОР
	               |		КОГДА ПВХСертификаты.ЭтоГруппа
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Картинка,
	               |	ПВХСертификаты.ТипЗначения КАК ТипЗначенияСвойства,
	               |	ДанныеСертификатов.Значение.ДатаС КАК ДатаС,
	               |	ДанныеСертификатов.Значение.ДатаПо КАК ДатаПо
	               |ИЗ
	               |	ПланВидовХарактеристик.Сертификаты КАК ПВХСертификаты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеСертификатов КАК ДанныеСертификатов
	               |		ПО (ДанныеСертификатов.Номенклатура = &Номенклатура)
	               |			И ПВХСертификаты.Ссылка = ДанныеСертификатов.Сертификат
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПВХСертификаты.Ссылка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Номенклатура",Ссылка.Код);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Возврат Дерево;
КонецФункции

Процедура ПеренестиЗначенияСертификатов(Ссылка, ДеревоСертификатов) Экспорт
	НаборЗаписей = РегистрыСведений.ДанныеСертификатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Ссылка.Код);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	ЗаполнитьДанныеПоСертификатам(Ссылка, НаборЗаписей, ДеревоСертификатов);
	НаборЗаписей.Записать();
КонецПроцедуры

Процедура ЗаполнитьДанныеПоСертификатам(Ссылка, НаборЗаписей, ДеревоСертификатов)
	
	Для Каждого Стр Из ДеревоСертификатов.Строки Цикл
		Если Стр.Картинка = 1 Тогда
        	ЗаполнитьДанныеПоСертификатам(Ссылка, НаборЗаписей, Стр)
        Иначе
			новСтр = НаборЗаписей.Добавить();
			новСтр.Номенклатура = Ссылка.Код;
			ЗаполнитьЗначенияСвойств(новСтр,Стр);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ЗаполнитьДеревоНастроекБП(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПВХДополнительныеРеквизиты.Ссылка КАК Свойство,
	               |	ДополнительныеРеквизиты.Значение,
	               |	0 КАК Картинка,
	               |	ПВХДополнительныеРеквизиты.ТипЗначения КАК ТипЗначенияСвойства
	               |ИЗ
	               |	ПланВидовХарактеристик.ДополнительныеРеквизитыБП КАК ПВХДополнительныеРеквизиты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкаБизнесПроцессов.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	               |		ПО ПВХДополнительныеРеквизиты.Ссылка = ДополнительныеРеквизиты.Свойство
	               |			И (ДополнительныеРеквизиты.Ссылка = &Ссылка)
	               |ГДЕ
	               |	НЕ ПВХДополнительныеРеквизиты.ЭтоГруппа
	               |	И ПВХДополнительныеРеквизиты.Родитель.Наименование = &Наименование
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПВХДополнительныеРеквизиты.Ссылка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	Запрос.УстановитьПараметр("Наименование",Ссылка.Наименование);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Возврат Дерево;
КонецФункции
