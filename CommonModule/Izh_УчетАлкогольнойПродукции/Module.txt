
Процедура Izh_АвтоСозданиеПриложенияПоАлкоголюОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	// !!!! efim
	#Если _ Тогда
		Источник = Документы.ПередачаТоваровМеждуФирмами.СоздатьДокумент();
		Источник = Документы.КоррекцияЗапасов.СоздатьДокумент();
	#КонецЕсли
	// !!!!
		
	УчетАлкоголя = УчетАлкогольнойПродукции.ВестиУчетАлкоголя();
	Если НЕ УчетАлкоголя Тогда
		Возврат;
	КонецЕсли;
	
	Приложения = Неопределено;
	НовыеПриложения = Новый Массив();
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПередачаТоваровМеждуФирмами") Тогда
		
		ПриложениеОснования = СтарыеПриложения(Источник.ДокОснование);
		Если ПриложениеОснования.Следующий() И ПриложениеОснования.Проведен Тогда
			
			ПриложениеРасход = НовоеПриложение(Приложения, Источник);
			ПриложениеПриход = НовоеПриложение(Приложения, Источник);
			
			Попытка
				ПриложениеРасход.Заполнить(ПриложениеОснования.Ссылка);
				ПриложениеПриход.Заполнить(ПриложениеОснования.Ссылка);
			Исключение
				Возврат;
			КонецПопытки;
			
			ПриложениеРасход.УстановитьКодОперации(Источник.Ссылка,, Ложь);
			
			пр = ПриложениеРасход;
			пр.Фирма = Источник.Фирма;
			
			ПриложениеПриход.УстановитьКодОперации(Источник.Ссылка,, Истина);
			
			пп = ПриложениеПриход;
			пп.Фирма = Источник.ФирмаПолучатель;
			
			НовыеПриложения.Добавить(ПриложениеРасход);
			НовыеПриложения.Добавить(ПриложениеПриход);
			
		КонецЕсли;
		
	Иначе
		
		Создавать = Истина;
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.КоррекцияЗапасов") И Источник.Оприходование Тогда
			Создавать = Ложь;
		КонецЕсли;
		
		Если Создавать Тогда
			
			Приложение = НовоеПриложение(Приложения, Источник);
			
			Попытка
				Приложение.Заполнить(Источник.Ссылка);
			Исключение
				Возврат;
			КонецПопытки;
			
			НовыеПриложения.Добавить(Приложение);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого Приложение Из НовыеПриложения Цикл
		
		Приложение.ДокОснование = Источник.Ссылка;     //dps+ 15/07/20 133752
		
		// !!!! efim
		#Если _ Тогда
			Приложение = Документы.ПриложениеПоАлкоголю.СоздатьДокумент();
		#КонецЕсли
		// !!!!
		
		Если Приложение.Состав.Количество() = 0 Тогда
			УдалитьПриложение(Приложения, Приложение);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаписатьПриложения(Приложения, Источник);
	
КонецПроцедуры

Функция НовоеПриложение(Приложения = Неопределено, Источник)
	
	Если Приложения = Неопределено Тогда
		Приложения = СтарыеПриложения(Источник.Ссылка, Ложь);
	КонецЕсли;
	
	Указатель = Приложения.Указатель;
	Если Указатель > 0 Тогда
		
		Указатель = Указатель - 1;
		Приложения.Указатель = Указатель;
		
		Приложение = Приложения.Таблица[Указатель];
		НовыйДокумент = Приложение.Ссылка.ПолучитьОбъект();
		
	Иначе
		
		Приложение = Приложения.Таблица.Добавить();
		НовыйДокумент = Документы.ПриложениеПоАлкоголю.СоздатьДокумент();
		
	КонецЕсли;
	
	Приложение.Объект = НовыйДокумент;
	
	НовыйДокумент.Дата = Источник.Дата;
	НовыйДокумент.СозданАвтоматически = Истина;
	НовыйДокумент.ПометкаУдаления = Ложь;
	
	Возврат НовыйДокумент;
	
КонецФункции

Процедура УдалитьПриложение(Приложения, Приложение)
	
	Если Приложения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Приложение = Приложения.Таблица.Найти(Приложение, "Объект");
	Если Приложение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Приложение.Объект = Неопределено;
	Приложение = Неопределено;
	
КонецПроцедуры

Процедура ЗаписатьПриложения(Приложения, Источник)
	
	Если Приложения = Неопределено Тогда
		Приложения = СтарыеПриложения(Источник.Ссылка, Ложь);
	КонецЕсли;
	
	Для каждого Приложение Из Приложения.Таблица Цикл
		Объект = Приложение.Объект;
		Если Объект <> Неопределено Тогда
			Объект.Записать(РежимЗаписиДокумента.Проведение);
		ИначеЕсли ЗначениеЗаполнено(Приложение.Ссылка) И НЕ Приложение.ПометкаУдаления Тогда
			Приложение.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
		КонецЕсли;
	КонецЦикла;
	
	Приложения = Неопределено;
	
КонецПроцедуры

Функция СтарыеПриложения(Знач ДокументОснование, Знач Выборка = Истина)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокОснование", ДокументОснование);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПриложениеПоАлкоголю.Ссылка,
		|	ПриложениеПоАлкоголю.ПометкаУдаления,
		|	ПриложениеПоАлкоголю.Проведен
		|ИЗ
		|	Документ.ПриложениеПоАлкоголю КАК ПриложениеПоАлкоголю
		|ГДЕ
		|	ПриложениеПоАлкоголю.ДокОснование = &ДокОснование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПриложениеПоАлкоголю.Проведен УБЫВ";
		
	Если Выборка Тогда
		
		Приложения = Запрос.Выполнить().Выбрать();
		
	Иначе
		
		Приложения = Запрос.Выполнить().Выгрузить();
		Приложения.Сортировать("Проведен");
		
		Izh_ОбработкаКоллекций.ДобавитьКолонки(Приложения, "Объект");
		
		Приложения = Новый Структура(
			"Указатель, Таблица",
			Приложения.Количество(), Приложения
		);
		
	КонецЕсли;
	
	Возврат Приложения;
	
КонецФункции


Процедура ВыгрузкаВЦВД(Приложение, ДляВыгрузки) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		Приложение = Документы.ПриложениеПоАлкоголю.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Выгружать = Истина;
	
	// BiT ALProzorov 02/09/2015 -->>
	// Приложение по алкоголю выгружаем в обе стороны
	//Выгружать = Выгружать И ПараметрыСеанса.ЭтоЦентр;
	// BiT ALProzorov 02/09/2015 <<--
	Выгружать = Выгружать 
		И ТипЗнч(Приложение) = Тип("ДокументОбъект.ПриложениеПоАлкоголю")
		И Приложение.Проведен;
	
	Если Выгружать Тогда
		Izh_ОбменДаннымиRIB.ВыгружатьОбъект(Приложение, ДляВыгрузки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузкаВRIB(Объект, ДляВыгрузки) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		Объект = Документы.ПриложениеПоАлкоголю.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Выгружать = Истина;
	
	Выгружать = Выгружать И НЕ ПараметрыСеанса.ЭтоЦентр;
	
	Выгружать = Выгружать 
		И ТипЗнч(Объект) = Тип("ДокументОбъект.ПриложениеПоАлкоголю")
		И Объект.Проведен
		И ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.СтруктурныеЕдиницы");
		
	Если НЕ Выгружать Тогда
		Возврат;
	КонецЕсли;
	
	ТрансляцияОснования = Новый Соответствие();
	то = ТрансляцияОснования;
	то.Вставить(Тип("ДокументСсылка.РасходнаяНакладная"), Документы.ПриходнаяНакладная);
	то.Вставить(Тип("ДокументСсылка.КорректировкаРеализации"), Документы.КорректировкаПоступления);
	
	МенеджерВПриемнике = ТрансляцияОснования[ТипЗнч(Объект.ДокОснование)];
	Если МенеджерВПриемнике = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Приложение = Объект.Скопировать();
	ЗаполнитьЗначенияСвойств(Приложение, Объект);
	Приложение.УстановитьСсылкуНового(
		Документы.ПриложениеПоАлкоголю.ПолучитьСсылку(Объект.Ссылка.УникальныйИдентификатор())
	);
	
	// !!!! efim
	#Если _ Тогда
		МенеджерВПриемнике = Документы.ПриходнаяНакладная;
	#КонецЕсли
	// !!!!
	
	Приложение.ДокОснование = МенеджерВПриемнике.ПолучитьСсылку(
		Приложение.ДокОснование.УникальныйИдентификатор()
	);
	
	Приложение.СтруктурнаяЕдиница = Приложение.Контрагент;
	Приложение.УстановитьКодОперации(,, Истина);
	
	Приложение.ДополнительныеСвойства.Вставить("КодЛокации", Приложение.Контрагент.Код);
	
	Izh_ОбменДаннымиRIB.ВыгружатьОбъект(Приложение, ДляВыгрузки);
	
КонецПроцедуры
