Процедура СформироватьДокументыГотоваяПродукция(Объект,Режим,Отказ) Экспорт
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ZОтчет") Тогда
		ИмяТаблицыДокумента = "ZОтчет.Состав";
		ИмяРеквизитаСклад = "СоставДокумента.Склад";
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.Комплектация") Тогда
		ИмяТаблицыДокумента = "Комплектация.Состав";
		ИмяРеквизитаСклад = "СоставДокумента.Ссылка.Склад";
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.КоррекцияЗапасов") Тогда
		ИмяТаблицыДокумента = "КоррекцияЗапасов.Состав";
		ИмяРеквизитаСклад = "СоставДокумента.Ссылка.Склад";
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		ИмяТаблицыДокумента = "РасходнаяНакладная.Состав";
		ИмяРеквизитаСклад = "СоставДокумента.Ссылка.Склад";
	Иначе
		Возврат;
	КонецЕсли;
	
	флВыпускПродукцииПоПлановымЦенам = ОбщегоНазначенияСервер.ПолучитьУчетнуюПолитикуФирмы(Объект.Фирма,Объект.Дата).ВыпускПродукцииПоПлановымЦенам;
	Момент = ПроведениеДокументов.ПолучитьМоментВремени(Объект.Ссылка,Объект.Дата,Режим);
	
	//получим список уже созданных документов ГотоваяПродукция
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГотоваяПродукция.Ссылка
	|ИЗ
	|	Документ.ГотоваяПродукция КАК ГотоваяПродукция
	|ГДЕ
	|	ГотоваяПродукция.ДокОснование = &ДокОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ГотоваяПродукция.Ссылка");
	Запрос.УстановитьПараметр("ДокОснование",Объект.Ссылка);
	тзГотоваяПродукция = Запрос.Выполнить().Выгрузить();
	
	Если тзГотоваяПродукция.Количество() > 0 Тогда
		ТолькоПросмотрДокумента = РаботаСФормамиСервер.ЗапрещеноРедактировать(тзГотоваяПродукция[0].Ссылка);
		Если ТолькоПросмотрДокумента Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Для каждого СтрокаТзГотоваяПродукция из тзГотоваяПродукция Цикл
		ТолькоПросмотрДокумента = РаботаСФормамиСервер.ЗапрещеноРедактировать(СтрокаТзГотоваяПродукция.Ссылка);
		ДокументОбъект = СтрокаТзГотоваяПродукция.Ссылка.ПолучитьОбъект();
		Если ТолькоПросмотрДокумента Тогда
			СпособЗаписиДокумента = РежимЗаписиДокумента.Проведение;
		Иначе 
			СпособЗаписиДокумента = РежимЗаписиДокумента.ОтменаПроведения;
		КонецЕсли;
		ДокументОбъект.Записать(СпособЗаписиДокумента);
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивНоменклатуры",Объект.Состав.ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("ТекущаяСтруктурнаяЕдиница",ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Склады.Ссылка,
	|	Склады.Автопроизводство
	|ПОМЕСТИТЬ Склады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Автопроизводство = ИСТИНА
	|	И Склады.Владелец В(&ТекущаяСтруктурнаяЕдиница)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТехнологическаяКарта.Ссылка,
	|	ТехнологическаяКарта.ВидПроизводства
	|ПОМЕСТИТЬ ТехнологическиеКарты
	|ИЗ
	|	Документ.ТехнологическаяКарта КАК ТехнологическаяКарта
	|ГДЕ
	|	ТехнологическаяКарта.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Приготовление)
	|	И ТехнологическаяКарта.Продукция В(&МассивНоменклатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПродукцияПоЦехам.Склад) КАК Склад,
	|	ПродукцияПоЦехам.Продукция
	|ПОМЕСТИТЬ ПродукцияПоЦехам
	|ИЗ
	|	РегистрСведений.ПродукцияПоЦехам КАК ПродукцияПоЦехам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Склады КАК Склады
	|		ПО ПродукцияПоЦехам.Склад = Склады.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТехнологическиеКарты КАК ТехнологическиеКарты
	|		ПО ПродукцияПоЦехам.ТехнологическаяКарта = ТехнологическиеКарты.Ссылка
	|ГДЕ
	|	ПродукцияПоЦехам.ФлагАктивности = ИСТИНА
	|	И ПродукцияПоЦехам.Продукция В(&МассивНоменклатуры)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродукцияПоЦехам.Продукция
	|;";

	Если флВыпускПродукцииПоПлановымЦенам Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ПлановаяСебестоимостьПродукции.Продукция КАК Номенклатура,
		|	ПлановаяСебестоимостьПродукции.Себестоимость КАК Цена
		|ПОМЕСТИТЬ ПлановыеЦены
		|ИЗ
		|	РегистрСведений.ПлановаяСебестоимостьПродукции.СрезПоследних(&МоментВремени, ) КАК ПлановаяСебестоимостьПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродукцияПоЦехам КАК ПродукцияПоЦехам
		|		ПО ПлановаяСебестоимостьПродукции.Продукция = ПродукцияПоЦехам.Продукция
		|;";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	СоставДокумента.Номенклатура,
	|	ПродукцияПоЦехам.Склад КАК СкладПроизводства,
	|	"+ИмяРеквизитаСклад+" КАК Склад,
	|	СоставДокумента.Количество * СоставДокумента.Коэффициент КАК Количество,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	"+?(флВыпускПродукцииПоПлановымЦенам," ПлановыеЦены.Цена "," 0 ")+" КАК ПлановаяЦена
	|ИЗ
	|	Документ."+ИмяТаблицыДокумента+" КАК СоставДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродукцияПоЦехам КАК ПродукцияПоЦехам
	|		ПО СоставДокумента.Номенклатура = ПродукцияПоЦехам.Продукция
	|	"+?(флВыпускПродукцииПоПлановымЦенам," ЛЕВОЕ СОЕДИНЕНИЕ ПлановыеЦены КАК ПлановыеЦены
	|		ПО СоставДокумента.Номенклатура = ПлановыеЦены.Номенклатура ","")+"
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&МоментВремени,
	|				МестоХранения В
	|						(ВЫБРАТЬ
	|						  "+ИмяРеквизитаСклад+"
	|						ИЗ
	|							Документ."+ИмяТаблицыДокумента+" КАК СоставДокумента
	|						ГДЕ
	|							СоставДокумента.Ссылка = &Ссылка
	|						СГРУППИРОВАТЬ ПО
	|							"+ИмяРеквизитаСклад+")
	|					И Номенклатура В
	|						(ВЫБРАТЬ
	|							ПродукцияПоЦехам1.Продукция
	|						ИЗ
	|							ПродукцияПоЦехам КАК ПродукцияПоЦехам1)
	|					И Фирма = &Фирма) КАК ОстаткиНоменклатурыОстатки
	|		ПО СоставДокумента.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|ГДЕ
	|	СоставДокумента.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	СкладПроизводства,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПродукцияПоЦехам
	|;";
	
	Если флВыпускПродукцииПоПлановымЦенам Тогда
	    ТекстЗапроса = ТекстЗапроса + " УНИЧТОЖИТЬ ПлановыеЦены ";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	Запрос.УстановитьПараметр("Фирма",Объект.Фирма);
	Запрос.УстановитьПараметр("МоментВремени",Момент);
	ВыборкаСкладПроизводства = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСкладПроизводства.Следующий() Цикл
		ВыборкаСклад = 	ВыборкаСкладПроизводства.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСклад.Следующий() Цикл
			
			//получим документ ГотоваяПродукция из таблицы уже ранее введенных
			Если тзГотоваяПродукция.Количество()>0 Тогда
				ДокументОбъект = тзГотоваяПродукция[0].Ссылка.ПолучитьОбъект();
			Иначе	
				ДокументОбъект = Документы.ГотоваяПродукция.СоздатьДокумент();
			КонецЕсли;
			ДокументОбъект.Автор = ПараметрыСеанса.ТекущийПользователь;
			ДокументОбъект.Фирма              = Объект.Фирма;
			ДокументОбъект.Дата               = Объект.Дата-1;
			ДокументОбъект.ВидПроизводства    = Перечисления.ВидыПроизводства.Приготовление;
			ДокументОбъект.ДокОснование       = Объект.Ссылка;
			ДокументОбъект.Склад              = ВыборкаСклад.Склад;
			ДокументОбъект.СкладПроизводства  = ВыборкаСклад.СкладПроизводства;
			ДокументОбъект.СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиница;
			ДокументОбъект.ПометкаУдаления    = Ложь;
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("НеМенятьВремя", ИСТИНА);

			ДокументОбъект.Продукция.Очистить();
			ДокументОбъект.Состав.Очистить();
			
			Выборка = ВыборкаСклад.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				чКолАвтопроизводства = Выборка.Количество - Выборка.КоличествоОстаток;
				Если чКолАвтопроизводства > 0 Тогда
					НоваяСтрокаПродукция = ДокументОбъект.Продукция.Добавить();
					НоваяСтрокаПродукция.Номенклатура      = Выборка.Номенклатура;
					НоваяСтрокаПродукция.ЕдиницаИзмерения  = РаботаСФормамиСервер.ПолучитьБазовуюЕдиницуИзмерения(Выборка.Номенклатура);
					НоваяСтрокаПродукция.Коэффициент       = 1;
					НоваяСтрокаПродукция.Ключ              = Строка(Новый УникальныйИдентификатор());
					НоваяСтрокаПродукция.Количество        = чКолАвтопроизводства;
					НоваяСтрокаПродукция.ПоТехКарте        = истина;
                    Если флВыпускПродукцииПоПлановымЦенам Тогда
						НоваяСтрокаПродукция.ПлановаяСтоимость = Выборка.ПлановаяЦена;
						НоваяСтрокаПродукция.СуммаПлановая     = Окр(Выборка.ПлановаяЦена * чКолАвтопроизводства,2);
					КонецЕсли;
					ДокументОбъект.ЗаполнитьСостав(НоваяСтрокаПродукция.Номенклатура,НоваяСтрокаПродукция.Количество,НоваяСтрокаПродукция.Коэффициент,НоваяСтрокаПродукция.ТехнологическаяКарта,НоваяСтрокаПродукция.ПоТехКарте,НоваяСтрокаПродукция.Ключ,флВыпускПродукцииПоПлановымЦенам);
				КонецЕсли;
			КонецЦикла;
			
			Если ДокументОбъект.Продукция.Количество() > 0 Тогда
				
				Если ДокументОбъект.Состав.Количество() = 0 Тогда
					Сообщить( "При автопроизводстве номенклатуры """ + Строка(НоваяСтрокаПродукция.Номенклатура) + """ не удалось определить состав ингридиентов.");
					Отказ = Истина;
					Возврат;
				КонецЕсли;
				
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);
				Исключение
					Сообщить(ОписаниеОшибки());
					Отказ = Истина;
					Возврат;
				КонецПопытки;
				Если тзГотоваяПродукция.Количество() > 0 Тогда
					//удаляем в таблице строку с документом, чтобы в таблице остались только не используемые документы, которые потом пометим на удаление
					тзГотоваяПродукция.Удалить(тзГотоваяПродукция[0]);
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого СтрокаТзГотоваяПродукция Из тзГотоваяПродукция Цикл
		ДокументОбъект = СтрокаТзГотоваяПродукция.Ссылка.ПолучитьОбъект();
		Попытка 
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
		Исключение
			Сообщить(ОписаниеОшибки());
			Отказ = Истина;
			Возврат;
		КонецПопытки;	
	КонецЦикла;	
	
	Отказ = Ложь;
	
КонецПроцедуры	

