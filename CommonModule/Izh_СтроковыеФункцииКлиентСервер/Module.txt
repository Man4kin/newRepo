
Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Ложь, Знач УдалитьЛишниеПробелы = Истина) Экспорт
	
	Результат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Строка, Разделитель, ПропускатьПустыеСтроки);
	
	Если УдалитьЛишниеПробелы Тогда
		Для Сч = 0 По Результат.Количество() - 1 Цикл
			Результат[Сч] = СокрЛП(Результат[Сч]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив типов, сформированный из аргументов
//
// Параметры:
//  ИменаТипов	 - Строка - Имена типов, через запятую
//  ИмяМенеджера - Строка - Имя ссылочного менеджера, например ДокументСсылка или СправочникСсылка
// Возвращаемое значение:
//  Массив - Массив типов
Функция МассивТипов(Знач ИменаТипов, Знач ИмяМенеджера) Экспорт 
	
	Если Прав(ИмяМенеджера, 1) <> "." Тогда
		ИмяМенеджера = ИмяМенеджера + ".";
	КонецЕсли;
	
	Если ТипЗнч(ИменаТипов) = Тип("Строка") Тогда
		ИменаТипов = РазложитьСтрокуВМассивПодстрок(ИменаТипов);
	КонецЕсли;
	
	Результат = Новый Массив();
	Для каждого Имя Из ИменаТипов Цикл
		Результат.Добавить(
			Тип(ИмяМенеджера + Имя)
		);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПодставитьПараметрыВСтрокуИзСтруктуры(Знач СтрокаПодстановки, Знач СтруктураПараметров) Экспорт 
	
	СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%%", Символ(0));
	
	Для каждого Параметр Из СтруктураПараметров Цикл
		
		ЗначениеПараметра = Параметр.Значение;
		Если ЗначениеПараметра = Неопределено Тогда
			ЗначениеПараметра = "";
		Иначе
			ЗначениеПараметра = Строка(ЗначениеПараметра);
		КонецЕсли;
		
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%" + Параметр.Ключ + "%", ЗначениеПараметра);
		
	КонецЦикла;
	
	СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, Символ(0), "%");
	
	Возврат СтрокаПодстановки;
	
КонецФункции


Функция ДополнитьСтрокуПробелами(Знач ВходящаяСтрока,Знач ДлинаСтроки) Экспорт
	ДлинаВходящейСтроки = СтрДлина(ВходящаяСтрока);
	Результат = ВходящаяСтрока;
	Если ДлинаВходящейСтроки>ДлинаСтроки Тогда
		Результат = Лев(ВходящаяСтрока,ДлинаСтроки);
	ИначеЕсли ДлинаВходящейСтроки<ДлинаСтроки Тогда
		Разница = 	ДлинаСтроки-ДлинаВходящейСтроки;
		Для ъ=1 по Разница Цикл
			Результат = Результат + " ";	
		КонецЦикла;
	КонецЕсли;
	Возврат Результат
КонецФункции
