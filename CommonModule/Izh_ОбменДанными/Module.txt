
Функция ЗапрещеноИзменение(Знач Объект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = РегистрыСведений.Izh_ОтправленныеОбъекты.ОбъектОтправлен(Объект);
	
	Возврат Результат;
	
КонецФункции

Процедура ПометитьКакОтправленный(Знач Объект) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.Izh_ОтправленныеОбъекты.ДобавитьОбъект(Объект);
	
КонецПроцедуры

Функция ВсеУзлыПланаОбмена(Знач ПланОбмена) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		ПланОбмена = ПланыОбмена.ОбменЦВД;
	#КонецЕсли
	// !!!!
	
	СписокУзлов = Новый Массив();
	
	Узел = ПланОбмена.Выбрать();
	ЭтотУзел = ПланОбмена.ЭтотУзел();
	
	Пока Узел.Следующий() Цикл
		Если Узел.Ссылка <> ЭтотУзел Тогда
			СписокУзлов.Добавить(Узел.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокУзлов;
	
КонецФункции

Процедура ДобавитьВоВсеУзлыПланаОбмена(Знач Объект, Знач ПланОбмена)
	
	Попытка
		Для каждого Узел Из ВсеУзлыПланаОбмена(ПланОбмена) Цикл
			Объект.ОбменДанными.Получатели.Добавить(Узел);
		КонецЦикла;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура УдалитьИзмененияВоВсехУзлахПланаОбмена(Знач Объект, Знач ПланОбмена)
	
	Попытка
		ПланыОбмена.УдалитьРегистрациюИзменений(
			ВсеУзлыПланаОбмена(ПланОбмена),
			Объект
		);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьОбменДаннымиИзОснования(Объект, Знач Источник) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		Объект = Документы.Izh_DESADV.СоздатьДокумент();
		Источник = Объект;
	#КонецЕсли
	// !!!!
	
	Объект.ОбменДанными.Загрузка = Источник.ОбменДанными.Загрузка;
	
	Попытка
		Объект.ОбменДанными.Отправитель = Источник.ОбменДанными.Отправитель;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура УстановитьОбменДаннымиЗагрузка(Объект, Знач Значение = Истина, Знач УзелОтправитель = Неопределено) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		Объект = Документы.Izh_DESADV.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Объект.ОбменДанными.Загрузка = Значение;
	
	Если ЗначениеЗаполнено(УзелОтправитель) Тогда
		Попытка
			Объект.ОбменДанными.Отправитель = УзелОтправитель;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьДвиженияДокумента(Документ) 
	
	// !!!! efim
	#Если _ Тогда
		Документ = Документы.Izh_DESADV.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Для каждого НаборДвижений Из Документ.Движения Цикл
		НаборДвижений.Очистить();
		УстановитьОбменДаннымиЗагрузка(НаборДвижений);
		НаборДвижений.Записать();
		УстановитьОбменДаннымиЗагрузка(НаборДвижений, Ложь);
	КонецЦикла;
	
	Для каждого Последовательность Из Документ.ПринадлежностьПоследовательностям Цикл
		Последовательность.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтменитьПроведениеДокумента(Документ) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		Документ = Документы.Izh_DESADV.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	УстановитьОбменДаннымиЗагрузка(Документ);
	
	НачатьТранзакцию();
	
	Попытка
		
		УдалитьДвиженияДокумента(Документ);
		
		Документ.Проведен = Ложь;
		Документ.Записать();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

Процедура НеРегистрироватьВЦВД(Объект, Знач Загрузка = Истина) Экспорт 
	
	// !!!! efim
	#Если _ Тогда
		Объект = Документы.Izh_DESADV.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	УзелОтправитель = ПланыОбмена.ОбменЦВД.НайтиПоКоду(1);
	
	УстановитьОбменДаннымиЗагрузка(Объект, Загрузка, УзелОтправитель);
	
КонецПроцедуры

Процедура НеРегистрироватьВRIB(Объект, Знач Загрузка = Истина) Экспорт 
	
	УзелОтправитель = ПланыОбмена.ОбменRIB.НайтиПоКоду(1);
	
	УстановитьОбменДаннымиЗагрузка(Объект, Загрузка, УзелОтправитель);
	
КонецПроцедуры

Процедура ЗарегистрироватьВЦВД(Источник)
	
	// !!!! efim
	#Если _ Тогда
		Источник = Документы.ПриходнаяНакладная.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Если ПараметрыСеанса.ИспользованиеЦВД Тогда
		
		//dps+ 15.08.21 136110 при формировании регистрации с\ф регистрировать для обмена ПН
		Если ТипЗнч(Источник) = Тип("ДокументСсылка.РегистрацияСчетФактуры") Тогда  
			Источник = ОпеределитьИсходнуюПН(Источник);
		КонецЕсли;
		//dps- 15.08.21 136110 при формировании регистрации с\ф регистрировать для обмена ПН
		
		ДобавитьВоВсеУзлыПланаОбмена(Источник, ПланыОбмена.ОбменЦВД);
		
	КонецЕсли;
	
КонецПроцедуры

//dps+ 15.08.21 136110 при формировании регистрации с\ф регистрировать для обмена ПН
//
// Параметры:
//  Док	 - Документ Регистрация Счета-фактуры.
// Возвращаемое значение:
//  Возвращает ссылку на исходную приходную накладную, если такой нет, что странно, то возвращает саму регистрацию СФ. 
Функция ОпеределитьИсходнуюПН(Док)
	#Если _ Тогда
		Док = Документы.РегистрацияСчетФактуры.СоздатьДокумент();
	#КонецЕсли
	ТекДок = Док;
	Пока Истина Цикл
		Если ТипЗнч(ТекДок.ДокументОснование) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда 
			ТекДок = ТекДок.ДокументОснование;
		ИначеЕсли ТипЗнч(ТекДок.ДокументОснование) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда  
			Возврат ТекДок.ДокументОснование;
		Иначе 
			//о_О
			Возврат Док;
		КонецЕсли;
	КонецЦикла;
КонецФункции
//dps- 15.08.21 136110 при формировании регистрации с\ф регистрировать для обмена ПН

Процедура ЗарегистрироватьВRIB(Источник)
	
	// !!!! efim
	#Если _ Тогда
		Источник = Документы.ПриходнаяНакладная.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Если НЕ Метаданные.ПланыОбмена.ОбменRIB.Состав.Содержит(Источник.Метаданные())
		ИЛИ ОбменДаннымиОбщийСервер.RIBИсключитьОбъект(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьВоВсеУзлыПланаОбмена(Источник, ПланыОбмена.ОбменRIB);
	
КонецПроцедуры

Процедура Izh_РегистрацияОбменЦВДПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	// !!!! efim
	#Если _ Тогда
		Источник = Документы.ПриходнаяНакладная.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	//Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения 
	//	ИЛИ Источник.ПометкаУдаления <> Источник.Ссылка.ПометкаУдаления Тогда
	//	УдалитьИзмененияВоВсехУзлахПланаОбмена(Источник, ПланыОбмена.ОбменЦВД);
	//КонецЕсли;
	
	Регистрировать = Ложь;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Регистрировать = Истина;
		
		//++БИТ БВО 2015-11-02 Расходные накладные теперь тоже участвуют в обмене ЦВД
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.РасходнаяНакладная") 
			И НЕ Источник.Возврат Тогда		
			
			Регистрировать = Ложь;
		КонецЕсли;
		//--БИТ БВО 2015-11-02
	КонецЕсли;
	
	Если Регистрировать Тогда
		ЗарегистрироватьВЦВД(Источник);
	КонецЕсли;
		
КонецПроцедуры

Процедура Izh_РегистрацияОбменаRIBПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	// !!!! efim
	#Если _ Тогда
		Источник = Документы.ПриходнаяНакладная.СоздатьДокумент();
	#КонецЕсли
	// !!!!
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения 
		ИЛИ Источник.ПометкаУдаления <> Источник.Ссылка.ПометкаУдаления Тогда
		УдалитьИзмененияВоВсехУзлахПланаОбмена(Источник, ПланыОбмена.ОбменRIB);
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ЗарегистрироватьВRIB(Источник);
	КонецЕсли;
	
КонецПроцедуры


Процедура Izh_ЗапретИзмененияОбъектОтправленОбработкаПроверкиЗаполнения(Источник, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Отказ = ЗапрещеноИзменение(Источник);
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Запрещено изменение! Объект уже отправлен в другие системы!'"),
			Источник
		);
	КонецЕсли;
	
КонецПроцедуры
