
// Обработчик ЗаписатьГруппыИВидыДоступа события ПриЗаписи подписки на события
// "ЗаписатьГруппыИВидыДоступа" вызывает метод записи групп доступа и видов доступа
// в РегистрСведений.ГруппыЗначенийДоступа, для требуемых объектов метаданных.
//
Процедура ЗаписатьГруппыИВидыДоступа(Знач Объект, Отказ) Экспорт
	
	Если Объект.ОбменДанными.Загрузка И НЕ Объект.ДополнительныеСвойства.Свойство("ЗаписатьГруппыИВидыДоступа") Тогда
		Возврат;
	КонецЕсли;
	
	#Если Сервер Тогда
		УправлениеДоступом.ЗаписатьГруппыИВидыДоступа(Объект, Отказ);
	#Иначе
		УправлениеДоступом.ЗаписатьГруппыИВидыДоступа(Объект.Ссылка, Отказ);
	#КонецЕсли
	
	//popn+
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.ГруппыПользователей") Тогда
		
		НеНужноОбновлениеРолей = НЕ ГруппаИмеетРоли(Объект.Ссылка);
		
		Если НеНужноОбновлениеРолей Тогда
			Возврат;
		КонецЕсли;
		
		ПользователиГруппы = Объект.Состав.Выгрузить().ВыгрузитьКолонку("Пользователь");
		Если Объект.ДополнительныеСвойства.Свойство("МассивСтарыхПользователейГруппы") Тогда
			МассивСтарыхПользователейГруппы = Объект.ДополнительныеСвойства.МассивСтарыхПользователейГруппы;
			Для Каждого СтарыйПользователь Из МассивСтарыхПользователейГруппы Цикл
				Если ПользователиГруппы.Найти(СтарыйПользователь) = НЕОПРЕДЕЛЕНО Тогда
					ПользователиГруппы.Добавить(СтарыйПользователь);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		УправлениеДоступом.ОбновитьРолиПользователей(ПользователиГруппы, Ложь);
		
		Для Каждого Пользователь Из ПользователиГруппы Цикл
			ПользовательОбъект = Пользователь.ПолучитьОбъект();
			ПользовательОбъект.Записать();
		КонецЦикла;
		
	КонецЕсли;
	//popn+

КонецПроцедуры // ЗаписатьГруппыИВидыДоступа()

//popn+
Функция ГруппаИмеетРоли(ГруппаПользователей)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПользователиГруппыДоступа.ГруппаДоступа
		|ИЗ
		|	РегистрСведений.ПользователиГруппыДоступа КАК ПользователиГруппыДоступа
		|ГДЕ
		|	ПользователиГруппыДоступа.Пользователь = &Пользователь";

	Запрос.УстановитьПараметр("Пользователь", ГруппаПользователей);

	Результат = Запрос.Выполнить();
	Возврат НЕ Результат.Пустой();	
	
КонецФункции
//popn-