Процедура РаспределитьКоличествоПоСтрокам(МассивСтрок, МетодРазнесенияКоличества, Количество) Экспорт

	чКолСтрок = МассивСтрок.Количество();
	Если чКолСтрок = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Если МетодРазнесенияКоличества = ПредопределенноеЗначение("Перечисление.МетодРаспределенияКоличестваМеждуФирмами.ВРавныхДолях") Тогда
		
		чКолНаСтроку = Окр(Количество / чКолСтрок , 4);
		чКолРаспределили = 0;
		Для каждого Строка из МассивСтрок Цикл
			Строка.Количество = чКолНаСтроку;
			чКолРаспределили = чКолРаспределили + чКолНаСтроку;
		КонецЦикла;
		Если Количество <> чКолРаспределили Тогда
			МассивСтрок[0].Количество = МассивСтрок[0].Количество + Количество - чКолРаспределили;
		КонецЕсли;
		
	ИначеЕсли МетодРазнесенияКоличества = ПредопределенноеЗначение("Перечисление.МетодРаспределенияКоличестваМеждуФирмами.ВсеНаБольшееКоличество") Тогда
		
		СтрокаМакс = МассивСтрок[0];
		чМаксКол = СтрокаМакс.КоличествоУчет;
		Для каждого Строка из МассивСтрок Цикл
			Если чМаксКол < Строка.КоличествоУчет Тогда
				чМаксКол = Строка.КоличествоУчет;
				СтрокаМакс = Строка;
			КонецЕсли;	
			Строка.Количество = 0;
		КонецЦикла;
		
		СтрокаМакс.Количество = Количество;
		 
	ИначеЕсли МетодРазнесенияКоличества = ПредопределенноеЗначение("Перечисление.МетодРаспределенияКоличестваМеждуФирмами.ПропорциональноУчетномуКоличеству") Тогда
		
		чКолУчетноеИтого = 0;
		Для каждого Строка из МассивСтрок Цикл
			//popn+
			Если Строка.КоличествоУчет < 0 Тогда
				Продолжить;
			КонецЕсли;
			//popn-
			чКолУчетноеИтого = чКолУчетноеИтого + Строка.КоличествоУчет;
		КонецЦикла;
		Если чКолУчетноеИтого = 0 Тогда
			Возврат;
		КонецЕСли;	
			
		чКолРаспределили = 0;
		Для каждого Строка из МассивСтрок Цикл
			//popn+
			Если Строка.КоличествоУчет < 0 Тогда
				Продолжить;
			КонецЕсли;
			//popn-
			чКолНаСтроку = Окр(Количество * Строка.КоличествоУчет / чКолУчетноеИтого , 4);
			Строка.Количество = чКолНаСтроку;
			чКолРаспределили = чКолРаспределили + чКолНаСтроку;
		КонецЦикла;
		Если Количество <> чКолРаспределили Тогда
			МассивСтрок[0].Количество = МассивСтрок[0].Количество + Количество - чКолРаспределили;
		КонецЕсли;
	КонецЕсли;	

КонецПроцедуры

Процедура ПриИзмененииМетодаРазнесенияКоличества(Объект) Экспорт
	
	МетодРазнесенияКоличества = Объект.МетодРазнесенияКоличества;
	
	Для каждого сСостав из Объект.Состав Цикл
		мНайденныеСтроки = Объект.СоставПоФирмам.НайтиСтроки(Новый Структура("Склад, Номенклатура", сСостав.Склад, сСостав.Номенклатура));
		Если мНайденныеСтроки.Количество() > 0 Тогда
			РаспределитьКоличествоПоСтрокам(мНайденныеСтроки, МетодРазнесенияКоличества, сСостав.Количество);
		КонецЕсли; 
	КонецЦикла;	
	
КонецПроцедуры
