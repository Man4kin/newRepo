Функция ПолучитьСписокНапоминаний() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗадачаЗадачиПоИсполнителю.Наименование КАК Тема,
	               |	ЗадачаЗадачиПоИсполнителю.Ссылка КАК Ссылка,
	               |	ЗадачаЗадачиПоИсполнителю.Дата КАК Дата
	               |ИЗ
	               |	Задача.ЗадачаИсполнителя.ЗадачиПоИсполнителю(
	               |			&ТекПользователь,
	               |			Выполнена = ЛОЖЬ
	               |				И Напоминать
	               |				И Дата <= &ДатаСреза) КАК ЗадачаЗадачиПоИсполнителю";
		
	Запрос.УстановитьПараметр("ТекПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ДатаСреза"      , ТекущаяДата());
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаРезультата = РезультатЗапроса.Выгрузить();
		ТаблицаРезультата.Колонки.Добавить("Срок");
		Для Каждого Строка Из ТаблицаРезультата Цикл
			Строка.Срок = СрокСтрокой(Строка.Дата);
		КонецЦикла;
		ТаблицаРезультата.Колонки.Удалить("Дата");
		УдалитьИзВременногоХранилища(ПараметрыСеанса.ТекущийПользователь.УникальныйИдентификатор());
		Возврат ПоместитьВоВременноеХранилище(ТаблицаРезультата, ПараметрыСеанса.ТекущийПользователь.УникальныйИдентификатор());
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция СрокСтрокой(ДатаСрока)
	
	РазностьДат = КонецМинуты(ТекущаяДата()) - КонецМинуты(ДатаСрока);
	Если РазностьДат > 0 Тогда
		
		Предлог     = "на";
		Определение = "просрочен";
		Секунды     = РазностьДат;
		
	Иначе
		
		Предлог     = "через";
		Определение = "истекает";
		Секунды     = - РазностьДат;
		
	КонецЕсли;
	
	ЧислоМинут = 1;
	ЧислоЧасов = 0;
	ЧислоДней  = 0;
	
	Если (Секунды >= 60) И (Секунды < 3600) Тогда // в 1 часе 3600 секунд
		ЧислоМинут = Цел(Секунды/60);
	ИначеЕсли (Секунды >= 3600) И (Секунды < 86400) Тогда // в 1 дне 86400 секунд
		
		ЧислоЧасов = Цел(Секунды/(60*60));
		ЧислоМинут = Цел((Секунды - ЧислоЧасов*60*60)/60);
		
	ИначеЕсли Секунды >= 86400 Тогда
		
		ЧислоДней  = Цел(Секунды/(60*60*24));
		ЧислоЧасов = Цел((Секунды - ЧислоДней*60*60*24)/(60*60));
		ЧислоМинут = Цел((Секунды - (ЧислоДней*60*60*24 + ЧислоЧасов*60*60))/60);
		
	КонецЕсли;
	
	Возврат Предлог + " " + ?(ЧислоДней > 0, Строка(ЧислоДней) + " дн. ", "") + ?(ЧислоЧасов > 0, Строка(ЧислоЧасов) + " час. ", "") + Строка(ЧислоМинут) + " мин. " + Определение;
	
КонецФункции // СрокСтрокой()

Функция ОткрыватьСписокНапоминаний() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗадачаЗадачиПоИсполнителю.Наименование КАК Тема,
	               |	ЗадачаЗадачиПоИсполнителю.Ссылка КАК Ссылка,
	               |	ЗадачаЗадачиПоИсполнителю.Дата КАК Дата
	               |ИЗ
	               |	Задача.ЗадачаИсполнителя.ЗадачиПоИсполнителю(
	               |			&ТекПользователь,
	               |			Выполнена = ЛОЖЬ
	               |				И Напоминать
	               |				И Дата <= &ДатаСреза
	               |				И ссылка <> НЕОПРЕДЕЛЕНО
	               |				И СтруктурнаяЕдиница В ИЕРАРХИИ (&Се)) КАК ЗадачаЗадачиПоИсполнителю
	               |ГДЕ
	               |	(НЕ ЗадачаЗадачиПоИсполнителю.ПометкаУдаления)";
		
	Запрос.УстановитьПараметр("ТекПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ДатаСреза"      , ТекущаяДата());
	Запрос.УстановитьПараметр("СЕ",Параметрысеанса.ТекущаяСтруктурнаяЕдиница);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат НЕ РезультатЗапроса.Пустой();
КонецФункции

