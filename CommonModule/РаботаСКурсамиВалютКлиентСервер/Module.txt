// Процедура для загрузки курсов валют по определенному периоду.
//
// Параметры
// Валюты		- Любая коллекция - со следующими полями:
//					КодВалюты - числовой код валюты
//					Валюта - ссылка на валюту
// НачалоПериодаЗагрузки	- Дата - начало периода загрузки курсов
// ОкончаниеПериодаЗагрузки	- Дата - окончание периода загрузки курсов
// ПоясняющееСообщение		- строка - в случае возникновения ошибки
//							(исключения) в функции, содержит текстовое представление описания ошибки
//
// Возвращаемое значение:
// Истина	- валюты успешно загружены
// Ложь		- хотя бы одна валюта не была загружена
//
Функция ЗагрузитьКурсыВалютПоПараметрам(знач Валюты,
										знач НачалоПериодаЗагрузки,
										знач ОкончаниеПериодаЗагрузки,
										ПоясняющееСообщение = "") Экспорт
	
	СтатусОперации = Истина;
	
	СерверИсточник = "cbrates.rbc.ru";
	Если НачалоПериодаЗагрузки = ОкончаниеПериодаЗагрузки Тогда
		Адрес = "tsv/";
		ТМП   = "/"+Формат(Год(ОкончаниеПериодаЗагрузки),"ЧРГ=; ЧГ=0")+"/"+Формат(Месяц(ОкончаниеПериодаЗагрузки),"ЧЦ=2;ЧДЦ=0;ЧВН=")+"/"+Формат(День(ОкончаниеПериодаЗагрузки),"ЧЦ=2;ЧДЦ=0;ЧВН=");
	Иначе
		Адрес = "tsv/cb/";
		ТМП   = "";
	КонецЕсли;
	
	Для Каждого Валюта из Валюты Цикл
		ФайлНаВебСервере = "http://" + СерверИсточник + "/" + Адрес + Прав(Валюта.КодВалюты, 3) + ТМП + ".tsv";
		
#Если НаКлиенте Тогда
		Результат = ПолучениеФайловИзИнтернетаКлиент.СкачатьФайлНаКлиенте(ФайлНаВебСервере);
#Иначе
		Результат = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(ФайлНаВебСервере);
#КонецЕсли
		
		Если Результат.Статус Тогда
#Если НаКлиенте Тогда
			ДвоичныеДанные = Новый ДвоичныеДанные (Результат.Путь);
			АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			ПоясняющееСообщение = ПоясняющееСообщение + РаботаСКурсамиВалют.ЗагрузитьКурсВалютыИзФайла(Валюта.Валюта, АдресВоВременномХранилище, НачалоПериодаЗагрузки, ОкончаниеПериодаЗагрузки) + Символы.ПС;
#Иначе
			ПоясняющееСообщение = ПоясняющееСообщение + РаботаСКурсамиВалют.ЗагрузитьКурсВалютыИзФайла(Валюта.Валюта, Результат.Путь, НачалоПериодаЗагрузки, ОкончаниеПериодаЗагрузки) + Символы.ПС;
#КонецЕсли
			УдалитьФайлы(Результат.Путь);
		Иначе
			СтатусОперации = Ложь;
			ПоясняющееСообщение= НСтр("ru = 'Не возможно получить файл данных с курсами валюты (%1 - %2):
					|%3
					|Возможно, нет доступа к веб сайту с курсами валют, либо указана несуществующая валюта.'");
			ПоясняющееСообщение = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ПоясняющееСообщение,
								Валюта.КодВалюты,
								Валюта.Валюта,
								Результат.СообщениеОбОшибке);
		КонецЕсли;
	КонецЦикла;
	
	Если СтатусОперации Тогда
		ПоясняющееСообщение = Лев(ПоясняющееСообщение, СтрДлина(ПоясняющееСообщение) - 2);
	КонецЕсли;
	
	Возврат СтатусОперации;
	
КонецФункции

#Если Сервер тогда
Функция ПолучитьКурсВалютыНаДату(знач Валюта,Дата=Неопределено) экспорт
	Если Дата=Неопределено Тогда
		Дата=ТекущаяДата();
	КонецЕсли;	
		
	
	з=Новый запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыВалютСрезПоследних");
	з.УстановитьПараметр("Дата",Дата);
	з.УстановитьПараметр("Валюта",Валюта);
	в=з.Выполнить().Выбрать();
	с=Новый Структура;
	с.Вставить("Курс",0);
	с.Вставить("Кратность",1);
	Если в.Следующий() Тогда
		с.Курс = в.Курс;
		с.Кратность = в.Кратность;
	КонецЕсли;	

	Возврат с;
КонецФункции	
#КонецЕсли