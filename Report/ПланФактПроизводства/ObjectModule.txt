
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	ПараметрДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ITEM");
	ПараметрДанных.Использование = Истина;
	ПараметрДанных.Значение = ПолучитьСписокПродукции();
	
	ТаблицаДат = Новый ТаблицаЗначений;
	ТаблицаДат.Колонки.Добавить("Дата",Новый ОписаниеТипов("Дата"));
	ТаблицаДат.Колонки.Добавить("ДеньНедели",Новый ОписаниеТипов("Число"));
	ДатаНачала = НачалоДня(КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ПериодОтчета").Значение.ДатаНачала);
	ДатаОкончания = НачалоДня(КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ПериодОтчета").Значение.ДатаОкончания);
	Пока ДатаНачала <= ДатаОкончания Цикл
		НоваяСтрока = ТаблицаДат.Добавить();
		НоваяСтрока.Дата = ДатаНачала;
		НоваяСтрока.ДеньНедели = ДеньНедели(ДатаНачала);
		ДатаНачала = ДатаНачала + 86400;
	КонецЦикла;	
	СтандартнаяОбработка = Ложь;
	
	//Связь между таблицей значений и именами в СКД      
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаДат", ТаблицаДат);
	
	//Макет компоновки      
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровки);
	
	//Компоновка данных      
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	
	//Вывод результата      
	ДокументРезультат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
КонецПроцедуры

Функция ПолучитьСписокПродукции()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродукцияПоЦехам.Продукция.Код КАК Код
	|ИЗ
	|	РегистрСведений.ПродукцияПоЦехам КАК ПродукцияПоЦехам
	|СГРУППИРОВАТЬ ПО
	|	ПродукцияПоЦехам.Продукция.Код";
	
	СписокТоваров = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Код");
	Для I=0 по СписокТоваров.Количество()-1 Цикл
		СписокТоваров[I] = СокрЛП(СписокТоваров[I]);
	КонецЦикла;	
	Возврат СписокТоваров;
конецфункции
