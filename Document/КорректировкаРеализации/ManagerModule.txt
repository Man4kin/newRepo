
Функция ТекстЗапросаДанныеДляПечатиСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("ВременнаяТаблицаНаличиеТоваров", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",                      НомераТаблиц.Количество());
	// Корректировка реализации по документам, оформленным в у.е., не поддерживается, поэтому таблица РублевыеСуммы создается пустой
	НомераТаблиц.Вставить("РублевыеСуммы",                  НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",      НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ НаличиеТоваров
	|ИЗ
	|	Документ.КорректировкаРеализации.Состав КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Фирма КАК Фирма,
	|	Реквизиты.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	Реквизиты.Фирма КАК Поставщик,
	|	ЕСТЬNULL(ЗапросПоКПП.КПП, Реквизиты.Фирма.КПП) КАК КППпоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Контрагент
	|		ИНАЧЕ Реквизиты.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	Реквизиты.Контрагент КАК Покупатель,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ""он же""
	|		ИНАЧЕ Реквизиты.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА НаличиеТоваров.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьТовары,
	|	Реквизиты.СтруктурнаяЕдиница.Валюта КАК Валюта
	|ИЗ
	|	Документ.КорректировкаРеализации КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаличиеТоваров КАК НаличиеТоваров
	|		ПО Реквизиты.Ссылка = НаличиеТоваров.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактура КАК СчетФактураВыданный
	|		ПО (СчетФактураВыданный.Ссылка = &СчетФактура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СтруктурныеЕдиницыДополнительныеРеквизиты.Значение КАК КПП,
	|			СтруктурныеЕдиницыДополнительныеРеквизиты.Ссылка КАК СтруктурнаяЕдиница
	|		ИЗ
	|			Справочник.СтруктурныеЕдиницы.ДополнительныеРеквизиты КАК СтруктурныеЕдиницыДополнительныеРеквизиты
	|		ГДЕ
	|			 СтруктурныеЕдиницыДополнительныеРеквизиты.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыСтруктурныхЕдиниц.КППОбособленногоПодразделения)) КАК ЗапросПоКПП
	|		ПО Реквизиты.СтруктурнаяЕдиница = ЗапросПоКПП.СтруктурнаяЕдиница
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ДокументОснование КАК ДокументОснование,
	|	0 КАК Всего,
	|	0 КАК ВсегоНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Товар,
	|	ТаблицаТовары.Номенклатура.Наименование КАК ТоварНаименование,
	|	ТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ИСТИНА КАК СуммаВключаетНДС,
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации.Состав КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияПереопределяемый.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаДанныеДляПечатиКорректировочныхСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",                      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаНоменклатуры",   НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",      НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Фирма КАК Фирма,
	|	Реквизиты.Фирма КАК Поставщик,
	|	ЕСТЬNULL(ЗапросПоКПП.КПП, Реквизиты.Фирма.КПП) КАК КППпоставщика,
	|	Реквизиты.Контрагент КАК Покупатель,
	|	ИСТИНА КАК СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА Реквизиты.РежимКорректировки = ЗНАЧЕНИЕ(Перечисление.РежимыКорректировок.ИсправлениеОшибки)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Исправление,
	|	Реквизиты.СтруктурнаяЕдиница.Валюта КАК Валюта
	|ПОМЕСТИТЬ ВременнаяТаблицаРеквизиты
	|ИЗ
	|	Документ.КорректировкаРеализации КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактура КАК СчетФактураВыданный
	|		ПО Реквизиты.Ссылка = СчетФактураВыданный.ДокОснование
	|			И (НЕ СчетФактураВыданный.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			СтруктурныеЕдиницыДополнительныеРеквизиты.Значение КАК КПП,
	|			СтруктурныеЕдиницыДополнительныеРеквизиты.Ссылка КАК СтруктурнаяЕдиница
	|		ИЗ
	|			Справочник.СтруктурныеЕдиницы.ДополнительныеРеквизиты КАК СтруктурныеЕдиницыДополнительныеРеквизиты
	|		ГДЕ
	|			 СтруктурныеЕдиницыДополнительныеРеквизиты.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыСтруктурныхЕдиниц.КППОбособленногоПодразделения)) КАК ЗапросПоКПП
	|		ПО Реквизиты.СтруктурнаяЕдиница = ЗапросПоКПП.СтруктурнаяЕдиница
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.ДатаОснования,
	|	Реквизиты.Фирма,
	|	Реквизиты.Поставщик,
	|	Реквизиты.КППпоставщика,
	|	Реквизиты.Покупатель,
	|	Реквизиты.Валюта
	|ИЗ
	|	ВременнаяТаблицаРеквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Ссылка КАК Ссылка,
	|	1 КАК НомерТабЧасти,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНоменклатуры.Номенклатура КАК Товар,
	|	ТаблицаНоменклатуры.Номенклатура.Наименование КАК ТоварНаименование,
	|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ТаблицаНоменклатуры.КоличествоДоКорректировки
	|		ИНАЧЕ ТаблицаНоменклатуры.КоличествоДоИзменения
	|	КОНЕЦ КАК КоличествоДоИзменения,
	|	ТаблицаНоменклатуры.Количество КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ТаблицаНоменклатуры.ЦенаДоКорректировки
	|		ИНАЧЕ ТаблицаНоменклатуры.ЦенаДоИзменения
	|	КОНЕЦ КАК ЦенаДоИзменения,
	|	ТаблицаНоменклатуры.Цена КАК ЦенаПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|		ИНАЧЕ ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|	КОНЕЦ КАК СуммаНДСДоИзменения,
	|	ТаблицаНоменклатуры.СуммаНДС КАК СуммаНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА ТаблицаНоменклатуры.СуммаДоКорректировки - ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|					ИНАЧЕ ТаблицаНоменклатуры.СуммаДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА ТаблицаНоменклатуры.СуммаДоИзменения - ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|				ИНАЧЕ ТаблицаНоменклатуры.СуммаДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьБезНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА ТаблицаНоменклатуры.Сумма - ТаблицаНоменклатуры.СуммаНДС
	|		ИНАЧЕ ТаблицаНоменклатуры.Сумма
	|	КОНЕЦ КАК СтоимостьБезНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА ТаблицаНоменклатуры.СуммаДоКорректировки
	|					ИНАЧЕ ТаблицаНоменклатуры.СуммаДоКорректировки + ТаблицаНоменклатуры.СуммаНДСДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА ТаблицаНоменклатуры.СуммаДоИзменения
	|				ИНАЧЕ ТаблицаНоменклатуры.СуммаДоИзменения + ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьСНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА ТаблицаНоменклатуры.Сумма
	|		ИНАЧЕ ТаблицаНоменклатуры.Сумма + ТаблицаНоменклатуры.СуммаНДС
	|	КОНЕЦ КАК СтоимостьСНДСПослеИзменения
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.КорректировкаРеализации.Состав КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРеквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = ТаблицаНоменклатуры.Ссылка)
	|ГДЕ
	|	ТаблицаНоменклатуры.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.НомерТабЧасти КАК НомерТабЧасти,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНоменклатуры.Товар КАК Товар,
	|	ТаблицаНоменклатуры.ТоварНаименование КАК ТоварНаименование,
	|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.КоличествоДоИзменения КАК КоличествоДоИзменения,
	|	ТаблицаНоменклатуры.КоличествоПослеИзменения КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоДоИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения / ТаблицаНоменклатуры.КоличествоДоИзменения
	|	КОНЕЦ КАК ЦенаДоИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоПослеИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения / ТаблицаНоменклатуры.КоличествоПослеИзменения
	|	КОНЕЦ КАК ЦенаПослеИзменения,
	|	ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения КАК СтоимостьБезНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения КАК СтоимостьБезНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДСПослеИзменения КАК СуммаНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения КАК СтоимостьСНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения КАК СтоимостьСНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения > ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения - ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения > ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения - ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СуммаНДСДоИзменения > ТаблицаНоменклатуры.СуммаНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСДоИзменения - ТаблицаНоменклатуры.СуммаНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СуммаНДСПослеИзменения > ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСПослеИзменения - ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения > ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения - ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения > ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения - ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.ЭтоУслуга КАК ЭтоУслуга,
	|	ТаблицаНоменклатуры.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	(ТаблицаНоменклатуры.КоличествоПослеИзменения <> ТаблицаНоменклатуры.КоличествоДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения <> ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.СуммаНДСПослеИзменения <> ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.ЦенаПослеИзменения <> ТаблицаНоменклатуры.ЦенаДоИзменения)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияПереопределяемый.ТекстРазделителяЗапросовПакета();

КонецФункции


Функция ПечатьТОРГ12(МассивОбъектов,ОбъектыПечати) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	СтрукРеквизитыШапки = новый Структура();
	СТрукРеквизитыШапки.Вставить("СтруктурнаяЕдиница","СтруктурнаяЕдиница");
	//+Зернятко@27.03.2014 - Изменён порядок передачи даны в процедуру печати. Ключ - ИмяПоля, Значение - путь к реквизиту документа	
	//СтрукРеквизитыШапки.вставить("Контрагент","Покупатель");
	//СтрукРеквизитыШапки.Вставить("Грузополучатель","Грузополучатель");
	//СтрукРеквизитыШапки.Вставить("Грузоотправитель","Грузоотправитель");
	//СтрукРеквизитыШапки.Вставить("Фирма","Поставщик");
	//СтрукРеквизитыШапки.Вставить("ИсправляемыйДокумент","ДокОснование");
	СтрукРеквизитыШапки.вставить("Покупатель","Контрагент");
	СтрукРеквизитыШапки.Вставить("Грузополучатель","Грузополучатель");
	СтрукРеквизитыШапки.Вставить("Грузоотправитель","Грузоотправитель");
	СтрукРеквизитыШапки.Вставить("Поставщик","Фирма");
	СтрукРеквизитыШапки.Вставить("ДокОснование","ИсправляемыйДокумент");
 	///Зернятко@27.03.2014 - конец блока
	СтрукРеквизитыТабЧасти = новый Структура;
	СтрукРеквизитыТабЧасти.Вставить("НомерСтроки");
	СтрукРеквизитыТабЧасти.Вставить("Номенклатура");
	СтрукРеквизитыТабЧасти.Вставить("ЕдиницаИзмерения");
	СтрукРеквизитыТабЧасти.Вставить("Количество");
	СтрукРеквизитыТабЧасти.Вставить("Цена");
	СтрукРеквизитыТабЧАсти.Вставить("СтавкаНДС");
	СтрукРеквизитыТабЧасти.Вставить("СуммаНДС",Истина);
	СтрукРеквизитыТабЧасти.Вставить("Сумма",Истина);
	ТабДокумент = ОбработкаПечатиСервер.ПечатьТОРГ12Общая(МассивОбъектов,ОбъектыПечати,"Состав",СтрукРеквизитыШапки,СтрукРеквизитыТабЧасти);
	Возврат ТабДокумент;
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,
	ОбъектыПечати, ПараметрыВывода) Экспорт
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ТОРГ12", "ТОРГ-12", ПечатьТОРГ12(массивОбъектов,ОбъектыПечати));
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПоследнийКорректирующийДокумент(ДокументСсылка, СсылкаНаТекущийДокумент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"      , ДокументСсылка);
	Запрос.УстановитьПараметр("ЭтотДокумент", СсылкаНаТекущийДокумент);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаРеализации.Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.ИсправляемыйДокумент = &Ссылка
	//popn+ 050713
	//|	И КорректировкаРеализации.РежимКорректировки <> ЗНАЧЕНИЕ(Перечисление.РежимыКорректировок.СогласованноеИзменение)
	//popn-
	|	И КорректировкаРеализации.Ссылка <> &ЭтотДокумент
	|	И КорректировкаРеализации.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.Дата УБЫВ";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат ДокументСсылка;
	КонецЕсли;
		
КонецФункции

Процедура ЗаполнитьПоДокументу(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументРеализации) Тогда
		Возврат;
	КонецЕсли;
	
	стрИмяВидаДокумента = Объект.ДокументРеализации.Метаданные().Имя;

	Запрос = Новый Запрос;
	ТекстЗапрос =
	"ВЫБРАТЬ
	|	РасходнаяНакладнаяСостав.Номенклатура,
	|	РасходнаяНакладнаяСостав.ЕдиницаИзмерения,
	|	РасходнаяНакладнаяСостав.Количество,
	|	РасходнаяНакладнаяСостав.Количество КАК КоличествоДоИзменения,
	|	РасходнаяНакладнаяСостав.Коэффициент,
	|	РасходнаяНакладнаяСостав.Цена,
	|	РасходнаяНакладнаяСостав.Цена КАК ЦенаДоИзменения,
	|	РасходнаяНакладнаяСостав.СуммаНДС,
	|	РасходнаяНакладнаяСостав.СуммаНДС КАК СуммаНДСДоИзменения,
	|	РасходнаяНакладнаяСостав.Сумма,
	|	РасходнаяНакладнаяСостав.Сумма КАК СуммаДоИзменения,
	|	РасходнаяНакладнаяСостав.СтавкаНДС,
	|	РасходнаяНакладнаяСостав.СтавкаНДС КАК СтавкаНДСДоИзменения,";
	Если стрИмяВидаДокумента = "КорректировкаРеализации" Тогда
		ТекстЗапрос = ТекстЗапрос + "
		|	РасходнаяНакладнаяСостав.КоличествоДоКорректировки КАК КоличествоДоКорректировки,
		|	РасходнаяНакладнаяСостав.ЦенаДоКорректировки       КАК ЦенаДоКорректировки,
		|	РасходнаяНакладнаяСостав.СуммаДоКорректировки      КАК СуммаДоКорректировки,
		|	РасходнаяНакладнаяСостав.СуммаНДСДоКорректировки   КАК СуммаНДСДоКорректировки,";
	КонецЕсли;
	ТекстЗапрос = ТекстЗапрос + "
	|	РасходнаяНакладнаяСостав.Вес,
	|	РасходнаяНакладнаяСостав.ЕдиницаВеса
	|ИЗ
	|	Документ."+стрИмяВидаДокумента+".Состав КАК РасходнаяНакладнаяСостав
	|ГДЕ
	|	РасходнаяНакладнаяСостав.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",Объект.ДокументРеализации);
	Запрос.Текст = ТекстЗапрос; 
	Объект.Состав.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры