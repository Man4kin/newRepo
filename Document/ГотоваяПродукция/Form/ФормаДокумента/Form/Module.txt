&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиент.ПриИзмененииЕдиницыИзмеренияВТабличнойЧасти(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Если Элементы.Найти("ПодборНоменклатуры")<>Неопределено тогда
		Значение = Настройки.Получить("ОтображениеТаблицыПодбор");
		Если Значение<>Неопределено тогда
			Элементы.ПодборНоменклатуры.Отображение = Значение;
		конецесли;
	конецесли;
	Если ЗначениеЗаполнено(Объект.Ссылка) или ЗначениеЗаполнено(Параметры.Основание) или ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Настройки.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	Если Элементы.Найти("ПодборНоменклатуры")<>Неопределено тогда
		Настройки.вставить("ОтображениеТаблицыПодбор",элементы.подборНоменклатуры.Отображение);
	конецесли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТолькоПросмотр = РаботаСФормамиСервер.ЗапрещеноРедактировать(Объект.Ссылка,ЭтаФорма);
	//УправлениеДиалогом();
	УстановитьВыпускПродукцииПоПлановымЦенам(); 
	ОбщегоНазначенияСервер.ЗапретМодификацииОбъектов(ЭтаФорма,Элементы.ФормаСоздатьНаОсновании);
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
КонецПроцедуры

//Процедуры и функции для организации подбора
&НаКлиенте
Процедура ПодборНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Для Каждого Строка Из Элемент.ВыделенныеСтроки Цикл
		Количество = ОбработкаТабличныхЧастейКлиент.ЗапросКоличества(ЗапрашиватьКоличество);
		ТекСтрока = ОбработкаТабличныхЧастейКлиент.ЗаполнитьИзПодбора(ЭтаФорма,Объект,Элемент.ДанныеСтроки(Строка),Элементы.Продукция,СтандартнаяОбработка,Количество,,"Продукция");
		Если ТекСтрока <> Неопределено Тогда
			ЗаполнитьСоставНаСервере(ТекСтрока.Номенклатура,ТекСтрока.Количество,ТекСтрока.Коэффициент,ТекСтрока.ТехнологическаяКарта,ТекСтрока.ПоТехКарте,ТекСтрока.Ключ,ТекСтрока.ПлановаяСтоимость,ТекСтрока.ПлановаяСтоимостьНДС,ТекСтрока.СуммаПлановая, ТекСтрока.СуммаНДСПлановая);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ПодборОткрыть(Команда)
	УстановитьПараметры();
	Элементы.ГруппаПодбор.Видимость = НЕ Элементы.ГруппаПодбор.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура СоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ПодборОткрыть(Неопределено);
КонецПроцедуры

 &НаКлиенте
Процедура ПоказатьГруппыВПодборе(Команда)
	элементы.кнГруппы.пометка = не элементы.кнГруппы.пометка;
	флаг = Элементы.кнГруппы.пометка;
	ВЫполнить("ОбработкаТабличныхЧастейКлиент.ОтображениеГрупп(ПодборНоменклатуры,флаг)");
КонецПроцедуры

 &НаСервере
Процедура СохранитьПараметрыПодбора()
	ПодборНоменклатурыСервер.СохранитьПараметрыПодбора(Объект.Ссылка.Метаданные().имя,ЭтаФорма.ИмяФормы,ЭтаФорма.Элементы);
конецпроцедуры

&НаСервере                 
Функция ПолучитьНастройкуПодбора()
	Возврат ПодборНоменклатурыСервер.ПолучитьНастройкуПодбора(Объект.Ссылка.Метаданные().Имя,ЭтаФорма.ИмяФормы);	
конецфункции

&НаКлиенте
процедура ЗагрузитьНастройкиПодбора()
		Настройки = ПолучитьНастройкуПодбора();
		ОбработкаТабличныхЧастейКлиент.загрузитьнастройкиподбора(ЭтаФорма,Настройки);
конецпроцедуры

&НаКлиенте
Процедура ПодборЗакрыть(Команда)
	Элементы.ГруппаПодбор.Видимость  = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Дерево(Команда)
	обработкатабличныхчастейклиент.ОтображениеДинамическогоСпискаДерево(ЭтаФорма);	
КонецПроцедуры

&НаКлиенте
Процедура Иерархия(Команда)
	обработкатабличныхчастейклиент.отображениединамическогоспискаиерархия(этаформа);	
КонецПроцедуры

&НаКлиенте
Процедура Список(Команда)
	обработкатабличныхчастейклиент.ОтображениеДинамическогоСпискаСписок(этаформа);	
КонецПроцедуры

//Конец Процедуры и функции для организации подбора

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		//вызываем установку реквизита только для новых документов, так как значение реквизита Фирма могло быть сохранено в настройках
		УстановитьВыпускПродукцииПоПлановымЦенам();
	КонецЕсли;
	УправлениеДиалогом();
	УстановитьПараметры();
	ЗагрузитьНастройкиПодбора();
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	УправлениеДиалогом();
	Установитьпараметры();
КонецПроцедуры

Процедура УстановитьПараметры()
	ПодборНоменклатуры.Параметры.УстановитьЗначениеПараметра("СтруктурнаяЕдиница",Объект.СкладПроизводства.Владелец);
	ПодборНоменклатуры.Параметры.УстановитьЗначениеПараметра("ВидПроизводства",Объект.ВидПроизводства);
конецпроцедуры

&НаКлиенте
Процедура ПродукцияПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Элементы.Состав.ОтборСтрок = Новый ФиксированнаяСтруктура("Ключ", Элемент.ТекущиеДанные.Ключ);
		Элементы.Состав.ТолькоПросмотр = Элемент.ТекущиеДанные.ПоТехКарте;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеКоличествоПриИзменении(Элемент)
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ",Элементы.Распределение.ТекущиеДанные.Ключ);
	Массив = Объект.Распределение.НайтиСтроки(Отбор);
	Итог = 0;
	Для I=0 по Массив.Количество()-1 Цикл
		Итог = Итог+Массив[I][Элемент.Имя];
	КонецЦикла;	
	
	Массив = Объект.Состав.НайтиСтроки(Отбор);
	ТекущаяСтрока = Массив[0];
	ТекущаяСтрока[Элемент.Имя] = Итог;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПоТехКартеПриИзменении(Элемент)
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	Элементы.Состав.ТолькоПросмотр = ТекСтрока.ПоТехКарте;
	Если ТекСтрока.ПоТехКарте Тогда 
		ЗаполнитьСоставНаСервере(ТекСтрока.Номенклатура,ТекСтрока.Количество,ТекСтрока.Коэффициент,ТекСтрока.ТехнологическаяКарта,ТекСтрока.ПоТехКарте,ТекСтрока.Ключ,ТекСтрока.ПлановаяСтоимость,ТекСтрока.ПлановаяСтоимостьНДС,ТекСтрока.СуммаПлановая,ТекСтрока.СуммаНДСПлановая);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоЗаказам(Команда)
	Если Объект.Продукция.Количество() > 0 Тогда
		ТекстВопроса = "Табличные части будут очищены.
		|Продолжить?";
		Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Объект.Продукция.Очистить();
			Объект.Состав.Очистить();
		Иначе	
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	ЗаполнитьПоЗаказамНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПланам(Команда)
	Если Объект.Продукция.Количество() > 0 Тогда
		ТекстВопроса = "Табличные части будут очищены.
		|Продолжить?";
		Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Объект.Продукция.Очистить();
			Объект.Состав.Очистить();
		Иначе	
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	ЗаполнитьПоПланамНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоЗаказамНаСервере()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоЗаказам();
	ЗначениеВДанныеФормы(ДокументОбъект,Объект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПланамНаСервере()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоПланам();
	ЗначениеВДанныеФормы(ДокументОбъект,Объект);
КонецПроцедуры

&НаКлиенте
Процедура УправлениеДиалогом()
	флПриготовление = Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Приготовление");
	
	Элементы.ГруппаПодбор.Видимость         = ?(Не ЗначениеЗаполнено(Объект.Склад),Ложь,Элементы.ГруппаПодбор.Видимость);
	Элементы.ПродукцияЗаполнить.Доступность = флПриготовление;
	Элементы.СоставКоэффициентРаспределенияЦены.Видимость = НЕ флПриготовление И НЕ флВыпускПродукцииПоПлановымЦенам; 
	Элементы.ПродукцияПлановаяСтоимость.Видимость = флВыпускПродукцииПоПлановымЦенам И флПриготовление;
	Элементы.ПродукцияСуммаПлановая.Видимость     = флВыпускПродукцииПоПлановымЦенам И флПриготовление;
	Элементы.СоставПлановаяСтоимость.Видимость    = флВыпускПродукцииПоПлановымЦенам И НЕ флПриготовление;
	Элементы.СоставСуммаПлановая.Видимость        = флВыпускПродукцииПоПлановымЦенам И НЕ флПриготовление;
	//popn+
	//Элементы.СоставИнгредиент.Видимость            = флПриготовление;
	Элементы.ПродукцияСуммаНДСПлановая.Видимость     = флВыпускПродукцииПоПлановымЦенам И флПриготовление;
	Элементы.ПродукцияПлановаяСтоимостьНДС.Видимость = флВыпускПродукцииПоПлановымЦенам И флПриготовление;
	Элементы.СоставСуммаНДСПлановая.Видимость        = флВыпускПродукцииПоПлановымЦенам И НЕ флПриготовление;
	Элементы.СоставПлановаяСтоимостьНДС.Видимость    = флВыпускПродукцииПоПлановымЦенам И НЕ флПриготовление;
	Элементы.СоставПроцентСписания.Видимость         = НЕ флПриготовление;
	//popn-
КонецПроцедуры

&НаКлиенте
Процедура ВидПроизводстваПриИзменении(Элемент)
	УправлениеДиалогом();
	Объект.Продукция.Очистить();
	Объект.Состав.Очистить();
	УстановитьПараметры();
КонецПроцедуры

&НаКлиенте
Процедура ФирмаПриИзменении(Элемент)
	УстановитьВыпускПродукцииПоПлановымЦенам();
	УправлениеДиалогом();
КонецПроцедуры

&НаСервере
Процедура УстановитьВыпускПродукцииПоПлановымЦенам()
	флВыпускПродукцииПоПлановымЦенам = ОбщегоНазначенияСервер.ПолучитьУчетнуюПолитикуФирмы(Объект.Фирма,Объект.Дата).ВыпускПродукцииПоПлановымЦенам;
КонецПроцедуры	

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Пересчет(ТекСтрока);
		ПересчитатьДанныеСостава(ТекСтрока);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДанныеСостава(ТекСтрока)
	
	СтрукТехКарта = ПолучитьСтруктуруДанныхТехКарты(ТекСтрока.ТехнологическаяКарта);
	СоСтрокиТехКарты = СтрукТехКарта.СтрокиТехКарты;
	
	мсв = Объект.Состав.НайтиСтроки(Новый Структура("Ключ",ТекСтрока.Ключ));
	
	флПриготовление = Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Приготовление")
	ИЛИ (Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Разделка")
	И ОбщегоНазначения.ПолучитьЗначениеРеквизита(ТекСтрока.ТехнологическаяКарта, "ВозможнаРазделка"));
	
	флРазделка      = Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Разделка")
	И НЕ ОбщегоНазначения.ПолучитьЗначениеРеквизита(ТекСтрока.ТехнологическаяКарта, "ВозможнаРазделка");
	
	чКоличествоПродукции = ТекСтрока.Количество;
	Если флРазделка Тогда
		чКоличествоПродукцииРаспределили = 0;
	КонецЕсли;	
	
	Для каждого Эл из мсв Цикл
		//найдем строку тех карты, чтобы определить количество составляющего на единицу продукции
		//если задан ингредиент, то ищем по полю ингредиент
		Если ЗначениеЗаполнено(Эл.Ингредиент) Тогда
			спрНоменклатура = Эл.Ингредиент;
			//popn+
			Если ЗначениеЗаполнено(Эл.Номенклатура) Тогда
				СтрокаИнгредиента = Эл.Ингредиент.Состав.Найти(Эл.Номенклатура,"Номенклатура");
				Если СтрокаИнгредиента = НЕОПРЕДЕЛЕНО Тогда
					КоэффициентЗамены = 1;
				Иначе
					КоэффициентЗамены = СтрокаИнгредиента.КоэффициентЗамены;
				КонецЕсли;
			Иначе
				КоэффициентЗамены = 1;
			КонецЕсли;
			//popn-
		Иначе	
			спрНоменклатура = Эл.Номенклатура;
			//popn+
			КоэффициентЗамены = 1;
			//popn-
		КонецЕсли;
		чКоличество = СоСтрокиТехКарты.Получить(спрНоменклатура);
		Если чКоличество = Неопределено Тогда
			Сообщить("Ошибка! Не найдена строка в технологической карте для номенклатуры "+спрНоменклатура, СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;
		Если флПриготовление Тогда
			//popn+
			//popn-
			чКолНаЕдиницуПродукции = чКоличество / СтрукТехКарта.КоличествоПорций;
			Эл.Количество = Окр(чКолНаЕдиницуПродукции * чКоличествоПродукции * КоэффициентЗамены,4);
		Иначе
			Эл.Количество = Окр(чКоличествоПродукции * чКоличество / 100,4);
			Если флРазделка Тогда
				чКоличествоПродукцииРаспределили = чКоличествоПродукцииРаспределили + Эл.Количество;
			КонецЕсли;	
		КонецЕсли;
		Пересчет(Эл);
	КонецЦикла;	
	
	Если флРазделка Тогда
		чРазница = чКоличествоПродукции - чКоличествоПродукцииРаспределили;
		Если чРазница <> 0 Тогда 
			Эл.Количество = Эл.Количество + чРазница;
			Пересчет(Эл);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруДанныхТехКарты(ТехКарта)
	
	СтрукТехКарта = Новый Структура;
	СтрукТехКарта.Вставить("КоличествоПорций",ТехКарта.КоличествоПорций);
	
	СоСтрокиТехКарты = Новый Соответствие;
	
	Для Каждого СтрокаТехКарты Из ТехКарта.Состав Цикл
		СоСтрокиТехКарты.Вставить(СтрокаТехКарты.Номенклатура,СтрокаТехКарты.Количество);
	КонецЦикла;	
	
	СтрукТехКарта.Вставить("СтрокиТехКарты",СоСтрокиТехКарты);
	
	Возврат СтрукТехКарта;
	
КонецФункции	

&НаКлиенте
Процедура ПродукцияПлановаяСтоимостьПриИзменении(Элемент)
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Пересчет(ТекСтрока);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Пересчет(ТекСтрока)
	
	ТекСтрока.СуммаПлановая = Окр(ТекСтрока.Количество * ТекСтрока.ПлановаяСтоимость,2);
	//popn+
	ТекСтрока.СуммаНДСПлановая = Окр(ТекСтрока.Количество * ТекСтрока.ПлановаяСтоимостьНДС,2);
	//popn-
	
КонецПроцедуры	

&НаКлиенте
Процедура ПродукцияПередУдалением(Элемент, Отказ)
	
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	
	мсв = Объект.Состав.НайтиСтроки(Новый Структура("Ключ",ТекСтрока.Ключ));
	Для каждого Эл из мсв Цикл
		
		Объект.Состав.Удалить(Эл);
		
	КонецЦикла;	
	
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКоличествоПриИзменении(Элемент)
	ТекСтрока = Элементы.Состав.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Пересчет(ТекСтрока);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияТехнологическаяКартаПриИзменении(Элемент)
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	ЗаполнитьСоставНаСервере(ТекСтрока.Номенклатура,ТекСтрока.Количество,ТекСтрока.Коэффициент,ТекСтрока.ТехнологическаяКарта,ТекСтрока.ПоТехКарте,ТекСтрока.Ключ,ТекСтрока.ПлановаяСтоимость,ТекСтрока.ПлановаяСтоимостьНДС,ТекСтрока.СуммаПлановая,ТекСтрока.СуммаНДСПлановая);
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияТехнологическаяКартаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	СтандартнаяОбработка = Ложь;
	Если Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Разделка") Тогда
		ВидПроизводстваЗначение = Новый СписокЗначений();
		ВидПроизводстваЗначение.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Разделка"));
		ВидПроизводстваЗначение.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Приготовление"));
	Иначе	
		ВидПроизводстваЗначение = Объект.ВидПроизводства;
	КонецЕсли;
	СтрукОтбор     = Новый Структура("ВидПроизводства,Продукция",ВидПроизводстваЗначение,ТекСтрока.Номенклатура);
	СтрукПараметры = Новый Структура("Отбор,ТекущаяСтрока",СтрукОтбор,ТекСтрока.ТехнологическаяКарта);
	ФормаВыбораТехКарты = ПолучитьФорму("Документ.ТехнологическаяКарта.ФормаВыбора",СтрукПараметры,Элемент);
	ФормаВыбораТехКарты.Открыть();
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияТехнологическаяКартаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТехКартаНеДопустимая(ВыбранноеЗначение) Тогда  
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ТехКартаНеДопустимая(ТехКарта)
	
	Если Объект.ВидПроизводства = Перечисления.ВидыПроизводства.Разделка И ТехКарта.ВидПроизводства = Перечисления.ВидыПроизводства.Приготовление И НЕ ТехКарта.ВозможнаРазделка Тогда
		Текст = "Выбрана технологическая карта с Видом производства - Приготовление и с отключенным флагом возможна разделка!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,Объект.Ссылка);
		Возврат Истина;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПродукцияПоЦехам.ФлагАктивности
		|ИЗ
		|	РегистрСведений.ПродукцияПоЦехам КАК ПродукцияПоЦехам
		|ГДЕ
		|	ПродукцияПоЦехам.ТехнологическаяКарта = &ТехнологическаяКарта
		|	И ПродукцияПоЦехам.Склад = &Склад
		|	И ПродукцияПоЦехам.Продукция = &Продукция";

	Запрос.УстановитьПараметр("Продукция", ТехКарта.Продукция);
	Запрос.УстановитьПараметр("Склад", Объект.СкладПроизводства);
	Запрос.УстановитьПараметр("ТехнологическаяКарта", ТехКарта);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	ВыборкаДетальныеЗаписи.Следующий();
	
	Если ВыборкаДетальныеЗаписи.ФлагАктивности = Истина Тогда
		Возврат Ложь;
	Иначе	
		Текст = "Выбрана технологическая карта не активна!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,Объект.Ссылка);
		Возврат Истина;
	КонецЕсли;	
КонецФункции	

&НаСервере
Процедура ЗаполнитьСоставНаСервере(Номенклатура,Количество,Коэффициент,ТехнологическаяКарта,ПоТехКарте,Ключ,ПлановаяСтоимость,ПлановаяСтоимостьНДС,СуммаПлановая,СуммаНДСПлановая)
	
	ОбъектДокумент = РеквизитФормыВЗначение("Объект");
	Если флВыпускПродукцииПоПлановымЦенам Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПлановаяСебестоимостьПродукции.Себестоимость КАК Цена,
		|	ПлановаяСебестоимостьПродукции.СебестоимостьНДС КАК ЦенаНДС
		|ИЗ
		|	РегистрСведений.ПлановаяСебестоимостьПродукции.СрезПоследних(&Дата, Продукция = &Номенклатура) КАК ПлановаяСебестоимостьПродукции");
		Запрос.УстановитьПараметр("Дата",Объект.Дата);
		Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПлановаяСтоимость = Выборка.Цена;
			ПлановаяСтоимостьНДС = Выборка.ЦенаНДС;
		Иначе
			ПлановаяСтоимость = 0;
			ПлановаяСтоимостьНДС = 0;
		КонецЕсли;	
		СуммаПлановая = Окр(Количество * ПлановаяСтоимость,2);
		//popn+
		СуммаНДСПлановая = Окр(Количество * ПлановаяСтоимостьНДС,2);
		//popn-
	КонецЕсли;	
	
	ОбъектДокумент.ЗаполнитьСостав(Номенклатура,Количество,Коэффициент,ТехнологическаяКарта,ПоТехКарте,Ключ,флВыпускПродукцииПоПлановымЦенам);
	
	ЗначениеВРеквизитФормы(ОбъектДокумент,"Объект");
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриЗакрытии()
	СохранитьПараметрыПодбора();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
КонецПроцедуры

&НаКлиенте
Процедура СоставПриИзменении(Элемент)
	ТекСтрокаСостав = Элементы.Состав.ТекущиеДанные;
	ТекСтрокаПродукция = Элементы.Продукция.ТекущиеДанные;
	Если ТекСтрокаСостав<>Неопределено и ТекСтрокаПродукция<>Неопределено тогда
	ТекСтрокаСостав.Ключ = ТекСтрокаПродукция.Ключ;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	РаботаСФормамиКлиент.ПроверкаДатыРедактирования(Объект);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПодбора(Команда)
	ПараметрыПодбора = ОткрытьФормуМодально("ОбщаяФорма.НастройкаПодбора", Новый Структура("ЗапрашиватьКоличество", ЗапрашиватьКоличество));
	ЗапрашиватьКоличество = ПараметрыПодбора.ЗапрашиватьКоличество;
КонецПроцедуры

&НаКлиенте
Процедура СкладПроизводстваПриИзменении(Элемент)
	УстановитьПараметры();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТехнологическуюКарту(Команда)
	
	ТекСтрока = Элементы.Продукция.ТекущиеДанные;
	Если НЕ ТекСтрока.ТехнологическаяКарта.Пустая() Тогда
		СтруктураПараметров = Новый Структура("Ключ", ТекСтрока.ТехнологическаяКарта);
		ОткрытьФорму("Документ.ТехнологическаяКарта.Форма.ФормаДокумента",СтруктураПараметров,ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "СоставНоменклатура" Тогда
		
		//если разделку с ингредиентом делаем на основании документа производства -
		//надо запретить ручной подбор аналогов, потому что аналоги будут определяться 
		//при проведении, то есть приходоваться при разделке будут те же аналоги,
		//которые были списаны при производстве документом-основанием.
		
		Если Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Разделка")
			И ТипЗнч(Объект.ДокОснование) = Тип("ДокументСсылка.ГотоваяПродукция") Тогда
			Сообщить("Запрещен ручной подбор аналогов для режима ""Разделка"". Будут использованы аналоги из документа-основания.");
			Возврат;
		КонецЕсли;
		
		флПриготовление = Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Приготовление");
		
		ТекСтрока = Элемент.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекСтрока.Ингредиент) Тогда
			сзАналогов = ВернутьСписокАналоговИнгредиента(ТекСтрока.Ингредиент);
			СтандартнаяОбработка = ЛОЖЬ;
			ФормаВыбора = ПолучитьФорму("Справочник.Номенклатура.ФормаВыбора",,Элемент);
			Отбор = ФормаВыбора.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Отбор.ЛевоеЗначение = ФормаВыбора.Список.Отбор.ДоступныеПоляОтбора.Элементы.Найти("Ссылка").Поле;
			Отбор.Использование = Истина;
			Отбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
			Отбор.ПравоеЗначение = сзАналогов;
			Отбор.Представление = "Все аналоги данной номенклатуры";
			ВыбраннаяНоменклатура = ФормаВыбора.ОткрытьМодально();
			
			Если НЕ флПриготовление Тогда
				Если ВыбраннаяНоменклатура = НЕОПРЕДЕЛЕНО Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			ТекСтрока.Номенклатура = ВыбраннаяНоменклатура;
			ТекСтрокаПродукция = Элементы.Продукция.ТекущиеДанные;
			Если ЗначениеЗаполнено(ТекСтрокаПродукция.ТехнологическаяКарта) Тогда
				
				КоэффициентЗамены = ВернутьКоэффициентИнгредиента(ТекСтрока.Ингредиент,ТекСтрока.Номенклатура);
				СтруктураТК = ВернутьДанныеТехКарты(ТекСтрокаПродукция.ТехнологическаяКарта,ТекСтрока.Ингредиент);
				
				Если СтруктураТК = НЕОПРЕДЕЛЕНО Тогда
					чКоличествоПоТехКарте = 0;
					чКоличествоПродукции  = 0;
				Иначе
					чКоличествоПоТехКарте = СтруктураТК.Количество / СтруктураТК.КоличествоПорций;
					чКоличествоПродукции = Окр(ТекСтрокаПродукция.Количество * ТекСтрокаПродукция.Коэффициент,4);
					ТекСтрока.Коэффициент = СтруктураТК.Коэффициент;
					ТекСтрока.ЕдиницаИзмерения = СтруктураТК.ЕдиницаИзмерения;
				КонецЕсли;
				
				ТекСтрока.Количество = Окр(чКоличествоПродукции * чКоличествоПоТехКарте * КоэффициентЗамены,4);
				
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВернутьСписокАналоговИнгредиента(Ингредиент)
	
	Возврат Ингредиент.Состав.ВыгрузитьКолонку("Номенклатура");	
	
КонецФункции

&НаСервере
Функция ВернутьКоэффициентИнгредиента(Ингредиент,Номенклатура)
	
	СтрокаИнгредиента = Ингредиент.Состав.Найти(Номенклатура,"Номенклатура");	
	Если СтрокаИнгредиента = НЕОПРЕДЕЛЕНО Тогда
		КоэффициентЗамены = 1;
	Иначе
		КоэффициентЗамены = СтрокаИнгредиента.КоэффициентЗамены;
	КонецЕсли;
	
	Возврат КоэффициентЗамены;
	
КонецФункции

&НаСервере
Функция ВернутьДанныеТехКарты(ТехнологическаяКарта,Ингредиент);
	
	СоставТехКарты = ТехнологическаяКарта.Состав;
	СтрокаТехКартыСЭтимИнгредиентом = СоставТехКарты.Найти(Ингредиент, "Номенклатура");	
	
	Если СтрокаТехКартыСЭтимИнгредиентом <> НЕОПРЕДЕЛЕНО Тогда
		СтруктураТК = Новый Структура;
		СтруктураТК.Вставить("Количество",СтрокаТехКартыСЭтимИнгредиентом.Количество);
		СтруктураТК.Вставить("Коэффициент",СтрокаТехКартыСЭтимИнгредиентом.Коэффициент);
		СтруктураТК.Вставить("ЕдиницаИзмерения",СтрокаТехКартыСЭтимИнгредиентом.ЕдиницаИзмерения);
		СтруктураТК.Вставить("КоличествоПорций",ТехнологическаяКарта.КоличествоПорций);
		Возврат СтруктураТК;
	Иначе 
		Возврат НЕОПРЕДЕЛЕНО;	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура Заполнить(Команда)
	Если Объект.Продукция.Количество() > 0 Тогда
		ТекстВопроса = "Табличная часть будет очищена.
		|Продолжить?";
		Ответ = Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Объект.Продукция.Очистить();
			Объект.Состав.Очистить();
		Иначе	
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	//ДокументОбъект = ДанныеФормыВЗначение(Объект,Тип("ДокументОбъект.ГотоваяПродукция"));
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	//Если ДокументОбъект.ЭтоНовый() Тогда
	//	ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	//КонецЕсли;	
	ДокументОбъект.ЗаполнитьПоПланамИЗаказам();
	ЗначениеВДанныеФормы(ДокументОбъект,Объект);
КонецПроцедуры
