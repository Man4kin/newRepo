
Процедура ПриКопировании(ОбъектКопирования)
	Автор = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Если  ЗначениеЗаполнено(Контрагент.Валюта) Тогда
			Валюта = Контрагент.Валюта;
		Иначе
			Валюта = СтруктурныеЕдиницы.ПолучитьВалютуТекущейСЕ();
		КонецЕсли;
		
		СтруктураКурса = РаботаСКурсамиВалют.ЗаполнитьДанныеКурсаДляВалюты(Валюта);
		Курс = СтруктураКурса.Курс;
		Кратность = СтруктураКурса.Кратность;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) И НЕ ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница.ЭтоГруппа Тогда
		СтруктурнаяЕдиница = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипОтчета) Тогда
		ТипОтчета = Перечисления.ТипыОтчетаРеализатора.Комитенту;
	КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда
		СтавкаНДС = ОбщегоНазначенияСервер.ПолучитьОсновнуюСтавкуНДС();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Отказ = ПровестиОтчетРеализатора(ЭтотОбъект,РежимПроведения);
КонецПроцедуры


Функция ПровестиОтчетРеализатора(Объект,Режим)
	
	Если Объект.ТипОтчета = Перечисления.ТипыОтчетаРеализатора.Комитенту Тогда
		тзРеализация = Объект.Движения.Реализация.Выгрузить();
		
		// Установка исключительной блокировки контролируемых остатков 
		Запрос=Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ОтчетРеализатораСостав.Номенклатура,
		|	ОтчетРеализатораСостав.Ссылка.Контрагент КАК Комитент,
		|	ОтчетРеализатораСостав.Ссылка.Фирма КАК Фирма,
		|	ОтчетРеализатораСостав.ПоДокументу КАК ПоДокументу,
		|	ОтчетРеализатораСостав.КоличествоОст,
		|	ОтчетРеализатораСостав.Количество
		|ПОМЕСТИТЬ врСостав
		|ИЗ
		|	Документ.ОтчетРеализатора.Состав КАК ОтчетРеализатораСостав
		|ГДЕ
		|	ОтчетРеализатораСостав.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетРеализатораСостав.Номенклатура,
		|	ОтчетРеализатораСостав.Ссылка.Контрагент,
		|	ОтчетРеализатораСостав.Ссылка.Фирма,
		|	ОтчетРеализатораСостав.ПоДокументу,
		|	ОтчетРеализатораСостав.КоличествоОст,
		|	ОтчетРеализатораСостав.Количество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	врСостав.Номенклатура,
		|	врСостав.Комитент,
		|	врСостав.Фирма,
		|	врСостав.ПоДокументу
		|ИЗ
		|	врСостав КАК врСостав";
		Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
		тз=Запрос.Выполнить().Выгрузить();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Реализация");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = тз;
		Для каждого к из тз.Колонки Цикл
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных(к.Имя, к.Имя);
		КонецЦикла;
		Блокировка.Заблокировать();
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	врСостав.Номенклатура КАК Номенклатура,
		|	врСостав.Комитент КАК Комитент,
		|	врСостав.Фирма КАК Фирма,
		|	врСостав.ПоДокументу КАК ПоДокументу,
		|	врСостав.КоличествоОст КАК КоличествоОст,
		|	врСостав.Количество КАК Количество,
		|	о.КоличествоОстаток КАК КоличествоОстРегистр,
		|	о.СуммаОстаток КАК Сумма,
		|	о.СуммаПродажиОстаток КАК СуммаПродажи,
		|	о.СуммаНДСПродажиОстаток КАК СуммаНДСПродажи
		|ИЗ
		|	врСостав КАК врСостав
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Реализация.Остатки(
		|				&Момент,
		|				(Фирма, Номенклатура, Комитент, ПоДокументу) В
		|					(ВЫБРАТЬ
		|						с.Фирма,
		|						с.Номенклатура,
		|						с.Комитент,
		|						с.ПоДокументу
		|					ИЗ
		|						врСостав КАК с)) КАК о
		|		ПО врСостав.Номенклатура = о.Номенклатура
		|			И врСостав.ПоДокументу = о.ПоДокументу
		|			И врСостав.Фирма = о.Фирма
		|			И врСостав.Комитент = о.Комитент";
		Запрос.УстановитьПараметр("Момент",Объект.Дата);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КоличествоОст <> Выборка.КоличествоОстРегистр Тогда
				Сообщить("Данные в документе не актуальны! Необходимо перезаполнить документ!");
				Возврат Истина;
			КонецЕсли;	
			
			Движение = тзРеализация.Добавить();
			Движение.ВидДвижения     = ВидДвиженияНакопления.Расход;
			Движение.Активность      = истина;
			Движение.Регистратор     = Объект.Ссылка;
			Движение.Период          = Объект.Дата;
			Движение.Фирма           = Выборка.Фирма;
			Движение.Номенклатура    = Выборка.Номенклатура;
			Движение.Комитент        = Выборка.Комитент;
			Движение.ПоДокументу     = Выборка.ПоДокументу;
			ТекКол = Выборка.Количество;
			Движение.Количество      = ТекКол;
			к=ТекКол / Выборка.КоличествоОст;
			Движение.Сумма           = Окр(к * Выборка.Сумма,2);
			Движение.СуммаПродажи    = Окр(к * Выборка.СуммаПродажи,2);
			Движение.СуммаНДСПродажи = Окр(к * Выборка.СуммаНДСПродажи,2);
			
		КонецЦикла;	
		
		Объект.Движения.Реализация.Загрузить(тзРеализация);
		Объект.Движения.Реализация.Записывать=Истина;
	Иначе
		тзТоварыПереданныеНаКомиссию = Объект.Движения.ТоварыПереданныеНаКомиссию.Выгрузить();
		
		// Установка исключительной блокировки контролируемых остатков 
		Запрос=Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ОтчетРеализатораСостав.Номенклатура,
		|	ОтчетРеализатораСостав.Ссылка.Контрагент КАК Комиссионер,
		|	ОтчетРеализатораСостав.Ссылка.Фирма КАК Фирма,
		|	ОтчетРеализатораСостав.КоличествоОст,
		|	ОтчетРеализатораСостав.Количество
		|ПОМЕСТИТЬ врСостав
		|ИЗ
		|	Документ.ОтчетРеализатора.Состав КАК ОтчетРеализатораСостав
		|ГДЕ
		|	ОтчетРеализатораСостав.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетРеализатораСостав.Номенклатура,
		|	ОтчетРеализатораСостав.Ссылка.Контрагент,
		|	ОтчетРеализатораСостав.Ссылка.Фирма,
		|	ОтчетРеализатораСостав.КоличествоОст,
		|	ОтчетРеализатораСостав.Количество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	врСостав.Номенклатура,
		|	врСостав.Комиссионер,
		|	врСостав.Фирма
		|ИЗ
		|	врСостав КАК врСостав";
		Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
		тз=Запрос.Выполнить().Выгрузить();
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыПереданныеНаКомиссию");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = тз;
		Для каждого к из тз.Колонки Цикл
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных(к.Имя, к.Имя);
		КонецЦикла;
		Блокировка.Заблокировать();
		
		Запрос.Текст=
		"ВЫБРАТЬ
		|	врСостав.Номенклатура КАК Номенклатура,
		|	врСостав.Комиссионер КАК Комиссионер,
		|	врСостав.Фирма КАК Фирма,
		|	врСостав.КоличествоОст КАК КоличествоОст,
		|	врСостав.Количество КАК Количество,
		|	о.КоличествоОстаток КАК КоличествоОстРегистр,
		|	о.СуммаОстаток КАК Сумма,
		|	о.СуммаУпрОстаток КАК СуммаУпр,
		|	о.СуммаНДСОстаток КАК СуммаНДС,
		|	о.СуммаНДСУпрОстаток КАК СуммаНДСУпр,
		|	о.СуммаПродажиОстаток КАК СуммаПродажи,
		|	о.СуммаНДСПродажиОстаток КАК СуммаНДСПродажи
		|ИЗ
		|	врСостав КАК врСостав
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(
		|				&Момент,
		|				(Фирма, Номенклатура, Комиссионер) В
		|					(ВЫБРАТЬ
		|						с.Фирма,
		|						с.Номенклатура,
		|						с.Комиссионер
		|					ИЗ
		|						врСостав КАК с)) КАК о
		|		ПО врСостав.Номенклатура = о.Номенклатура
		|			И врСостав.Фирма = о.Фирма
		|			И врСостав.Комиссионер = о.Комиссионер";
		Запрос.УстановитьПараметр("Момент",Объект.Дата);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КоличествоОст <> Выборка.КоличествоОстРегистр Тогда
				Сообщить("Данные в документе не актуальны! Необходимо перезаполнить документ!");
				Возврат Истина;
			КонецЕсли;	
			
			Движение = тзТоварыПереданныеНаКомиссию.Добавить();
			Движение.ВидДвижения     = ВидДвиженияНакопления.Расход;
			Движение.Активность      = истина;
			Движение.Регистратор     = Объект.Ссылка;
			Движение.Период          = Объект.Дата;
			Движение.Фирма           = Выборка.Фирма;
			Движение.Номенклатура    = Выборка.Номенклатура;
			Движение.Комиссионер     = Выборка.Комиссионер;
			ТекКол = Выборка.Количество;
			Движение.Количество      = ТекКол;
			к=ТекКол / Выборка.КоличествоОст;
			Движение.Сумма           = Окр(к * Выборка.Сумма,2);
			Движение.СуммаУпр        = Окр(к * Выборка.СуммаУпр,2);
			Движение.СуммаНДС        = Окр(к * Выборка.СуммаНДС,2);
			Движение.СуммаНДСУпр     = Окр(к * Выборка.СуммаНДСУпр,2);
			Движение.СуммаПродажи    = Окр(к * Выборка.СуммаПродажи,2);
			Движение.СуммаНДСПродажи = Окр(к * Выборка.СуммаНДСПродажи,2);
			
		КонецЦикла;	
		
		Объект.Движения.ТоварыПереданныеНаКомиссию.Загрузить(тзТоварыПереданныеНаКомиссию);
		Объект.Движения.ТоварыПереданныеНаКомиссию.Записывать=Истина;
		
		
	КонецЕсли;	
	Возврат Ложь;
КонецФункции

