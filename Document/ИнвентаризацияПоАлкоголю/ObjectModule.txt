
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	УчетАлкоголя = УчетАлкогольнойПродукции.ВестиУчетАлкоголя();
	
	Если НЕ УчетАлкоголя Тогда
		ТекстСообщения = НСтр("ru='Для данной структурной единицы не настроено ведение учета алкоголя по производителям. Ввод документа невозможен.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Источник") И ДанныеЗаполнения.Источник = "ДанныеТСД" Тогда
			Для каждого сСостава Из Состав Цикл
				сСостава.Количество = 0;
			КонецЦикла;
			
			Для каждого эл Из ДанныеЗаполнения.Состав Цикл	//массив структур
				НайденныеСтроки = Состав.НайтиСтроки(Новый Структура("Номенклатура", эл.Номенклатура));
				Если НайденныеСтроки.Количество() = 0 Тогда
					сСостава = Состав.Добавить();
					ЗаполнитьЗначенияСвойств(сСостава, эл);//ключи Эл - "Номенклатура, ЕдиницаИзмерения, Количество, Коэффициент, Цена, Сумма"
				Иначе
					сСостава = НайденныеСтроки[0];
					сСостава.Количество = эл.Количество;
				КонецЕсли;
				сСостава.КоличествоРазница = сСостава.Количество - сСостава.КоличествоУчет;
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Инвентаризация") Тогда
		ТекстСообщения = НСтр("ru='Для оформления инвентаризации по алкоголю используйте пункт меню ""Создать инвентаризацию по алкоголю"".'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	СтруктурнаяЕдиница = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	ДокОснование = Неопределено;
	ТолькоТаблица = Истина;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	УчетАлкоголя = УчетАлкогольнойПродукции.ВестиУчетАлкоголя();
	Если УчетАлкоголя Тогда
		Отказ = УчетАлкогольнойПродукции.ПровестиИнвентаризациюПоАлкоголю(ЭтотОбъект,РежимПроведения);
	Иначе
		УчетАлкогольнойПродукции.ОчиститьДвиженияПоУчетуАлкоголя(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//{ СИТЕК БрыляковЕЮ 2015-04-30  
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	// СИТЕК БрыляковЕЮ 2015-04-30 }
	
	АктуализироватьУчетноеКоличество(РежимЗаписи, РежимПроведения);
КонецПроцедуры

Процедура АктуализироватьУчетноеКоличество(РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		//не актуализируем
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИнвентаризацияСоставПоПроизводителям.Номенклатура,
	|	ИнвентаризацияСоставПоПроизводителям.КоличествоУчет КАК КоличествоУчет,
	|	ИнвентаризацияСоставПоПроизводителям.ВидАлкогольнойПродукции,
	|	ИнвентаризацияСоставПоПроизводителям.Производитель,
	|	&Фирма,
	|	&СтруктурнаяЕдиница
	|ПОМЕСТИТЬ Состав
	|ИЗ
	|	&ИнвентаризацияСоставПоПроизводителям КАК ИнвентаризацияСоставПоПроизводителям
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетАлкоголяПоПроизводителямОстатки.Номенклатура,
	|	УчетАлкоголяПоПроизводителямОстатки.СтруктурнаяЕдиница,
	|	УчетАлкоголяПоПроизводителямОстатки.Фирма,
	|	УчетАлкоголяПоПроизводителямОстатки.ВидАлкогольнойПродукции,
	|	УчетАлкоголяПоПроизводителямОстатки.Производитель,
	|	СУММА(УчетАлкоголяПоПроизводителямОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрНакопления.УчетАлкоголяПоПроизводителям.Остатки(
	|			&МоментВремени,
	|			(Фирма, Номенклатура, СтруктурнаяЕдиница) В
	|				(ВЫБРАТЬ
	|					Состав.Фирма,
	|					Состав.Номенклатура,
	|					Состав.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|				ИЗ
	|					Состав КАК Состав)) КАК УчетАлкоголяПоПроизводителямОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	УчетАлкоголяПоПроизводителямОстатки.Номенклатура,
	|	УчетАлкоголяПоПроизводителямОстатки.СтруктурнаяЕдиница,
	|	УчетАлкоголяПоПроизводителямОстатки.ВидАлкогольнойПродукции,
	|	УчетАлкоголяПоПроизводителямОстатки.Производитель,
	|	УчетАлкоголяПоПроизводителямОстатки.Фирма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Состав.Номенклатура,
	|	Состав.СтруктурнаяЕдиница,
	|	Состав.ВидАлкогольнойПродукции,
	|	Состав.Производитель,
	|	Состав.Фирма,
	|	Состав.КоличествоУчет,
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	Состав КАК Состав
	|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
	|		ПО Состав.Номенклатура = Остатки.Номенклатура
	|			И Состав.СтруктурнаяЕдиница      = Остатки.СтруктурнаяЕдиница
	|			И Состав.ВидАлкогольнойПродукции = Остатки.ВидАлкогольнойПродукции
	|			И Состав.Производитель    = Остатки.Производитель
	|			И Состав.Фирма            = Остатки.Фирма");
	Запрос.УстановитьПараметр("ИнвентаризацияСоставПоПроизводителям",ЭтотОбъект.СоставПоПроизводителям.Выгрузить(,"Номенклатура, ВидАлкогольнойПродукции, Производитель, КоличествоУчет"));
	Запрос.УстановитьПараметр("Фирма",ЭтотОбъект.Фирма);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница",ЭтотОбъект.СтруктурнаяЕдиница);
	
	МоментВремениЗапроса = Новый МоментВремени(ЭтотОбъект.Дата,ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("МоментВремени",МоментВремениЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоУчет <> Выборка.КоличествоОстаток Тогда
			Сообщить("По номенклатуре ("+Выборка.Номенклатура +"), производителю ("+Выборка.Производитель+") учетное количество не соответствует текущим остаткам! Табличная часть будет перезаполнена!",СтатусСообщения.Обычное);
			мсв = ЭтотОбъект.СоставПоПроизводителям.НайтиСтроки(Новый Структура("Номенклатура,ВидАлкогольнойПродукции,Производитель",Выборка.Номенклатура,Выборка.ВидАлкогольнойПродукции,Выборка.Производитель));
			
			Если мсв.Количество() > 0 Тогда
				чРазница = мсв[0].КоличествоУчет - Выборка.КоличествоОстаток;
				мсв[0].КоличествоУчет = Выборка.КоличествоОстаток;
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры	
