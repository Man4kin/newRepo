////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//++Минин 0000144434 2015-11-06
	ДокументСсылка = УчетНДС.ПолучитьИсправляемыйДокументПоступления(Объект.ДокументОснование, Ложь);
	ДокументОснование = Документы.КорректировкаПоступления.ПолучитьПоследнийКорректирующийДокумент(ДокументСсылка, Объект.ДокументОснование);
	Если Объект.ДокументОснование <> ДокументОснование 
		И Объект.ДокументОснование.Дата < ДокументОснование.Дата Тогда
		ПоследнийДокументИсправления = ДокументОснование;
	КонецЕсли;
	//--Минин 0000144434 2015-11-06
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	ТолькоПросмотр = РаботаСФормамиСервер.ЗапрещеноРедактировать(Объект.Ссылка,ЭтаФорма);
	
	//{БредовЮГ 2014-12-03 Наряд№000135628
	Элементы.Izh_ОбработанЦВД.Видимость = ПараметрыСеанса.ЭтоЦентр;
	Izh_ОбработанЦВД = РегистрыСведений.Izh_СостояниеДокумента.ДокументЗафиксированВСостоянии(Объект,Перечисления.Izh_СостояниеДокумента.ПроверенЦВД);
	//БредовЮГ 2014-12-03 Наряд№000135628}
	
	//Если НЕ ПараметрыСеанса.ЭтоЦентр Тогда
	//	Элементы.ОбработанЦВД.Доступность = Ложь;
	//	Элементы.ОбработанЦВД.Видимость = ПараметрыСеанса.ИспользованиеЦВД;
	//КонецЕсли;	
	ОбщегоНазначенияСервер.ЗапретМодификацииОбъектов(ЭтаФорма);
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ
&НаКлиенте
Процедура ИсправлениеПриИзменении(Элемент)
	
	ИсправлениеПриИзмененииНаСервере();
	
	Если НЕ Объект.Исправление Тогда
		Объект.НомерИсправления	= 0;
		Объект.ДатаИсправления	= '00010101';
	Иначе
		Объект.НДСПредъявленКВычету = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийКод = Элемент.СписокВыбора.НайтиПоЗначению(Объект.КодВидаОперации);
	ВыбранныйКод = ВыбратьИзСписка(Элемент.СписокВыбора, Элемент, ТекущийКод);
	Если ВыбранныйКод <> Неопределено Тогда
		Объект.КодВидаОперации = ВыбранныйКод.Значение;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ДокументыОснования>

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УчетНДС.ЗаполнитьСписокКодовВидовОпераций(Истина, ЭтаФорма.Элементы.КодВидаОперации.СписокВыбора);
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;
	
	Элементы.НомерИсправления.Доступность	= Объект.Исправление;
	Элементы.ДатаИсправления.Доступность	= Объект.Исправление;
	
	ВычетНеОтражается = Объект.Исправление;
	Элементы.НДСПредъявленКВычету.Доступность  = НЕ ВычетНеОтражается;
	
КонецПроцедуры

&НаСервере
Процедура ИсправлениеПриИзмененииНаСервере()

	УстановитьКодВидаОперацииНаСервере();

КонецПроцедуры

&НаСервере
Процедура УстановитьКодВидаОперацииНаСервере()
	
	Объект.КодВидаОперации= "01";
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДокументаОснованияНаСервере()
	
	УстановитьКодВидаОперацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Перезаполнить(Команда)
	ПерезаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьНаСервере()
	
	ЭтотОбъект = ДанныеФормыВЗначение(Объект,Тип("ДокументОбъект.РегистрацияСчетФактуры"));
	ЭтотОбъект.Заполнить(Объект.ДокументОснование);
	ЗначениеВДанныеФормы(ЭтотОбъект,Объект);
	
КонецПроцедуры	

//++Минин 0000144434 2015-11-06
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.Ссылка.Пустая() И ЗначениеЗаполнено(ПоследнийДокументИсправления) Тогда
		ТекстВопроса = НСтр("ru = 'Для указанного документа существуют более поздние корректировки.
								|Использовать последнюю введенную корректировку?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Объект.ДокументОснование = ПоследнийДокументИсправления;
			ПерезаполнитьНаСервере();
		Иначе 
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//--Минин 0000144434 2015-11-06
