Функция ПолучитьДопСвойство(Номенкл,Свойство)
	СтрокаСвойства=Номенкл.ДополнительныеРеквизиты.Найти(Свойство, "Свойство");
	ЗначСвойства="";
	Если СтрокаСвойства<>Неопределено Тогда
		ЗначСвойства=СтрокаСвойства.Значение.Наименование;
	КонецЕсли;
	
	Если СокрП(Свойство.Код)="10" Тогда
		Возврат Справочники.ПроизводителиВводимые.НайтиПоНаименованию(ЗначСвойства,Истина);
	ИначеЕсли СокрП(Свойство.Код)="22" Тогда
		Возврат Справочники.КлассификаторСтранМира.НайтиПоНаименованию(ЗначСвойства,Истина);
	КонецЕсли;
	
КонецФункции
 
Функция ПолучитьПоследниеДанныеНоменклатуры(Номенкл)
	Рез_т=Новый Структура;
	
	Запрос=Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	Izh_ВетДокументы.Изготовитель,
	|	Izh_ВетДокументы.Страна,
	|	Izh_ВетДокументы.ВетКатегория
	|ИЗ
	|	РегистрСведений.Izh_ВетДокументы КАК Izh_ВетДокументы
	|ГДЕ
	|	Izh_ВетДокументы.Номенклатура = &Номенкл
	|
	|УПОРЯДОЧИТЬ ПО
	|	Izh_ВетДокументы.Период УБЫВ");
	
	Запрос.УстановитьПараметр("Номенкл",Номенкл);
	Рез=Запрос.Выполнить();
	Если Не Рез.Пустой() Тогда
		Выбр=Рез.Выбрать();
		Выбр.Следующий();
		Рез_т.Вставить("Изготовитель",Выбр.Изготовитель);
		Рез_т.Вставить("Страна",Выбр.Страна);
		Рез_т.Вставить("ВетКатегория",Выбр.ВетКатегория);
	КонецЕсли;
	
	Возврат Рез_т;	
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВетДокументы.Ссылка КАК ВетДокумент
	                      |ИЗ
	                      |	Документ.ВетеринарныйДокумент КАК ВетДокументы
	                      |ГДЕ
	                      |	ВетДокументы.ДокОснование = &ПН");
						  
	Запрос.УстановитьПараметр("ПН",ДанныеЗаполнения);
	выбрВетДок=Запрос.Выполнить().Выбрать();
	Пока выбрВетДок.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Уже имеется ветеринарный документ "+Строка(выбрВетДок.ВетДокумент));
	КонецЦикла; 
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	ДокОснование = ДанныеЗаполнения;
		
	Для каждого СтрСостав Из ДанныеЗаполнения.Состав Цикл
		СтрНоменкл=Номенклатура.Добавить();
		СтрНоменкл.Номенклатура=СтрСостав.Номенклатура;
		Если ТипЗнч(ДанныеЗаполнения.Контрагент)=Тип("СправочникСсылка.Контрагенты") Тогда
			СтрНоменкл.Поставщик=ДанныеЗаполнения.Контрагент;
		КонецЕсли; 
		
		ДаннНоменкл=ПолучитьПоследниеДанныеНоменклатуры(СтрСостав.Номенклатура);
		Если ЗначениеЗаполнено(ДаннНоменкл) Тогда
			СтрНоменкл.Изготовитель=ДаннНоменкл.Изготовитель;
			СтрНоменкл.Страна=ДаннНоменкл.Страна;
			СтрНоменкл.ВетКатегория=ДаннНоменкл.ВетКатегория;
		Иначе
			СтрНоменкл.Изготовитель=ПолучитьДопСвойство(СтрСостав.Номенклатура,ПланыВидовХарактеристик.ДополнительныеРеквизитыНоменклатуры.НайтиПоКоду(10));
			СтрНоменкл.Страна=ПолучитьДопСвойство(СтрСостав.Номенклатура,ПланыВидовХарактеристик.ДополнительныеРеквизитыНоменклатуры.НайтиПоКоду(22));
		КонецЕсли;	
			
		СтрНоменкл.Вес=СтрСостав.Вес*?(ЗначениеЗаполнено(СтрСостав.ЕдиницаВеса),СтрСостав.ЕдиницаВеса.Коэффициент,1);
	КонецЦикла;
	
	Вес=Номенклатура.Итог("Вес");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Для каждого СтрНоменклатура Из Номенклатура Цикл
		Izh_ВетДокументы=Движения.Izh_ВетДокументы.Добавить();
		Izh_ВетДокументы.ДокОсн 				= ДокОснование;
		Izh_ВетДокументы.Период 				= Дата;
		Izh_ВетДокументы.ВетСвидетельство 		= ВетСвидетельство;
		Izh_ВетДокументы.ДатаВетСвидетельства 	= ДатаВетСвидетельства;
		Izh_ВетДокументы.ВетСправка 			= ВетСправка;
		Izh_ВетДокументы.ДатаВетСправки			= ДатаВетСправки;
		Izh_ВетДокументы.Исследования 			= Исследования;
		Izh_ВетДокументы.Номенклатура 			= СтрНоменклатура.Номенклатура;
		Izh_ВетДокументы.Изготовитель 			= СтрНоменклатура.Изготовитель;
		Izh_ВетДокументы.Страна 				= СтрНоменклатура.Страна;
		Izh_ВетДокументы.Маркировка 			= СтрНоменклатура.Маркировка;
		Izh_ВетДокументы.Поставщик 				= СтрНоменклатура.Поставщик;
		Izh_ВетДокументы.ВетКатегория 			= СтрНоменклатура.ВетКатегория;
		Izh_ВетДокументы.Вес 					= СтрНоменклатура.Вес;
	КонецЦикла;
	Движения.Izh_ВетДокументы.Записывать=Истина;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ОтправкаRMS",Истина);
КонецПроцедуры
