
&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПоместитьИтоговуюТаблицуВоВременноеХранилище()
	
	ТСотр = РеквизитФормыВЗначение("ТаблицаСотрудников");
			
	Отбор = Новый Структура("Вкл", Истина);
	ПомеченныеСтроки = ТСотр.НайтиСтроки(Отбор);
	РезультирующаяТаблица = ТСотр.Скопировать(ПомеченныеСтроки);
	
	Возврат ПоместитьВоВременноеХранилище(РезультирующаяТаблица);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьСотрудников(Команда)
	
	АдресХранилища = ПоместитьИтоговуюТаблицуВоВременноеХранилище();	
	ЭтаФорма.Закрыть(АдресХранилища);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСотрудниковПодразделенияНаСервере(НомерМагазина)
	
	ТаблицаФормы = РеквизитФормыВЗначение("ТаблицаСотрудников");	
	
	XMLСтрокаРезультата	= "";
	СообщениеОшибки		= "";	
	
	Попытка		
		Отказ = Izh_ОбщегоНазначенияСервер.ПолучитьСотрудниковПодразделенияИзЗУП(НачалоМесяца(ПериодПерераспределения), 
																					НачалоМесяца(ПериодПерераспределения), 
																						КонецМесяца(ПериодПерераспределения), 
																							НомерМагазина, 
																								XMLСтрокаРезультата, 
																									СообщениеОшибки);
	Исключение
		СообщениеОшибки = ОписаниеОшибки();
		Сообщить(СообщениеОшибки);
		Возврат;
	КонецПопытки;
		
	Если НЕ Отказ  Тогда			
		ХТЗ = XMLЗначение(Тип("ХранилищеЗначения"), XMLСтрокаРезультата);	
		ТСотр = ХТЗ.Получить();
		
		Если ТипЗнч(ТСотр) = Тип("ТаблицаЗначений") Тогда		
			Для Каждого СтрокаТаб Из ТСотр Цикл
				
				Отбор = Новый Структура;
				Отбор.Вставить("КодСотрудника", СтрокаТаб.КодСотрудника);
				
				Если ТаблицаФормы.НайтиСтроки(Отбор).Количество() = 0 Тогда
					НоваяСтрока = ТаблицаФормы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаб);
					НоваяСтрока.КодПодразделения = СокрЛП(НомерМагазина);
				КонецЕсли;
				
			КонецЦикла;		
		КонецЕсли;
	Иначе
		Сообщить(СообщениеОшибки);
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ТаблицаФормы, "ТаблицаСотрудников");
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСотрудниковНаСервере()
		
	Если ЗначениеЗаполнено(НомерМагазина) Тогда
		ДобавитьСотрудниковПодразделенияНаСервере(НомерМагазина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСотрудников(Команда)
		
	ПолучитьСотрудниковНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПометки(Установить)
	
	Для Каждого СтрокаТаб Из ТаблицаСотрудников Цикл		
		СтрокаТаб.Вкл = Установить;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкиУстановить(Команда)
	
	ИзменитьПометки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкиСнять(Команда)
	
	ИзменитьПометки(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ЭтаФорма.ПериодПерераспределения = Параметры.Объект.ПериодПерераспределения;
	
КонецПроцедуры
