
Процедура ОбработкаУдаленияПроведения(Отказ)
	Отказ = Истина;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ДополнительныеСвойства.Вставить("ВводНаОсновании",Истина);
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТехнологическаяКарта") Тогда
		// Заполнение шапки
		ВидПроизводства = ДанныеЗаполнения.ВидПроизводства;
		КоличествоПорций = ДанныеЗаполнения.КоличествоПорций;
		МассаПорции = ДанныеЗаполнения.МассаПорции;
		НомерПоСправочнику = ДанныеЗаполнения.НомерПоСправочнику;
		Продукция = ДанныеЗаполнения.Продукция;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		Если ВидПроизводства = Перечисления.ВидыПроизводства.Приготовление Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.Номенклатура,
			|	ВложенныйЗапрос.Ингредиент,
			|	ИнгредиентыСостав.КоэффициентЗамены
			|ПОМЕСТИТЬ НоменклатураИнгредиентов
			|ИЗ
			|	(ВЫБРАТЬ
			|		МАКСИМУМ(ВложенныйЗапрос.Номенклатура) КАК Номенклатура,
			|		ВложенныйЗапрос.Ингредиент КАК Ингредиент
			|	ИЗ
			|		(ВЫБРАТЬ
			|			ИнгредиентыСостав.Номенклатура КАК Номенклатура,
			|			ИнгредиентыСостав.Ссылка КАК Ингредиент,
			|			НоменклатураКонтрагента.Цена КАК Цена,
			|			ИнгредиентыСостав.КоэффициентЗамены КАК КоэффициентЗамены
			|		ИЗ
			|			Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента
			|					ПО ИнгредиентыСостав.Номенклатура = НоменклатураКонтрагента.Номенклатура
			|						И (НоменклатураКонтрагента.ОсновнойПоставщик)
			|				ПО ТехнологическаяКартаСостав.Номенклатура = ИнгредиентыСостав.Ссылка
			|		ГДЕ
			|			ТехнологическаяКартаСостав.Ссылка = &Ссылка) КАК ВложенныйЗапрос
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|				ВложенныйЗапрос.Ингредиент КАК Ингредиент,
			|				МАКСИМУМ(ВложенныйЗапрос.Цена) КАК Цена
			|			ИЗ
			|				(ВЫБРАТЬ
			|					ИнгредиентыСостав.Номенклатура КАК Номенклатура,
			|					ИнгредиентыСостав.Ссылка КАК Ингредиент,
			|					НоменклатураКонтрагента.Цена КАК Цена
			|				ИЗ
			|					Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
			|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
			|							ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента
			|							ПО ИнгредиентыСостав.Номенклатура = НоменклатураКонтрагента.Номенклатура
			|								И (НоменклатураКонтрагента.ОсновнойПоставщик)
			|						ПО ТехнологическаяКартаСостав.Номенклатура = ИнгредиентыСостав.Ссылка
			|				ГДЕ
			|					ТехнологическаяКартаСостав.Ссылка = &Ссылка) КАК ВложенныйЗапрос
			|			
			|			СГРУППИРОВАТЬ ПО
			|				ВложенныйЗапрос.Ингредиент) КАК ВложенныйЗапрос1
			|			ПО ВложенныйЗапрос.Ингредиент = ВложенныйЗапрос1.Ингредиент
			|				И ВложенныйЗапрос.Цена = ВложенныйЗапрос1.Цена
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ВложенныйЗапрос.Ингредиент) КАК ВложенныйЗапрос
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
			|		ПО ВложенныйЗапрос.Номенклатура = ИнгредиентыСостав.Номенклатура
			|			И ВложенныйЗапрос.Ингредиент = ИнгредиентыСостав.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ИнгредиентыСостав.Номенклатура ЕСТЬ NULL 
			|			ТОГДА ТехнологическаяКартаСостав.Номенклатура
			|		ИНАЧЕ ИнгредиентыСостав.Номенклатура
			|	КОНЕЦ КАК Номенклатура,
			|	ВЫБОР
			|		КОГДА ИнгредиентыСостав.Номенклатура ЕСТЬ NULL 
			|			ТОГДА ТехнологическаяКартаСостав.Количество
			|		ИНАЧЕ ТехнологическаяКартаСостав.Количество * ИнгредиентыСостав.КоэффициентЗамены
			|	КОНЕЦ КАК Количество,
			|	ТехнологическаяКартаСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ТехнологическаяКартаСостав.Коэффициент КАК Коэффициент,
			|	ТехнологическаяКартаСостав.КоэффициентРаспределенияЦены КАК КоэффициентРаспределенияЦены,
			|	ИнгредиентыСостав.Ингредиент КАК Ингредиент
			|ПОМЕСТИТЬ СоставТехКарты
			|ИЗ
			|	Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
			|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИнгредиентов КАК ИнгредиентыСостав
			|		ПО ТехнологическаяКартаСостав.Номенклатура = ИнгредиентыСостав.Ингредиент
			|ГДЕ
			|	ТехнологическаяКартаСостав.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(ТК.ТехнологическаяКарта) КАК ТехнологическаяКарта,
			|	ТК.Продукция
			|ПОМЕСТИТЬ ТехКарты
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТехнологическаяКарта.Ссылка КАК ТехнологическаяКарта,
			|		ТехнологическаяКарта.Продукция КАК Продукция
			|	ИЗ
			|		СоставТехКарты КАК СоставТехКарты
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТехнологическаяКарта КАК ТехнологическаяКарта
			|			ПО СоставТехКарты.Номенклатура = ТехнологическаяКарта.Продукция
			|	ГДЕ
			|		ТехнологическаяКарта.Проведен = ИСТИНА
			|		И ТехнологическаяКарта.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Приготовление)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТехнологическаяКартаСостав.Ссылка,
			|		ТехнологическаяКартаСостав.Номенклатура
			|	ИЗ
			|		СоставТехКарты КАК СоставТехКарты
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
			|			ПО СоставТехКарты.Номенклатура = ТехнологическаяКартаСостав.Номенклатура
			|	ГДЕ
			|		ТехнологическаяКартаСостав.Ссылка.Проведен = ИСТИНА
			|		И ТехнологическаяКартаСостав.Ссылка.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Разделка)) КАК ТК
			|
			|СГРУППИРОВАТЬ ПО
			|	ТК.Продукция
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СоставТехКарты.Номенклатура,
			|	СоставТехКарты.Количество,
			|	СоставТехКарты.ЕдиницаИзмерения,
			|	СоставТехКарты.Коэффициент,
			|	СоставТехКарты.КоэффициентРаспределенияЦены,
			|	СоставТехКарты.Ингредиент,
			|	СоставТехКарты.Номенклатура.СтавкаНДС КАК СтавкаНДС,
			|	ВЫБОР
			|		КОГДА ТехКарты.ТехнологическаяКарта ЕСТЬ NULL 
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ЕстьТехКарта
			|ПОМЕСТИТЬ СоставСТехКартами
			|ИЗ
			|	СоставТехКарты КАК СоставТехКарты
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТехКарты КАК ТехКарты
			|		ПО СоставТехКарты.Номенклатура = ТехКарты.Продукция
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СоставСТехКартами.Номенклатура,
			|	СоставСТехКартами.Количество,
			|	СоставСТехКартами.ЕдиницаИзмерения,
			|	СоставСТехКартами.Коэффициент,
			|	СоставСТехКартами.КоэффициентРаспределенияЦены,
			|	СоставСТехКартами.Ингредиент,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА СоставСТехКартами.ЕстьТехКарта = ИСТИНА
			|				ТОГДА ЕСТЬNULL(ПлановаяСебестоимостьПродукции.Себестоимость, 0)
			|			ИНАЧЕ ВЫБОР
			|					КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
			|						ТОГДА ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0)
			|					ИНАЧЕ ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0) * (100 + СоставСТехКартами.СтавкаНДС.Ставка) / 100
			|				КОНЕЦ
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА СоставСТехКартами.ЕстьТехКарта = ИСТИНА
			|				ТОГДА ЕСТЬNULL(ПлановаяСебестоимостьПродукции.Себестоимость, 0)
			|			ИНАЧЕ ВЫБОР
			|					КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
			|						ТОГДА ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0)
			|					ИНАЧЕ ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0) * (100 + СоставСТехКартами.СтавкаНДС.Ставка) / 100
			|				КОНЕЦ
			|		КОНЕЦ * СоставСТехКартами.Количество * СоставСТехКартами.Коэффициент КАК ЧИСЛО(15, 2)) КАК Сумма,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА СоставСТехКартами.ЕстьТехКарта = ИСТИНА
			|				ТОГДА ЕСТЬNULL(ПлановаяСебестоимостьПродукции.Себестоимость, 0)
			|			ИНАЧЕ ВЫБОР
			|					КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
			|						ТОГДА ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0)
			|					ИНАЧЕ ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0) * (100 + СоставСТехКартами.СтавкаНДС.Ставка) / 100
			|				КОНЕЦ
			|		КОНЕЦ * СоставСТехКартами.Количество * СоставСТехКартами.СтавкаНДС.Ставка * СоставСТехКартами.Коэффициент / (100 + СоставСТехКартами.СтавкаНДС.Ставка) КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
			|	СоставСТехКартами.СтавкаНДС
			|ИЗ
			|	СоставСТехКартами КАК СоставСТехКартами
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановаяСебестоимостьПродукции.СрезПоследних(&Момент, ) КАК ПлановаяСебестоимостьПродукции
			|		ПО СоставСТехКартами.Номенклатура = ПлановаяСебестоимостьПродукции.Продукция
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента
			|		ПО СоставСТехКартами.Номенклатура = НоменклатураКонтрагента.Номенклатура
			|			И (НоменклатураКонтрагента.ОсновнойПоставщик = ИСТИНА),
			|	Константа.ВключатьНДСВЦеныКонтрагентов КАК ВключатьНДСВЦеныКонтрагентов";
			Запрос.УстановитьПараметр("Момент", ТекущаяДата());
			Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
			Состав.Загрузить(Запрос.Выполнить().Выгрузить());
			СебестоимостьПродукции = Окр(Состав.Итог("Сумма")/КоличествоПорций,2);
			СебестоимостьНДСПродукции = Окр(Состав.Итог("СуммаНДС")/КоличествоПорций,2);
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ВЫБОР
				|		КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
				|			ТОГДА НоменклатураКонтрагента.Цена
				|		ИНАЧЕ НоменклатураКонтрагента.Цена * (100 + НоменклатураКонтрагента.Номенклатура.СтавкаНДС.Ставка) / 100
				|	КОНЕЦ КАК Цена
				|ИЗ
				|	РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента,
				|	Константа.ВключатьНДСВЦеныКонтрагентов КАК ВключатьНДСВЦеныКонтрагентов
				|ГДЕ
				|	НоменклатураКонтрагента.Номенклатура = &Номенклатура
				|	И НоменклатураКонтрагента.ОсновнойПоставщик";

			Запрос.УстановитьПараметр("Номенклатура", Продукция);

			Результат = Запрос.Выполнить();

			Выборка = Результат.Выбрать();

			Выборка.Следующий();
			СебестоимостьПродукции = Выборка.Цена;
			Себестоимость = Выборка.Цена*МассаПорции;
			ИтогоСумма = 0;
			Для Каждого Строка из ДанныеЗаполнения.Состав Цикл
				НоваяСтрока = Состав.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
				НоваяСтрока.Сумма = Строка.КоэффициентРаспределенияЦены*Себестоимость/100;
				НоваяСтрока.СтавкаНДС = НоваяСтрока.Номенклатура.СтавкаНДС;
				НоваяСтрока.СуммаНДС = (НоваяСтрока.Сумма * НоваяСтрока.СтавкаНДС.Ставка)/(100+НоваяСтрока.СтавкаНДС.Ставка);
				Если Строка.НомерСтроки = ДанныеЗаполнения.Состав.Количество() Тогда
					НоваяСтрока.Сумма = Себестоимость - ИтогоСумма;
				КонецЕсли;
				НоваяСтрока.Цена = НоваяСтрока.Сумма*100/(МассаПорции*НоваяСтрока.Количество*НоваяСтрока.Коэффициент);
				ИтогоСумма = ИтогоСумма + НоваяСтрока.Сумма;
			КонецЦикла;
			СебестоимостьНДСПродукции = Состав.Итог("СуммаНДС");
		КонецЕсли;
	КонецЕсли;
	ИнициализироватьДокумент();
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	Автор = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если ВидПроизводства = Перечисления.ВидыПроизводства.Приготовление Тогда
		Движение = Движения.ПлановаяСебестоимостьПродукции.Добавить();
		Движение.Период = Дата;
		Движение.Продукция = Продукция;
		Движение.Себестоимость = СебестоимостьПродукции;
		Движение.СебестоимостьНДС = СебестоимостьНДСПродукции;
	Иначе
		Для Каждого Строка Из Состав Цикл
			Движение = Движения.ПлановаяСебестоимостьПродукции.Добавить();
			Движение.Период = Дата;
			Движение.Продукция = Строка.Номенклатура;
			Движение.Себестоимость = Строка.Цена;
			Движение.СебестоимостьНДС = (Строка.Цена * Строка.СтавкаНДС.Ставка) / (100 + Строка.СтавкаНДС.Ставка);
		КонецЦикла;	
	КонецЕсли;	
КонецПроцедуры
