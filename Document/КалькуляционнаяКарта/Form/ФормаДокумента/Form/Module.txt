

&НаКлиенте
Процедура УправлениеФормойПоВидуПроизводства()
	флПереработка = ПолучитьФлагПереработки();
	Колонки = Элементы.Состав.ПодчиненныеЭлементы;
	//popn+
	Колонки.СоставЦена.ТолькоПросмотр = флПереработка
	И НЕ ОбщегоНазначенияСервер.ЕстьПравоСмотретьСебестоимость();
	//popn-
	//Колонки.СоставКоэффициентРаспределенияЦены.Видимость = флПереработка;
	//Колонки.СоставКоличествоНетто.Видимость              = НЕ флПереработка;
	//Элементы.КоличествоПорций.Доступность     = НЕ флПереработка;
	Если флПереработка Тогда
		Колонки.СоставКоличество.Заголовок = "Выход (%)";
	Иначе
		Колонки.СоставКоличество.Заголовок = "Кол-во брутто";
	КонецЕсли;
Конецпроцедуры	

&НаСервере
Функция ПолучитьФлагПереработки()
	Возврат ?(Объект.ВидПроизводства = Перечисления.ВидыПроизводства.Разделка или Объект.ВидПроизводства = Перечисления.ВидыПроизводства.Разморозка,Истина,Ложь);
КонецФункции


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеФормойПоВидуПроизводства();
КонецПроцедуры



&НаКлиенте
Процедура СоставНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Элементы.Состав.ТекущиеДанные.Ингредиент) Тогда
		НовыеПараметры = УстановитьПараметрыВыбораИнгредиентов(Элементы.Состав.ТекущиеДанные.Ингредиент);
		Элементы.Состав.ПодчиненныеЭлементы.СоставНоменклатура.ПараметрыВыбора = НовыеПараметры;
	Иначе	
		СтандартнаяОбработка = Ложь;	
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция УстановитьПараметрыВыбораИнгредиентов(Ингредиент)
	НовыйПараметр = Новый ПараметрВыбора("Отбор.Ссылка", Ингредиент.Состав.ВыгрузитьКолонку("Номенклатура"));
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	Возврат НовыеПараметры;
КонецФункции	


&НаКлиенте
Процедура СоставНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекСтрока = Элементы.Состав.ТекущиеДанные;
	ТекСтрока.Коэффициент = ПолучитьКоэффициент(ВыбранноеЗначение,ТекСтрока.Ингредиент);
	ТекСтрока.Цена = ПолучитьЦену(ВыбранноеЗначение);
	//ОбработкаТабличныхЧастейКлиент.РасчитатьСуммуВСтрокеТабличнойЧасти(ТекСтрока);
	ТекСтрока.Сумма = ТекСтрока.Цена*ТекСтрока.Количество*ТекСтрока.Коэффициент;
	ТекСтрока.СтавкаНДС = ПолучитьСтавкуНДС(ВыбранноеЗначение);
	ТекСтрока.СуммаНДС = ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуНДС(ТекСтрока.Сумма,	Истина,	ОбщегоНазначенияСервер.ПолучитьСтавкуНДС(ТекСтрока.СтавкаНДС));
	//popn+
	Объект.СебестоимостьПродукции = Окр(Объект.Состав.Итог("Сумма")/Объект.КоличествоПорций,2);
	Объект.СебестоимостьНДСПродукции = Окр(Объект.Состав.Итог("СуммаНДС")/Объект.КоличествоПорций,2);
	//popn-
КонецПроцедуры

&НаСервере
Функция ПолучитьЦену(Номенклатура)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
		|			ТОГДА НоменклатураКонтрагента.Цена
		|		ИНАЧЕ НоменклатураКонтрагента.Цена * (100 + НоменклатураКонтрагента.Номенклатура.СтавкаНДС.Ставка) / 100
		|	КОНЕЦ КАК Цена
		|ИЗ
		|	РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента,
		|	Константа.ВключатьНДСВЦеныКонтрагентов КАК ВключатьНДСВЦеныКонтрагентов
		|ГДЕ
		|	НоменклатураКонтрагента.Номенклатура = &Номенклатура
		|	И НоменклатураКонтрагента.ОсновнойПоставщик";

	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	ВыборкаДетальныеЗаписи.Следующий();
	Возврат ВыборкаДетальныеЗаписи.Цена;
КонецФункции

&НаСервере
Функция ПолучитьКоэффициент(Номенклатура,Ингредиент)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнгредиентыСостав.КоэффициентЗамены
		|ИЗ
		|	Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
		|ГДЕ
		|	ИнгредиентыСостав.Ссылка = &Ссылка
		|	И ИнгредиентыСостав.Номенклатура = &Номенклатура";

	Запрос.УстановитьПараметр("Ссылка", Ингредиент);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	ВыборкаДетальныеЗаписи.Следующий();
	Возврат ВыборкаДетальныеЗаписи.КоэффициентЗамены;
КонецФункции

&НаСервере
Функция ПолучитьСтавкуНДС(Номенклатура)
	Возврат Номенклатура.СтавкаНДС;
КонецФункции

&НаКлиенте
Процедура СоставКоэффициентРаспределенияЦеныПриИзменении(Элемент)
	чИтог = Объект.Состав.Итог("КоэффициентРаспределенияЦены");
	Если чИтог <> 100 Тогда
		Текст = "Ошибка заполнения реквизита табличной части ""Коэффициент распределения цены""!
		|Итог должен равняться 100%.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		Возврат;
	КонецЕсли;
    Себестоимость = Объект.СебестоимостьПродукции*Объект.МассаПорции;
	ИтогоСумма = 0;
	Для Каждого Строка из Объект.Состав Цикл
		Строка.Сумма = Строка.КоэффициентРаспределенияЦены*Себестоимость/100;
		Если Строка.НомерСтроки = Объект.Состав.Количество() Тогда
			Строка.Сумма = Себестоимость - ИтогоСумма;
		КонецЕсли;	
		Строка.Цена = Строка.Сумма*100/(Объект.МассаПорции*Строка.Количество*Строка.Коэффициент);
		ИтогоСумма = ИтогоСумма + Строка.Сумма;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	Если ЗначениеЗаполнено(Объект.Ссылка)Тогда
		Настройки.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПараметрыСеанса.ЭтоЦентр Тогда
		ТолькоПросмотр = РаботаСФормамиСервер.ЗапрещеноРедактировать(Объект.Ссылка,ЭтаФорма);
	Иначе	
		ТолькоПросмотр = Истина;
	КонецЕсли;	
	ОбщегоНазначенияСервер.ЗапретМодификацииОбъектов(ЭтаФорма);
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СоставЦенаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Состав.ТекущиеДанные;
	Если Объект.ВидПроизводства = ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Приготовление") Тогда
		СтрокаТабличнойЧасти.Сумма = Окр(СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена,2);
		СтрокаТабличнойЧасти.СуммаНДС = ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуНДС(СтрокаТабличнойЧасти.Сумма, Истина, ОбщегоНазначенияСервер.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
	Иначе
		СтрокаТабличнойЧасти.Сумма = Окр(СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена / 100,2);
		СтрокаТабличнойЧасти.СуммаНДС = ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуНДС(СтрокаТабличнойЧасти.Сумма, Истина, ОбщегоНазначенияСервер.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
	КонецЕсли;
	
	Объект.СебестоимостьПродукции = Окр(Объект.Состав.Итог("Сумма")/Объект.КоличествоПорций,2);
	Объект.СебестоимостьНДСПродукции = Окр(Объект.Состав.Итог("СуммаНДС")/Объект.КоличествоПорций,2);

КонецПроцедуры
