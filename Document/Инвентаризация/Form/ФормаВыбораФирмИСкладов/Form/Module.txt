
&НаСервере
Функция ПолучитьСписокСкладовСтруктурнойЕдиницы(се)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Склады.Ссылка
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Владелец = &Владелец
	|	И (НЕ Склады.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Склады.Наименование");
	Запрос.УстановитьПараметр("Владелец",се);
	сз = Новый СписокЗначений;
	сз.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Возврат сз;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокФирм()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организации.Наименование");
	сз = Новый СписокЗначений;
	сз.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Возврат сз;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	сзСклады = ПолучитьСписокСкладовСтруктурнойЕдиницы(Объект.СтруктурнаяЕдиница);
	Для каждого эл из Параметры.сзСклады Цикл
		эл_сз = сзСклады.НайтиПоЗначению(эл.Значение);
		Если эл_сз <> Неопределено Тогда
			эл_сз.Пометка = Истина;
		КонецЕсли;	
	КонецЦикла;
	
	сзФирмы = ПолучитьСписокФирм();
	Для каждого эл из Параметры.сзФирмы Цикл
		эл_сз = сзФирмы.НайтиПоЗначению(эл.Значение);
		Если эл_сз <> Неопределено Тогда
			эл_сз.Пометка = Истина;
		КонецЕсли;	
	КонецЦикла;
	 
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	сзС = Новый СписокЗначений;
	Для Каждого стр Из сзСклады Цикл
		Если стр.Пометка Тогда
			сзС.Добавить(стр.Значение, ИСТИНА);
		КонецЕсли;
	КонецЦикла;	
	
	сзФ = Новый СписокЗначений;
	Для Каждого стр Из сзФирмы Цикл
		Если стр.Пометка Тогда
			сзФ.Добавить(стр.Значение, ИСТИНА);
		КонецЕсли;
	КонецЦикла;	
	
	Если сзФ.Количество() = 0 Тогда
		Сообщить("Выберите хотя бы одну фирму.");
		Возврат;
	КонецЕсли;
	
	Если сзС.Количество() = 0 Тогда
		Сообщить("Выберите хотя бы один склад.");
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("сзСклады", сзСклады);
	Результат.Вставить("сзФирмы",  сзФирмы);
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(НЕОПРЕДЕЛЕНО);
	
КонецПроцедуры
