
Процедура ПриКопировании(ОбъектКопирования)
	Автор = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИнициализироватьДокумент();
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = СтруктурныеЕдиницы.ПолучитьВалютуТекущейСЕ();
		
		СтруктураКурса = РаботаСКурсамиВалют.ЗаполнитьДанныеКурсаДляВалюты(Валюта);
		Курс = СтруктураКурса.Курс;
		Кратность = СтруктураКурса.Кратность;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) И НЕ ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница.ЭтоГруппа Тогда
		СтруктурнаяЕдиница = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница;
	КонецЕсли;
	
	
	Если НЕ ЗначениеЗаполнено(СтавкаНДС) Тогда
		СтавкаНДС = ОбщегоНазначенияСервер.ПолучитьОсновнуюСтавкуНДС();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Отказ = ПровестиОтчетРеализатораМеждуФирмами(ЭтотОбъект,РежимПроведения);
КонецПроцедуры

Функция ПровестиОтчетРеализатораМеждуФирмами(Объект,Режим)
	
	//по фирме комиссионера
	тзРеализация = Объект.Движения.Реализация.Выгрузить();
	
	// Установка исключительной блокировки контролируемых остатков 
	Запрос=Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ОтчетРеализатораСостав.Номенклатура,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомитент КАК Комитент,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомиссионер КАК Фирма,
	|	ОтчетРеализатораСостав.ПоДокументу КАК ПоДокументу,
	|	ОтчетРеализатораСостав.КоличествоОст,
	|	ОтчетРеализатораСостав.Количество
	|ПОМЕСТИТЬ врСостав
	|ИЗ
	|	Документ.ОтчетРеализатораМеждуФирмами.Состав КАК ОтчетРеализатораСостав
	|ГДЕ
	|	ОтчетРеализатораСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетРеализатораСостав.Номенклатура,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомитент,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомиссионер,
	|	ОтчетРеализатораСостав.ПоДокументу,
	|	ОтчетРеализатораСостав.КоличествоОст,
	|	ОтчетРеализатораСостав.Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врСостав.Номенклатура,
	|	врСостав.Комитент,
	|	врСостав.Фирма,
	|	врСостав.ПоДокументу
	|ИЗ
	|	врСостав КАК врСостав";
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	тз=Запрос.Выполнить().Выгрузить();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Реализация");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тз;
	Для каждого к из тз.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(к.Имя, к.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Запрос.Текст=
	"ВЫБРАТЬ
	|	врСостав.Номенклатура КАК Номенклатура,
	|	врСостав.Комитент КАК Комитент,
	|	врСостав.Фирма КАК Фирма,
	|	врСостав.ПоДокументу КАК ПоДокументу,
	|	врСостав.КоличествоОст КАК КоличествоОст,
	|	врСостав.Количество КАК Количество,
	//++БИТ БВО
	//|	о.КоличествоОстаток КАК КоличествоОстРегистр,
	//|	о.СуммаОстаток КАК Сумма,
	//|	о.СуммаПродажиОстаток КАК СуммаПродажи,
	//|	о.СуммаНДСПродажиОстаток КАК СуммаНДСПродажи
	|	ЕСТЬNULL(о.КоличествоОстаток, 0) КАК КоличествоОстРегистр,
	|	ЕСТЬNULL(о.СуммаОстаток, 0) КАК Сумма,
	|	ЕСТЬNULL(о.СуммаПродажиОстаток, 0) КАК СуммаПродажи,
	|	ЕСТЬNULL(о.СуммаНДСПродажиОстаток, 0) КАК СуммаНДСПродажи
	//--БИТ БВО
	|ИЗ
	|	врСостав КАК врСостав
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Реализация.Остатки(
	|				&Момент,
	|				(Фирма, Номенклатура, Комитент, ПоДокументу) В
	|					(ВЫБРАТЬ
	|						с.Фирма,
	|						с.Номенклатура,
	|						с.Комитент,
	|						с.ПоДокументу
	|					ИЗ
	|						врСостав КАК с)) КАК о
	|		ПО врСостав.Номенклатура = о.Номенклатура
	|			И врСостав.ПоДокументу = о.ПоДокументу
	|			И врСостав.Фирма = о.Фирма
	|			И врСостав.Комитент = о.Комитент";
	Запрос.УстановитьПараметр("Момент",Объект.Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоОст <> Выборка.КоличествоОстРегистр Тогда		
			Сообщить("Данные в документе не актуальны! Необходимо перезаполнить документ!");
			Возврат Истина;
		КонецЕсли;	
		
		Движение = тзРеализация.Добавить();
		Движение.ВидДвижения     = ВидДвиженияНакопления.Расход;
		Движение.Активность      = истина;
		Движение.Регистратор     = Объект.Ссылка;
		Движение.Период          = Объект.Дата;
		Движение.Фирма           = Выборка.Фирма;
		Движение.Номенклатура    = Выборка.Номенклатура;
		Движение.Комитент        = Выборка.Комитент;
		Движение.ПоДокументу     = Выборка.ПоДокументу;
		ТекКол = Выборка.Количество;
		Движение.Количество      = ТекКол;
		//++БИТ БВО - Предотвращение ошибок проведения
		//к = ТекКол / Выборка.КоличествоОст;
		к = ?(ЗначениеЗаполнено(Выборка.КоличествоОст), ТекКол / Выборка.КоличествоОст, 0);
		//--БИТ БВО
		Движение.Сумма           = Окр(к * Выборка.Сумма,2);
		Движение.СуммаПродажи    = Окр(к * Выборка.СуммаПродажи,2);
		Движение.СуммаНДСПродажи = Окр(к * Выборка.СуммаНДСПродажи,2);
		
	КонецЦикла;	
	
	Объект.Движения.Реализация.Загрузить(тзРеализация);
	Объект.Движения.Реализация.Записывать=Истина;
	
	
	//по фирме комитента
	тзТоварыПереданныеНаКомиссию = Объект.Движения.ТоварыПереданныеНаКомиссию.Выгрузить();
	
	// Установка исключительной блокировки контролируемых остатков 
	Запрос=Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ОтчетРеализатораСостав.Номенклатура,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомиссионер КАК Комиссионер,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомитент КАК Фирма,
	|	ОтчетРеализатораСостав.КоличествоОст,
	|	ОтчетРеализатораСостав.Количество
	|ПОМЕСТИТЬ врСостав
	|ИЗ
	|	Документ.ОтчетРеализатораМеждуФирмами.Состав КАК ОтчетРеализатораСостав
	|ГДЕ
	|	ОтчетРеализатораСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетРеализатораСостав.Номенклатура,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомиссионер,
	|	ОтчетРеализатораСостав.Ссылка.ФирмаКомитент,
	|	ОтчетРеализатораСостав.КоличествоОст,
	|	ОтчетРеализатораСостав.Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врСостав.Номенклатура,
	|	врСостав.Комиссионер,
	|	врСостав.Фирма
	|ИЗ
	|	врСостав КАК врСостав";
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	тз=Запрос.Выполнить().Выгрузить();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыПереданныеНаКомиссию");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = тз;
	Для каждого к из тз.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(к.Имя, к.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Запрос.Текст=
	"ВЫБРАТЬ
	|	врСостав.Номенклатура КАК Номенклатура,
	|	врСостав.Комиссионер КАК Комиссионер,
	|	врСостав.Фирма КАК Фирма,
	|	врСостав.КоличествоОст КАК КоличествоОст,
	|	врСостав.Количество КАК Количество,
	//++БИТ БВО
	//|	о.КоличествоОстаток КАК КоличествоОстРегистр,
	//|	о.СуммаОстаток КАК Сумма,
	//|	о.СуммаУпрОстаток КАК СуммаУпр,
	//|	о.СуммаНДСОстаток КАК СуммаНДС,
	//|	о.СуммаНДСУпрОстаток КАК СуммаНДСУпр,
	//|	о.СуммаПродажиОстаток КАК СуммаПродажи,
	//|	о.СуммаНДСПродажиОстаток КАК СуммаНДСПродажи
	|	ЕСТЬNULL(о.КоличествоОстаток, 0) КАК КоличествоОстРегистр,
	|	ЕСТЬNULL(о.СуммаОстаток, 0) КАК Сумма,
	|	ЕСТЬNULL(о.СуммаУпрОстаток, 0) КАК СуммаУпр,
	|	ЕСТЬNULL(о.СуммаНДСОстаток, 0) КАК СуммаНДС,
	|	ЕСТЬNULL(о.СуммаНДСУпрОстаток, 0) КАК СуммаНДСУпр,
	|	ЕСТЬNULL(о.СуммаПродажиОстаток, 0) КАК СуммаПродажи,
	|	ЕСТЬNULL(о.СуммаНДСПродажиОстаток, 0) КАК СуммаНДСПродажи
	//--БИТ БВО
	|ИЗ
	|	врСостав КАК врСостав
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(
	|				&Момент,
	|				(Фирма, Номенклатура, Комиссионер) В
	|					(ВЫБРАТЬ
	|						с.Фирма,
	|						с.Номенклатура,
	|						с.Комиссионер
	|					ИЗ
	|						врСостав КАК с)) КАК о
	|		ПО врСостав.Номенклатура = о.Номенклатура
	|			И врСостав.Фирма = о.Фирма
	|			И врСостав.Комиссионер = о.Комиссионер";
	Запрос.УстановитьПараметр("Момент",Объект.Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоОст > Выборка.КоличествоОстРегистр Тогда
			Сообщить("Данные в документе не актуальны! Необходимо перезаполнить документ!");
			Возврат Истина;
		КонецЕсли;	
		
		Движение = тзТоварыПереданныеНаКомиссию.Добавить();
		Движение.ВидДвижения     = ВидДвиженияНакопления.Расход;
		Движение.Активность      = истина;
		Движение.Регистратор     = Объект.Ссылка;
		Движение.Период          = Объект.Дата;
		Движение.Фирма           = Выборка.Фирма;
		Движение.Номенклатура    = Выборка.Номенклатура;
		Движение.Комиссионер     = Выборка.Комиссионер;
		ТекКол = Выборка.Количество;
		Движение.Количество      = ТекКол;
		//++БИТ БВО
		//к=ТекКол / Выборка.КоличествоОстРегистр;
		к=?(ЗначениеЗаполнено(Выборка.КоличествоОстРегистр), ТекКол / Выборка.КоличествоОстРегистр, 0);
		//--БИТ БВО
		Движение.Сумма           = Окр(к * Выборка.Сумма,2);
		Движение.СуммаУпр        = Окр(к * Выборка.СуммаУпр,2);
		Движение.СуммаНДС        = Окр(к * Выборка.СуммаНДС,2);
		Движение.СуммаНДСУпр     = Окр(к * Выборка.СуммаНДСУпр,2);
		Движение.СуммаПродажи    = Окр(к * Выборка.СуммаПродажи,2);
		Движение.СуммаНДСПродажи = Окр(к * Выборка.СуммаНДСПродажи,2);
		
	КонецЦикла;	
	
	Объект.Движения.ТоварыПереданныеНаКомиссию.Загрузить(тзТоварыПереданныеНаКомиссию);
	Объект.Движения.ТоварыПереданныеНаКомиссию.Записывать=Истина;
	
	Возврат Ложь;
КонецФункции
