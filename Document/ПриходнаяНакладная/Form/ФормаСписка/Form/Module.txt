
//Работа с ТСД  
&НаКлиенте
Процедура ВыполнитьДействиеДляТСД(Действие, ТекстВопроса)
	ТекстВопроса = "Вы действительно хотите " + ТекстВопроса;
	Если Вопрос(ТекстВопроса,РежимДиалогаВопрос.ДаНет,20,КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Да Тогда
		СписокДокументов = Новый СписокЗначений;
		Для Каждого Строка Из Элементы.Список.ВыделенныеСтроки Цикл
			СписокДокументов.Добавить(Строка);
		КонецЦикла;
		ПараметрыФормы = Новый Структура("СписокДокументов, КлючНазначенияИспользования, ВидДокумента, КакВыгружать", СписокДокументов, Действие, "ПриходнаяНакладная", "Как приходную накладную");
		ОткрытьФорму("Обработка.ТСДЗагрузкаВыгрузка.Форма", ПараметрыФормы, ЭтаФорма,);
	КонецЕсли;
КонецПроцедуры

//Работа с ТСД  Загрузить
&НаКлиенте
Процедура ЗагрузитьИзТСД(Команда)
	ВыполнитьДействиеДляТСД("Загрузить","загрузить документы из ТСД?");
КонецПроцедуры

//Работа с ТСД  Выгрузить
&НаКлиенте
Процедура ВыгрузитьВТСД(Команда)
	СтрокаВопроса = ?(Элементы.Список.ВыделенныеСтроки.Количество()>1,"выгрузить выделенные документы в ТСД?","выгрузить выделенный документ в ТСД?");
	ВыполнитьДействиеДляТСД("Выгрузить",СтрокаВопроса);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// BiT ALProzorov 03/09/2015 -->>
	//Изменим тип запроса динамического списка.
	//Добавим поле "ТребуетсяОформлениеПриложения"
	// Ложь 	- Приходная накладная не по алкоголю или приходная накладная по алкоголю и для нее оформлен документ "Приложение по алкоголю"
	// Истина   - Приходная накладная по алкоголю и по ней нет оформленного документа "Приложение по алкоголю" или есть, но помечен на удаление
	Список.ТекстЗапроса = "ВЫБРАТЬ
	                      |	ДокументПриходнаяНакладная.Ссылка,
	                      |	ДокументПриходнаяНакладная.ВерсияДанных,
	                      |	ДокументПриходнаяНакладная.ПометкаУдаления,
	                      |	ДокументПриходнаяНакладная.Номер,
	                      |	ДокументПриходнаяНакладная.Дата,
	                      |	ДокументПриходнаяНакладная.Проведен,
	                      |	ДокументПриходнаяНакладная.Автор,
	                      |	ДокументПриходнаяНакладная.БонуснаяПоставка,
	                      |	ДокументПриходнаяНакладная.Валюта,
	                      |	ДокументПриходнаяНакладная.Грузоотправитель,
	                      |	ДокументПриходнаяНакладная.Грузополучатель,
	                      |	ДокументПриходнаяНакладная.ДатаДокумента,
	                      |	ДокументПриходнаяНакладная.ДатаСФ,
	                      |	ДокументПриходнаяНакладная.ДокОснование,
	                      |	ДокументПриходнаяНакладная.Контрагент,
	                      |	ДокументПриходнаяНакладная.Курс,
	                      |	ДокументПриходнаяНакладная.НомерДокумента,
	                      |	ДокументПриходнаяНакладная.НомерСФ,
	                      |	ДокументПриходнаяНакладная.Приемщик,
	                      |	ДокументПриходнаяНакладная.Склад,
	                      |	ДокументПриходнаяНакладная.СтруктурнаяЕдиница,
	                      |	ДокументПриходнаяНакладная.Фирма,
	                      |	ДокументПриходнаяНакладная.Комментарий,
	                      |	ДокументПриходнаяНакладная.ТипДоговора,
	                      |	ДокументПриходнаяНакладная.Кратность,
	                      |	ДокументПриходнаяНакладная.СуммаИтого,
	                      |	ДокументПриходнаяНакладная.СуммаНДСИтого,
	                      |	ДокументПриходнаяНакладная.Контролер,
	                      |	ВЫБОР
	                      |		КОГДА Izh_СостояниеДокумента.Документ ЕСТЬ NULL 
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК ОбработанЦВД,
	                      |	ДокументПриходнаяНакладная.Загружен,
	                      |	ДокументПриходнаяНакладная.НомерRMS,
	                      |	ДокументПриходнаяНакладная.Снабженец,
	                      |	СтатусыEDI.Статус КАК СтатусEDI,
	                      |	СтатусыEDI.ОписаниеОшибки,
	                      |	СтатусыEDI.ДатаИзменения,
	                      |	ВЫБОР
	                      |		КОГДА ВложенныйЗапрос.Ссылка ЕСТЬ NULL 
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК ТребуетсяОформлениеПриложения
	                      |ИЗ
	                      |	Документ.ПриходнаяНакладная КАК ДокументПриходнаяНакладная
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыEDI КАК СтатусыEDI
	                      |		ПО ДокументПриходнаяНакладная.Ссылка = СтатусыEDI.Документ
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Izh_СостояниеДокумента.СрезПоследних КАК Izh_СостояниеДокумента
	                      |		ПО ДокументПриходнаяНакладная.Ссылка = Izh_СостояниеДокумента.Документ
	                      |			И (ЗНАЧЕНИЕ(Перечисление.Izh_СостояниеДокумента.ПроверенЦВД) = Izh_СостояниеДокумента.Состояние)
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |			ПриходнаяНакладнаяСостав.Ссылка КАК Ссылка
	                      |		ИЗ
	                      |			Документ.ПриходнаяНакладная.Состав КАК ПриходнаяНакладнаяСостав
	                      |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	                      |				ПО ПриходнаяНакладнаяСостав.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка
	                      |		ГДЕ
	                      |			НЕ ПриходнаяНакладнаяСостав.Ссылка В
	                      |						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |							ПриложениеПоАлкоголю.ДокОснование КАК ПриходнаяНакладная
	                      |						ИЗ
	                      |							Документ.ПриложениеПоАлкоголю КАК ПриложениеПоАлкоголю
	                      |						ГДЕ
	                      |							НЕ ПриложениеПоАлкоголю.ПометкаУдаления)
	                      |			И НоменклатураДополнительныеРеквизиты.Свойство = &ВидАлко) КАК ВложенныйЗапрос
	                      |		ПО ДокументПриходнаяНакладная.Ссылка = ВложенныйЗапрос.Ссылка";
	Список.Параметры.УстановитьЗначениеПараметра("ВидАлко",ПланыВидовХарактеристик.ДополнительныеРеквизитыНоменклатуры.Реквизит("ВидАлкогольнойПродукции"));
	КолонкаТаблицы = Элементы.Вставить("ТребуетсяОформлениеПриложения", Тип("ПолеФормы"), Элементы.Список);
	КолонкаТаблицы.ПутьКДанным	= "Список.ТребуетсяОформлениеПриложения";
	КолонкаТаблицы.Вид = ВидПоляФормы.ПолеФлажка;
	КолонкаТаблицы.Видимость 	= True;
	// BiT ALProzorov 03/09/2015 <<--
	
	//popn+
	//Если ПараметрыСеанса.ЭтоЦентр Тогда
	//	Элементы.Список.ИзменятьСоставСтрок = Ложь;
	//КонецЕсли;
	//popn-
	//{БредовЮГ 2014-12-03 Наряд№000135628
	Элементы.ФормаОбработанЦВД.Видимость = ПараметрыСеанса.ЭтоЦентр;
	//БредовЮГ 2014-12-03 Наряд№000135628}
	ОбщегоНазначенияСервер.ЗапретМодификацииОбъектов(Элементы.Список,Элементы.ФормаСоздатьНаОсновании);
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	Если ( Источник = "BarCodeScaner" ) И ( Событие = "BarCodeValue" ) Тогда
	Если Не ВводДоступен() Тогда
		КомпонентаСканераШК.СобытиеОбработано();
		Возврат;
	КонецЕсли;
		СтрокаТЧ=РаботаСоСканеромШККлиент.ОбработкаШтрихКодаВЖурналах(Данные,Элементы.Список);
		КомпонентаСканераШК.СобытиеОбработано();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		Элементы.ГруппаEDI.ПодчиненныеЭлементы.СтатусEDI.Подсказка = Элементы.Список.ТекущиеДанные.ОписаниеОшибки;
	КонецЕсли;	
КонецПроцедуры

//{БредовЮГ 2014-12-03 Наряд№000135628
&НаСервереБезКонтекста
Процедура ОбработанЦВДСервер(Документ)
	РегистрыСведений.Izh_СостояниеДокумента.ЗафиксироватьДокумент(Документ,Перечисления.Izh_СостояниеДокумента.ПроверенЦВД);
КонецПроцедуры

&НаКлиенте
Процедура ОбработанЦВД(Команда)
	ОбработанЦВДСервер(Элементы.Список.ТекущаяСтрока);
	Элементы.Список.Обновить();
КонецПроцедуры
//БредовЮГ 2014-12-03 Наряд№000135628}