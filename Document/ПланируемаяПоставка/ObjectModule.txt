Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаИтого = Состав.Итог("Сумма");
	СуммаНДСИтого = Состав.Итог("СуммаНДС");
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если Статус = Перечисления.СтатусыПланируемойПоставки.Черновик Тогда
			Если НЕ Контрагент.ЗаказEDI ИЛИ НЕ Контрагент.ПодтверждениеЗаказаEDI Тогда
				Статус = Перечисления.СтатусыПланируемойПоставки.Утвержден;
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	Если СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы <> Перечисления.ТипыСтруктурныхЕдиниц.Склад И ЗначениеЗаполнено(Склад) Тогда
		Склад = Неопределено;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ИнициализироватьДокумент();
КонецПроцедуры

Процедура ИнициализироватьДокумент()Экспорт
	Автор = ПараметрыСеанса.ТекущийПользователь;
		
	Если НЕ ЗначениеЗаполнено(СтруктурнаяЕдиница) И НЕ ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница.ЭтоГруппа Тогда
		СтруктурнаяЕдиница = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница;
	КонецЕсли;
	АдресДоставки = УправлениеКонтактнойИнформациейСервер.ПолучитьКонтактнуюИнформацияОбъекта(СтруктурнаяЕдиница,Справочники.ВидыКонтактнойИнформации.ФактАдресСтруктурнойЕдиницы);

	//если создаем интерактивно, то это заказ магазина
	ЗаказМагазина = истина;
	Статус = Перечисления.СтатусыПланируемойПоставки.Черновик;
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") И ЗначениеЗаполнено(Контрагент.Валюта) Тогда
			Валюта = Контрагент.Валюта;
		Иначе
			Валюта = СтруктурныеЕдиницы.ПолучитьВалютуТекущейСЕ();
		КонецЕсли;
		
		СтруктураКурса = РаботаСКурсамиВалют.ЗаполнитьДанныеКурсаДляВалюты(Валюта);
		Курс = СтруктураКурса.Курс;
		Кратность = СтруктураКурса.Кратность;
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Автор = ПараметрыСеанса.ТекущийПользователь;
	ЗаказМагазина = Истина;
	Статус = Перечисления.СтатусыПланируемойПоставки.Черновик;
	НомерRMS = "";
	ДатаПоставкиДо = "";
	ДатаПоставкиОт = "";
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если ЗаказМагазина И Контрагент.ЗаказEDI Тогда
		Если НЕ (ДополнительныеСвойства.Свойство("БылПроведен") И ДополнительныеСвойства.БылПроведен) Тогда
			Узел = ОбменДаннымиEDIСервер.ПолучитьУзелРегистрацииEDIКлиент(Контрагент);
			Если Узел <> Неопределено Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(Узел,ЭтотОбъект);	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если Статус = Перечисления.СтатусыПланируемойПоставки.Утвержден Тогда
		ПровестиПланируемаяПоставка(ЭтотОбъект);
	КонецЕсли;
	//{ СИТЕК БрыляковЕЮ 2014-12-19  
	// Исходный текст -->
	//Izh_ОбменEDI.РегПланируемаяПоставка(ЭтотОбъект.Ссылка);
	// <-- Исходный текст 
	// СИТЕК БрыляковЕЮ 2014-12-19 }
КонецПроцедуры

                          
Процедура ПровестиПланируемаяПоставка(Объект)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПланируемаяПоставкаСостав.Номенклатура,
	|	СУММА(ПланируемаяПоставкаСостав.Коэффициент * ПланируемаяПоставкаСостав.Количество) КАК Количество,
	|	СУММА(ПланируемаяПоставкаСостав.Сумма) КАК Сумма,
	|	ПланируемаяПоставкаСостав.Ссылка КАК Регистратор,
	|	ПланируемаяПоставкаСостав.Ссылка КАК ПоДокументу,
	|	ПланируемаяПоставкаСостав.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.КодыОпераций.ЗаказМагазина) КАК КодОперации,
	|	ПланируемаяПоставкаСостав.Ссылка.Контрагент,
	|	ИСТИНА КАК Активность
	|ИЗ
	|	Документ.ПланируемаяПоставка.Состав КАК ПланируемаяПоставкаСостав
	|ГДЕ
	|	ПланируемаяПоставкаСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланируемаяПоставкаСостав.Номенклатура,
	|	ПланируемаяПоставкаСостав.Ссылка,
	|	ПланируемаяПоставкаСостав.Ссылка.Дата,
	|	ПланируемаяПоставкаСостав.Ссылка.Контрагент");
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	тз=Запрос.Выполнить().Выгрузить();
	Объект.Движения.ДвиженияТМЦ.Загрузить(тз);
	Объект.Движения.ДвиженияТМЦ.Записывать=Истина;
	Объект.Движения.ЗаказыИсходящие.Загрузить(тз);
	Объект.Движения.ЗаказыИсходящие.Записывать=Истина;
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		ПроверяемыеРеквизиты.Добавить("Склад");
	КонецЕсли;
КонецПроцедуры

