

Функция ПровестиВозвратнуюНакладнуюОтПокупателя(Объект,Режим)
	
	прчКупляПродажа = Перечисления.ТипыДоговоров.КупляПродажа;
	прчКомиссия = Перечисления.ТипыДоговоров.Комиссия;
	
	
	Если Объект.ТипДоговора = прчКомиссия Тогда
		КодОперации = Перечисления.КодыОпераций.ВозвратРеализация;
	Иначе	
		КодОперации = Перечисления.КодыОпераций.ВозвратОтПокупателя;
	КонецЕсли;
	
	
	
		
	//popn+
	тзОстаткиНоменклатурыИсходная = ПроведениеДокументов.ПолучитьДвиженияДокументаПоРегистру(Объект,"ОстаткиНоменклатуры");
	Для Каждого РегистрДвижений Из Объект.Движения Цикл
		РегистрДвижений.Очистить();
	КонецЦикла;
	//popn-
	
	//serg++
	Для каждого Стр из ЭтотОбъект.Состав Цикл
		Если НЕ ЗначениеЗаполнено(Строка (Стр.Цена))Тогда
			Сообщить ("Невведена сумма в строке:" + " " + Стр.НомерСтроки);
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;	
	//serg--

	тзОстаткиНоменклатуры = Объект.Движения.ОстаткиНоменклатуры.Выгрузить();
	тзОстаткиНоменклатуры.Колонки.Добавить("СуммаПродажи");
	тзОстаткиНоменклатуры.Колонки.Добавить("СуммаНДСПродажи");
	тзОстаткиНоменклатуры.Колонки.Добавить("Контрагент");
	тзОстаткиНоменклатуры.Колонки.Добавить("СтавкаНДС");
	
	тзРеализация = Объект.Движения.Реализация.Выгрузить();
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВозвратнаяНакладнаяОтПокупателяСостав.Номенклатура КАК Номенклатура,
	|	СУММА(ВозвратнаяНакладнаяОтПокупателяСостав.СуммаНДС) КАК СуммаНДСПродажи,
	|	СУММА(ВозвратнаяНакладнаяОтПокупателяСостав.Сумма) КАК СуммаПродажи,
	|	СУММА(ВозвратнаяНакладнаяОтПокупателяСостав.СебестоимостьСумма) КАК Сумма,
	|	СУММА(ВозвратнаяНакладнаяОтПокупателяСостав.СебестоимостьНДС) КАК СуммаНДС,
	|	СУММА(ВозвратнаяНакладнаяОтПокупателяСостав.СебестоимостьСуммаУпр) КАК СуммаУпр,
	|	СУММА(ВозвратнаяНакладнаяОтПокупателяСостав.СебестоимостьНДСУпр) КАК СуммаНДСУпр,
	|	ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование КАК ДокОснование,
	|	СУММА(ВЫРАЗИТЬ(ВозвратнаяНакладнаяОтПокупателяСостав.Коэффициент * ВозвратнаяНакладнаяОтПокупателяСостав.Количество КАК ЧИСЛО(15, 3))) КАК Количество,
	|	ВозвратнаяНакладнаяОтПокупателяСостав.СтавкаНДС КАК СтавкаНДС,
	//++БИТ БВО №0000129900
	|	ВЫБОР КОГДА ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПустаяСсылка)
	|		ТОГДА ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка.ТипДоговора
	|		ИНАЧЕ ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара
	|	КОНЕЦ КАК Статус	
	//--БИТ БВО №0000129900
	|ПОМЕСТИТЬ врСостав
	|ИЗ
	|	Документ.ВозвратнаяНакладнаяОтПокупателя.Состав КАК ВозвратнаяНакладнаяОтПокупателяСостав
	|ГДЕ
	|	ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратнаяНакладнаяОтПокупателяСостав.Номенклатура,
	|	ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование,
	|	ВозвратнаяНакладнаяОтПокупателяСостав.СтавкаНДС,
	//++БИТ БВО №0000129900
	|	ВЫБОР КОГДА ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПустаяСсылка)
	|		ТОГДА ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка.ТипДоговора
	|		ИНАЧЕ ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара
	|	КОНЕЦ
	//--БИТ БВО №0000129900
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратнаяНакладнаяОтПокупателяСостав.Номенклатура,
	|	СУММА(ВЫРАЗИТЬ(ВозвратнаяНакладнаяОтПокупателяСостав.Коэффициент * ВозвратнаяНакладнаяОтПокупателяСостав.Количество КАК ЧИСЛО(15, 3))) КАК Количество,
	|	ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование,
	//++БИТ БВО №0000129900
	|	ВЫБОР КОГДА ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПустаяСсылка)
	|		ТОГДА ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка.ТипДоговора
	|		ИНАЧЕ ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР КОГДА ВозвратнаяНакладнаяОтПокупателяСостав.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|		ТОГДА ВозвратнаяНакладнаяОтПокупателяСостав.Номенклатура.СтавкаНДС
	|		ИНАЧЕ ВозвратнаяНакладнаяОтПокупателяСостав.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС	
	//--БИТ БВО №0000129900
	|ПОМЕСТИТЬ УжеВернули
	|ИЗ
	|	Документ.ВозвратнаяНакладнаяОтПокупателя.Состав КАК ВозвратнаяНакладнаяОтПокупателяСостав
	|ГДЕ
	|	ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование В
	|			(ВЫБРАТЬ
	|				ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование
	|			ИЗ
	|				Документ.ВозвратнаяНакладнаяОтПокупателя.Состав КАК ВозвратнаяНакладнаяОтПокупателяСостав
	|			ГДЕ
	|				ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка = &Ссылка
	|			СГРУППИРОВАТЬ ПО
	|						ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование)
	|	И ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка.Фирма = &Фирма
	|	И ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка <> &Ссылка
	|	И ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка.Дата < &Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратнаяНакладнаяОтПокупателяСостав.Номенклатура,
	|	ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование,
	|	ВЫБОР КОГДА ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПустаяСсылка)
	|		ТОГДА ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка.ТипДоговора
	|		ИНАЧЕ ВозвратнаяНакладнаяОтПокупателяСостав.ТипРеализацииТовара
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ВозвратнаяНакладнаяОтПокупателяСостав.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|		ТОГДА ВозвратнаяНакладнаяОтПокупателяСостав.Номенклатура.СтавкаНДС
	|		ИНАЧЕ ВозвратнаяНакладнаяОтПокупателяСостав.СтавкаНДС
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Состав.Номенклатура,
	|	Состав.СуммаНДСПродажи,
	|	Состав.СуммаПродажи,
	|	Состав.Сумма,
	|	Состав.СуммаНДС,
	|	Состав.СуммаУпр,
	|	Состав.СуммаНДСУпр,
	|	Состав.ДокОснование,
	|	Состав.Количество,
	|	ЕСТЬNULL(УжеВернули.Количество, 0) КАК КоличествоВернули,
	|	Состав.СтавкаНДС,
	//++БИТ БВО №0000129900
	|	Состав.Статус
	//--БИТ БВО №0000129900
	|ПОМЕСТИТЬ СоставИтог
	|ИЗ
	|	врСостав КАК Состав
	|		ЛЕВОЕ СОЕДИНЕНИЕ УжеВернули КАК УжеВернули
	|		ПО Состав.Номенклатура = УжеВернули.Номенклатура
	|			И Состав.ДокОснование = УжеВернули.ДокОснование
	//++БИТ БВО №0000129900
	|			И Состав.Статус = УжеВернули.Статус
	|			И Состав.СтавкаНДС = УжеВернули.СтавкаНДС
	//--БИТ БВО №0000129900
	|;
	|           
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНоменклатуры.Номенклатура,
	|	ОстаткиНоменклатуры.Статус,
	|	СУММА(ОстаткиНоменклатуры.Количество) КАК Количество,
	|	СУММА(ОстаткиНоменклатуры.Сумма) КАК Сумма,
	|	СУММА(ОстаткиНоменклатуры.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ОстаткиНоменклатуры.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ОстаткиНоменклатуры.СуммаНДСУпр) КАК СуммаНДСУпр,
	|	ОстаткиНоменклатуры.Комитент,
	|	ОстаткиНоменклатуры.Регистратор
	|ПОМЕСТИТЬ СебестоимостьОснование
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры КАК ОстаткиНоменклатуры
	|ГДЕ
	|	ОстаткиНоменклатуры.Регистратор В
	|			(ВЫБРАТЬ
	|				ВозвратнаяНакладнаяОтПокупателяСостав.ДокОснование
	|			ИЗ
	|				Документ.ВозвратнаяНакладнаяОтПокупателя.Состав КАК ВозвратнаяНакладнаяОтПокупателяСостав
	|			ГДЕ
	|				ВозвратнаяНакладнаяОтПокупателяСостав.Ссылка = &Ссылка)
	|	И ОстаткиНоменклатуры.Фирма = &Фирма
	|	И ОстаткиНоменклатуры.ВидДвижения = &ВидДвиженияРасход
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНоменклатуры.Номенклатура,
	|	ОстаткиНоменклатуры.Статус,
	|	ОстаткиНоменклатуры.Комитент,
	|	ОстаткиНоменклатуры.Регистратор
	|;
	//++БИТ БВО №0000129900
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставИтог.Статус КАК Статус,
	|	СебестоимостьОснование.Комитент,
	|	СоставИтог.Номенклатура КАК Номенклатура,
	|	СоставИтог.ДокОснование КАК ДокОснование,
	|	СоставИтог.СтавкаНДС КАК СтавкаНДС,	
	|	СУММА(ЕСТЬNULL(СебестоимостьОснование.Количество, 0)) КАК Количество,
	|	СУММА(СебестоимостьОснование.Сумма) КАК Сумма,
	|	СУММА(СебестоимостьОснование.СуммаУпр) КАК СуммаУпр,
	|	СУММА(СебестоимостьОснование.СуммаНДС) КАК СуммаНДС,
	|	СУММА(СебестоимостьОснование.СуммаНДСУпр) КАК СуммаНДСУпр,	
	|	СУММА(СоставИтог.СуммаНДСПродажи) КАК СуммаНДСПродажи,
	|	СУММА(СоставИтог.СуммаПродажи) КАК СуммаПродажи,
	|	СУММА(СоставИтог.Количество) КАК КоличествоВозврат,
	|	СУММА(СоставИтог.КоличествоВернули) КАК КоличествоУжеВернули,
	|	СУММА(СоставИтог.Сумма) КАК СуммаБО,
	|	СУММА(СоставИтог.СуммаНДС) КАК СуммаНДСБО,
	|	СУММА(СоставИтог.СуммаУпр) КАК СуммаУпрБО,
	|	СУММА(СоставИтог.СуммаНДСУпр) КАК СуммаНДСУпрБО
	|ПОМЕСТИТЬ СоставИтогГруппировка
	|ИЗ
	|	СоставИтог КАК СоставИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ СебестоимостьОснование КАК СебестоимостьОснование
	|		ПО СоставИтог.Номенклатура = СебестоимостьОснование.Номенклатура
	|			И СоставИтог.ДокОснование = СебестоимостьОснование.Регистратор	
	|			И ВЫБОР КОГДА СоставИтог.Статус = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|				ИНАЧЕ СоставИтог.Статус = СебестоимостьОснование.Статус
	|			КОНЕЦ	
	|СГРУППИРОВАТЬ ПО
	|	СоставИтог.Статус,
	|	СебестоимостьОснование.Комитент,
	|	СоставИтог.Номенклатура,
	|	СоставИтог.ДокОснование,
	|	СоставИтог.СтавкаНДС
	|;
	//--БИТ БВО №0000129900
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	//++БИТ БВО №0000129900	
	//|	СебестоимостьОснование.Статус КАК Статус,
	|	СоставИтог.Статус КАК Статус,
	//--БИТ БВО №0000129900
	|	СебестоимостьОснование.Комитент,
	|	ЕСТЬNULL(СебестоимостьОснование.Количество, 0) КАК Количество,
	|	СебестоимостьОснование.Сумма КАК Сумма,
	|	СебестоимостьОснование.СуммаУпр КАК СуммаУпр,
	|	СебестоимостьОснование.СуммаНДС КАК СуммаНДС,
	|	СебестоимостьОснование.СуммаНДСУпр КАК СуммаНДСУпр,
	|	СоставИтог.Номенклатура КАК Номенклатура,
	|	СоставИтог.СуммаНДСПродажи КАК СуммаНДСПродажи,
	|	СоставИтог.СуммаПродажи КАК СуммаПродажи,
	|	СоставИтог.КоличествоВозврат КАК КоличествоВозврат,
	|	СоставИтог.КоличествоУжеВернули КАК КоличествоУжеВернули,
	|	СоставИтог.СуммаБО КАК СуммаБО,
	|	СоставИтог.СуммаНДСБО КАК СуммаНДСБО,
	|	СоставИтог.СуммаУпрБО КАК СуммаУпрБО,
	|	СоставИтог.СуммаНДСУпрБО КАК СуммаНДСУпрБО,
	|	СоставИтог.ДокОснование КАК ДокОснование,
	|	СоставИтог.СтавкаНДС КАК СтавкаНДС	
	|ИЗ
	|	СоставИтогГруппировка КАК СоставИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ СебестоимостьОснование КАК СебестоимостьОснование
	|		ПО СоставИтог.Номенклатура = СебестоимостьОснование.Номенклатура
	|			И СоставИтог.ДокОснование = СебестоимостьОснование.Регистратор
	//++БИТ БВО №0000129900
	|			И ВЫБОР КОГДА СебестоимостьОснование.Статус = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|				ИНАЧЕ СоставИтог.Статус = СебестоимостьОснование.Статус
	|			КОНЕЦ
	//++БИТ БВО №0000129900
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Сумма),
	|	СУММА(СуммаУпр),
	|	СУММА(СуммаНДС),
	|	СУММА(СуммаНДСУпр),
	|	МАКСИМУМ(СуммаНДСПродажи),
	|	МАКСИМУМ(СуммаПродажи),
	|	МАКСИМУМ(КоличествоВозврат),
	|	МАКСИМУМ(КоличествоУжеВернули),
	|	МАКСИМУМ(СуммаБО),
	|	МАКСИМУМ(СуммаНДСБО),
	|	МАКСИМУМ(СуммаУпрБО),
	|	МАКСИМУМ(СуммаНДСУпрБО),
	|	МАКСИМУМ(СтавкаНДС)
	|ПО
	|	Номенклатура,	
	//++БИТ БВО №0000129900
	|	Статус,
	|	СтавкаНДС,
	//--БИТ БВО №0000129900
	|	ДокОснование
	|";
	
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	Запрос.УстановитьПараметр("ВидДвиженияРасход",ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Фирма",Объект.Фирма);
	Запрос.УстановитьПараметр("Дата",Объект.Дата);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		//++БИТ БВО №0000129900
		ВыборкаСтатус = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтатус.Следующий() Цикл			
			ВыборкаСтавкаНДС = ВыборкаСтатус.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСтавкаНДС.Следующий() Цикл
			//--БИТ БВО №0000129900
			
				//++БИТ БВО №0000129900
				//ВыборкаДокОснование = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				ВыборкаДокОснование = ВыборкаСтавкаНДС.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				//--БИТ БВО №0000129900
				Пока ВыборкаДокОснование.Следующий() Цикл
					
					Если ВыборкаДокОснование.ДокОснование = Неопределено Тогда
						
						Движение = тзОстаткиНоменклатуры.Добавить();
						Движение.Активность = Истина;
						Движение.Регистратор = Объект.Ссылка;
						Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
						Движение.Период = Объект.Дата;
						Движение.Фирма = Объект.Фирма;
						Движение.МестоХранения = Объект.Склад;
						//++БИТ БВО №0000129900
						//Движение.Статус = прчКупляПродажа;
						Движение.Статус = ВыборкаДокОснование.Статус;
						//--БИТ БВО №0000129900
						Движение.Номенклатура = ВыборкаДокОснование.Номенклатура;
						Движение.СтавкаНДС = ВыборкаДокОснование.СтавкаНДС;
						Движение.Количество = ВыборкаДокОснование.КоличествоВозврат;
						Движение.Сумма = ВыборкаДокОснование.СуммаБО;
						Движение.СуммаУпр = ВыборкаДокОснование.СуммаУпрБО;
						Движение.СуммаНДС = ВыборкаДокОснование.СуммаНДСБО;
						Движение.СуммаНДСУпр = ВыборкаДокОснование.СуммаНДСУпрБО;
						Движение.КодОперации = КодОперации;
						Движение.СуммаПродажи = ВыборкаДокОснование.СуммаПродажи;
						Движение.СуммаНДСПродажи = ВыборкаДокОснование.СуммаНДСПродажи;
						Движение.Контрагент = Объект.Контрагент;
						
					Иначе
						
						чКолСписали = 0;
						чКолСписать = ВыборкаДокОснование.КоличествоВозврат;
						чКолУжеВернули = ВыборкаДокОснование.КоличествоУжеВернули;
						чСуммаПродажиИтог=0;
						чСуммаНДСПродажиИтог=0;
						Выборка = ВыборкаДокОснование.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока Выборка.Следующий() Цикл
							
							чКолОст = Выборка.Количество - чКолУжеВернули;
							Если чКолОст < 0 Тогда
								чКолУжеВернули = чКолУжеВернули - Выборка.Количество;
							Иначе
								чКолУжеВернули = 0;
							КонецЕсли;	
							
							чКолТек = МИН(чКолСписать,чКолОст);
							Если чКолТек<=0 Тогда
								Продолжить;
							КонецЕсли;	
							
							к = чКолТек / Выборка.Количество;
							
							Движение = тзОстаткиНоменклатуры.Добавить();
							Движение.Активность = Истина;
							Движение.Регистратор = Объект.Ссылка;
							Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
							Движение.Период = Объект.Дата;
							Движение.Фирма = Объект.Фирма;
							Движение.МестоХранения = Объект.Склад;
							Движение.Номенклатура = ВыборкаДокОснование.Номенклатура;
							Движение.СтавкаНДС = ВыборкаДокОснование.СтавкаНДС;
							Движение.Количество = чКолТек;
							чСумма = Окр(Выборка.Сумма * к, 2);
							Движение.Сумма = чСумма;
							Движение.СуммаУпр = Окр(Выборка.СуммаУпр * к, 2);
							Движение.СуммаНДС = Окр(Выборка.СуммаНДС * к, 2);
							Движение.СуммаНДСУпр = Окр(Выборка.СуммаНДСУпр * к, 2);
							Движение.КодОперации = КодОперации;
							Движение.Статус = Выборка.Статус;
							Движение.Комитент = Выборка.Комитент;
							Движение.Контрагент = Объект.Контрагент;
							чСуммаПродажиТек = Окр(чКолТек*ВыборкаДокОснование.СуммаПродажи/ВыборкаДокОснование.КоличествоВозврат,2);
							чСуммаНДСПродажиТек = Окр(чКолТек*ВыборкаДокОснование.СуммаНДСПродажи/ВыборкаДокОснование.КоличествоВозврат,2);
							Движение.СуммаПродажи = чСуммаПродажиТек;
							Движение.СуммаНДСПродажи = чСуммаНДСПродажиТек;
							
							Если Выборка.Статус = прчКомиссия Тогда
								ДвижениеРеализация = тзРеализация.Добавить();
								ДвижениеРеализация.Активность = Истина;
								ДвижениеРеализация.Регистратор = Объект.Ссылка;
								ДвижениеРеализация.ВидДвижения = ВидДвиженияНакопления.Расход;
								ДвижениеРеализация.Период = Объект.Дата;
								ДвижениеРеализация.Фирма = Объект.Фирма;
								ДвижениеРеализация.Номенклатура = ВыборкаДокОснование.Номенклатура;
								ДвижениеРеализация.Количество = чКолТек;
								ДвижениеРеализация.Сумма = чСумма;
								ДвижениеРеализация.Комитент = Выборка.Комитент;
								ДвижениеРеализация.СуммаПродажи = чСуммаПродажиТек;
								ДвижениеРеализация.СуммаНДСПродажи = чСуммаНДСПродажиТек;
								ДвижениеРеализация.ПоДокументу = Объект.Ссылка;
							КонецЕсли;	
							
							чСуммаПродажиИтог=чСуммаПродажиИтог+чСуммаПродажиТек;
							чСуммаНДСПродажиИтог=чСуммаНДСПродажиИтог+чСуммаНДСПродажиТек;
							чКолСписать = чКолСписать - чКолТек;
							
						КонецЦикла;
						
						чРазница = ВыборкаДокОснование.СуммаПродажи - чСуммаПродажиИтог;
						Если чРазница <> 0 и Движение <> Неопределено Тогда
							Движение.СуммаПродажи = Движение.СуммаПродажи + чРазница;
							Если Движение.Статус = прчКомиссия Тогда
								ДвижениеРеализация.СуммаПродажи = ДвижениеРеализация.СуммаПродажи + чРазница;
							КонецЕсли;	
						КонецЕсли;	
						чРазницаНДС = ВыборкаДокОснование.СуммаНДСПродажи - чСуммаНДСПродажиИтог;
						Если чРазницаНДС <> 0 и Движение <> Неопределено Тогда
							Движение.СуммаНДСПродажи = Движение.СуммаНДСПродажи + чРазницаНДС;
							Если Движение.Статус = прчКомиссия Тогда
								ДвижениеРеализация.СуммаНДСПродажи = ДвижениеРеализация.СуммаНДСПродажи + чРазницаНДС;
							КонецЕсли;	
						КонецЕсли;
						
					КонецЕсли;	
				КонецЦикла;
		//++БИТ БВО №0000129900		
			КонецЦикла;
		КонецЦикла;
		//--БИТ БВО №0000129900
	КонецЦикла;	
	
	
	//проведение по ТоварыПереданныеНаКомиссию
	Если Объект.ТипДоговора = прчКомиссия Тогда
		тзТоварыПереданныеНаКомиссию = Объект.Движения.ТоварыПереданныеНаКомиссию.Выгрузить();
		
		Параметры = Новый Структура();
		Параметры.Вставить("Запрос",Запрос);
		Параметры.Вставить("тзТоварыПереданныеНаКомиссию",тзТоварыПереданныеНаКомиссию);
		Параметры.Вставить("Контрагент",Объект.Контрагент);
        Параметры.Вставить("Фирма",Объект.Фирма);
		Параметры.Вставить("Объект",Объект);
		ПроведениеДокументов.ПровестиСписаниеПоТоварыПереданныеНаКомиссию(Параметры);		
		
	КонецЕсли;	
	
	Объект.Движения.ОстаткиНоменклатуры.Загрузить(тзОстаткиНоменклатуры);
	
	//popn+    
	тзОстаткиНоменклатурыКопия = тзОстаткиНоменклатуры.Скопировать();
	ПроведениеДокументов.ПодготовитьТаблицыДляСравнения(Объект.Метаданные().Имя,тзОстаткиНоменклатурыКопия, тзОстаткиНоменклатурыИсходная);
	НеБылоИзменений = ПроведениеДокументов.ТаблицыНаборовЗаписейОдинаковые(тзОстаткиНоменклатурыКопия, тзОстаткиНоменклатурыИсходная);
	Объект.Движения.ОстаткиНоменклатуры.Записывать = НЕ НеБылоИзменений;
	//popn-
	
	тзОстаткиНоменклатуры.Свернуть("Регистратор,Активность,Период,Фирма,Контрагент,Номенклатура,КодОперации,Статус,СтавкаНДС","Количество,Сумма,СуммаНДС,СуммаУпр,СуммаНДСУпр,СуммаПродажи,СуммаНДСПродажи");
	Объект.Движения.ДвиженияТМЦ.Загрузить(тзОстаткиНоменклатуры);
	Объект.Движения.Реализация.Загрузить(тзРеализация);
	Если тзТоварыПереданныеНаКомиссию <> неопределено Тогда
		Объект.Движения.ТоварыПереданныеНаКомиссию.Загрузить(тзТоварыПереданныеНаКомиссию);
	КонецЕсли;
	
	Объект.Движения.ТоварыПереданныеНаКомиссию.Записывать=Истина;
	Объект.Движения.ДвиженияТМЦ.Записывать=Истина;
	Объект.Движения.Реализация.Записывать=Истина;

	Возврат Ложь;	
КонецФункции	

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Отказ = ПровестиВозвратнуюНакладнуюОтПокупателя(ЭтотОбъект,Режим);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		
		Фирма = ДанныеЗаполнения.Фирма;
		Контрагент = ДанныеЗаполнения.Контрагент;
		Склад = ДанныеЗаполнения.Склад;
		ДокОснование = ДанныеЗаполнения;
		ТипДоговора = ДанныеЗаполнения.ТипДоговора;
		Валюта = ДанныеЗаполнения.Валюта;
		СтруктураКурса = РаботаСКурсамиВалютКлиентСервер.ПолучитьКурсВалютыНаДату(Валюта);
		Курс = СтруктураКурса.Курс;
		Кратность = СтруктураКурса.Кратность;
		Основание = ДанныеЗаполнения.Основание;
		АдресДоставки = ДанныеЗаполнения.АдресДоставки;
		Грузоотправитель = ДанныеЗаполнения.Грузополучатель;
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСостав.Номенклатура,
		|	РасходнаяНакладнаяСостав.ЕдиницаИзмерения,
		//popn+
		|	РасходнаяНакладнаяСостав.Ссылка КАК ДокОснование,
		|	ДвиженияДокумента.СуммаРасход КАК СебестоимостьСумма,
		|	ДвиженияДокумента.СуммаУпрРасход КАК СебестоимостьСуммаУпр,
		|   ВЫБОР 
		|		КОГДА РасходнаяНакладнаяСостав.Количество = 0
		|	ТОГДА 0
		|		ИНАЧЕ ДвиженияДокумента.СуммаРасход / РасходнаяНакладнаяСостав.Количество
		|	КОНЕЦ КАК СебестоимостьЦена,
		|	ДвиженияДокумента.СуммаНДСРасход КАК СебестоимостьНДС,
		|	ДвиженияДокумента.СуммаНДСУпрРасход КАК СебестоимостьНДСУпр,
		|   ВЫБОР 
		|		КОГДА РасходнаяНакладнаяСостав.Количество = 0
		|	ТОГДА 0
		|		ИНАЧЕ ДвиженияДокумента.СуммаУпрРасход / РасходнаяНакладнаяСостав.Количество
		|	КОНЕЦ КАК СебестоимостьЦенаУпр,
		//popn-
		|	РасходнаяНакладнаяСостав.Количество,
		|	РасходнаяНакладнаяСостав.Коэффициент,
		|	РасходнаяНакладнаяСостав.Цена,
		|	РасходнаяНакладнаяСостав.Сумма,
		|	РасходнаяНакладнаяСостав.СтавкаНДС,
		|	РасходнаяНакладнаяСостав.СуммаНДС
		|ИЗ
		|	Документ.РасходнаяНакладная.Состав КАК РасходнаяНакладнаяСостав
		//popn+
		|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|		ОстаткиНоменклатурыОбороты.*
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Обороты(, , Регистратор, ) КАК ОстаткиНоменклатурыОбороты
		|ГДЕ
		|	ОстаткиНоменклатурыОбороты.Регистратор = &Ссылка) КАК ДвиженияДокумента
		|		ПО РасходнаяНакладнаяСостав.Номенклатура = ДвиженияДокумента.Номенклатура
		//popn-
		|ГДЕ
		|	РасходнаяНакладнаяСостав.Ссылка = &Ссылка
		|	И РасходнаяНакладнаяСостав.Номенклатура.ВидТовара <> ЗНАЧЕНИЕ(Перечисление.ВидыТоваров.Услуга)");
		Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
		Состав.Загрузить(Запрос.Выполнить().Выгрузить());
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Zотчет") Тогда
		
				
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ZотчетСостав.Номенклатура,
		|	ZотчетСостав.ЕдиницаИзмерения,
		//popn+
		|	ZотчетСостав.Ссылка КАК ДокОснование,
		|	ДвиженияДокумента.СуммаРасход КАК СебестоимостьСумма,
		|	ДвиженияДокумента.СуммаУпрРасход КАК СебестоимостьСуммаУпр,
		|   ВЫБОР 
		|		КОГДА ZотчетСостав.Количество = 0
		|	ТОГДА 0
		|		ИНАЧЕ ДвиженияДокумента.СуммаРасход / ZотчетСостав.Количество
		|	КОНЕЦ КАК СебестоимостьЦена,
		|	ДвиженияДокумента.СуммаНДСРасход КАК СебестоимостьНДС,
		|	ДвиженияДокумента.СуммаНДСУпрРасход КАК СебестоимостьНДСУпр,
		|   ВЫБОР 
		|		КОГДА ZотчетСостав.Количество = 0
		|	ТОГДА 0
		|		ИНАЧЕ ДвиженияДокумента.СуммаУпрРасход / ZотчетСостав.Количество
		|	КОНЕЦ КАК СебестоимостьЦенаУпр,
		//popn-
		|	ZотчетСостав.Коэффициент,
		|	ZотчетСостав.Количество,
		|	ZотчетСостав.СтавкаНДС,
		|	ZотчетСостав.СуммаНДС,
		|	ZотчетСостав.Сумма,
		|   ВЫБОР 
		|		КОГДА ZотчетСостав.Количество = 0
		|	ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(ZотчетСостав.Сумма / ZотчетСостав.Количество КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК Цена,
		|	ZотчетСостав.Склад КАК Склад
		|ИЗ
		|	Документ.Zотчет.Состав КАК ZотчетСостав
		//popn+
		|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|		ОстаткиНоменклатурыОбороты.*
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Обороты(, , Регистратор, ) КАК ОстаткиНоменклатурыОбороты
		|ГДЕ
		|	ОстаткиНоменклатурыОбороты.Регистратор = &Ссылка) КАК ДвиженияДокумента
		|		ПО ZотчетСостав.Номенклатура = ДвиженияДокумента.Номенклатура
		//popn-
		|ГДЕ
		|	ZотчетСостав.Ссылка = &Ссылка
		|ИТОГИ ПО
		|	Склад");
		Запрос.УстановитьПараметр("Ссылка",ДанныеЗаполнения);
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		флПервыйСклад=Истина;
		Пока Выборка.Следующий() Цикл
			Если флПервыйСклад Тогда
				флПервыйСклад = Ложь;
				Объект = ЭтотОбъект;
			Иначе
				//Объект = Документы.ВозвратнаяНакладнаяОтПокупателя.СоздатьДокумент();
				//пока не создаем по разным складам
				Продолжить;
			КонецЕсли;
			
			Объект.Фирма = ДанныеЗаполнения.Фирма;
			Объект.Контрагент = Справочники.Контрагенты.РозничныйПокупатель;
			Объект.ДокОснование = ДанныеЗаполнения;
			Объект.ТипДоговора = Перечисления.ТипыДоговоров.КупляПродажа;
			Объект.Склад = Выборка.Склад;
			Объект.Валюта = СтруктурныеЕдиницы.ПолучитьВалютуТекущейСЕ();
			
			СтруктураКурса = РаботаСКурсамиВалютКлиентСервер.ПолучитьКурсВалютыНаДату(Валюта);
			Объект.Курс = СтруктураКурса.Курс;
			Объект.Кратность = СтруктураКурса.Кратность;
			
			ВыборкаСостав = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСостав.Следующий() Цикл
				НоваяСтрока = Объект.Состав.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,ВыборкаСостав);
			КонецЦикла;	
			//Форма = Объект.ПолучитьФорму();
			//Форма.Открыть();
		КонецЦикла;	
		
	КонецЕсли;	
	
	//++БИТ БВО №0000129900
	ПерераспределитьСоставПоОстаткам();
	//--БИТ БВО №0000129900
	
	ИнициализироватьДокумент();
КонецПроцедуры

Процедура ИнициализироватьДокумент() экспорт
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда		
		Если ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(Контрагент.Валюта) Тогда
			Валюта = Контрагент.Валюта;
		Иначе
			Валюта = СтруктурныеЕдиницы.ПолучитьВалютуТекущейСЕ();
		КонецЕсли;
		
		СтруктураКурса = РаботаСКурсамиВалют.ЗаполнитьДанныеКурсаДляВалюты(Валюта);
		Курс = СтруктураКурса.Курс;
		Кратность = СтруктураКурса.Кратность;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТипДоговора) Тогда
		ТипДоговора = Перечисления.ТипыДоговоров.КупляПродажа;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	//Если НЕ ЭтотОбъект.ЭтоНовый() Тогда
	//	Отказ = РаботаСФормамиСервер.ЗапрещеноРедактировать(ЭтотОбъект.Ссылка);
	//	Если Отказ Тогда
	//		Возврат;
	//	КонецЕсли;
	//КонецЕсли;	

	СуммаИтого = Состав.Итог("Сумма");
	СуммаНДСИтого = Состав.Итог("СуммаНДС");
	СтруктурнаяЕдиница = Склад.Владелец;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Автор = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

//++БИТ БВО №0000129900
Процедура ПерераспределитьСоставПоОстаткам() Экспорт
		
	Izh_ОбщегоНазначенияСервер.ПерераспределитьСоставПоОстаткам(ЭтотОбъект);
	
КонецПроцедуры
//--БИТ БВО №0000129900