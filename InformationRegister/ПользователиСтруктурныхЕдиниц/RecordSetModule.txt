
Процедура ПередЗаписью(Отказ, Замещение)
	Если ЭтотОбъект.ОбменДанными.Загрузка Тогда
		УстановитьПривилегированныйРежим(Истина);
		Пользователь = ЭтотОбъект.Отбор.Пользователь.Значение;
		Если ЭтотОбъект.Количество() = 0 Тогда
			ОписаниеОшибки = "";
			НаборЗаписей = РегистрыСведений.ПользователиСтруктурныхЕдиниц.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
			НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество()>0 Тогда
				СтарыйИдентификаторПользователяИБ = НаборЗаписей[0].УникальныйИдентификатор;
				Если НЕ Пользователи.УдалитьПользователяИБ(СтарыйИдентификаторПользователяИБ, ОписаниеОшибки) Тогда
					Если ОписаниеОшибки <> "" Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , , , Отказ);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе	
			НовыеСвойства = Новый Структура;
			ПерваяСтрока = ЭтотОбъект[0];
			Для каждого Колонка Из ЭтотОбъект.Метаданные().Ресурсы Цикл
				НовыеСвойства.Вставить("ПользовательИнфБазы"+Колонка.Имя,ПерваяСтрока[Колонка.Имя]);	
			КонецЦикла;	
			НовыеРоли     = ПерваяСтрока.Роли.Получить();
			
			ОписаниеОшибки = "";
			ИдентификаторПользователяИБ = Пользователи.ПолучитьУникальныйИдентификаторПользователяИБ(Пользователь,ЭтотОбъект.Отбор.СтруктурнаяЕдиница.Значение);
			Если ИдентификаторПользователяИБ = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000") Тогда
				Идентификатор = ПерваяСтрока.Имя;
			Иначе
				Идентификатор = ИдентификаторПользователяИБ;
			КонецЕсли;	
			ПользовательИБСуществует = Пользователи.ПрочитатьПользователяИБ(Идентификатор);
			Если НЕ ПользовательИБСуществует Тогда
				ПользовательИБСуществует = Пользователи.ПрочитатьПользователяИБ(ПерваяСтрока.Имя);
			КонецЕсли;
			
			// {ВетровИА Обращение №0000140168 05.10.2015 Не создавались пользователи ИБ по группе СЕ Платформа.
			Если ЭтотОбъект.Отбор.СтруктурнаяЕдиница.Значение = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница
				ИЛИ ЭтотОбъект.Отбор.СтруктурнаяЕдиница.Значение.Родитель = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница Тогда
					СоздатьНового = Истина;
			Иначе
					СоздатьНового = Ложь;
			КонецЕсли;		
			// ВетровИА Обращение №0000140168 05.10.2015 Не создавались пользователи ИБ по группе СЕ Платформа.}		
					
			Если Пользователи.ЗаписатьПользователяИБ(Идентификатор, НовыеСвойства, НовыеРоли, СоздатьНового, ОписаниеОшибки) Тогда
				Если ПерваяСтрока.УникальныйИдентификатор <> НовыеСвойства.ПользовательИнфБазыУникальныйИдентификатор Тогда
					ПерваяСтрока.УникальныйИдентификатор = НовыеСвойства.ПользовательИнфБазыУникальныйИдентификатор;
				КонецЕсли;
			Иначе
				Если ОписаниеОшибки <> "" Тогда
					ЗаписьЖурналаРегистрации(ОписаниеОшибки, УровеньЖурналаРегистрации.Ошибка, ,Пользователь ,);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры
