
&НаКлиенте
Процедура Номенклатура1ПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		НаборЗаписей.Очистить();
	Иначе
		ЗагрузитьДанные(Элемент.ТекущаяСтрока);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанные(Номенклатура)
	Запрос = Новый Запрос;
	Если Номенклатура.Группа = 1 Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПараметрыЗаказов.СтраховойЗапас,
		|	&Номенклатура,
		|	СтруктурныеЕдиницы.Ссылка КАК СтруктурнаяЕдиница,
		|	ПараметрыЗаказов.МаксимальноеОтклонение,
		|	ПараметрыЗаказов.МинимальноеОтклонение
		|ИЗ
		|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЗаказов КАК ПараметрыЗаказов
		|		ПО (ПараметрыЗаказов.СтруктурнаяЕдиница = СтруктурныеЕдиницы.Ссылка)
		|			И (ПараметрыЗаказов.Номенклатура = &Номенклатура)
		|"+?(ПараметрыСеанса.ЭтоЦентр,""," ГДЕ СтруктурныеЕдиницы.Ссылка = &СтруктурнаяЕдиница")+"
		|УПОРЯДОЧИТЬ ПО
		|	СтруктурныеЕдиницы.Код";
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтатусыНоменклатуры.Номенклатура,
		|	СтатусыНоменклатуры.СтруктурнаяЕдиница,
		|	ПараметрыЗаказов.СтраховойЗапас,
		|	ПараметрыЗаказов.МаксимальноеОтклонение,
		|	ПараметрыЗаказов.МинимальноеОтклонение
		|ИЗ
		|	РегистрСведений.ПараметрыЗаказов КАК ПараметрыЗаказов
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыНоменклатуры КАК СтатусыНоменклатуры
		|		ПО ПараметрыЗаказов.СтруктурнаяЕдиница = СтатусыНоменклатуры.СтруктурнаяЕдиница
		|			И ПараметрыЗаказов.Номенклатура = СтатусыНоменклатуры.Номенклатура
		|ГДЕ
		|	СтатусыНоменклатуры.Номенклатура = &Номенклатура
		|"+?(ПараметрыСеанса.ЭтоЦентр,"","   И СтатусыНоменклатуры.СтруктурнаяЕдиница = &СтруктурнаяЕдиница")+"
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтатусыНоменклатуры.СтруктурнаяЕдиница.Код";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница);
	
	Результат = Запрос.Выполнить();
	
	НаборЗаписей.Загрузить(Результат.Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура СтраховойЗапасПриИзменении(Элемент)
	СтруктураПараметров = Новый Структура("Номенклатура,СтруктурнаяЕдиница,СтраховойЗапас,МаксимальноеОтклонение,МинимальноеОтклонение",
		Элементы.НаборЗаписей.ТекущиеДанные.Номенклатура,
		Элементы.НаборЗаписей.ТекущиеДанные.СтруктурнаяЕдиница,
		Элементы.НаборЗаписей.ТекущиеДанные.СтраховойЗапас,
		Элементы.НаборЗаписей.ТекущиеДанные.МаксимальноеОтклонение,
		Элементы.НаборЗаписей.ТекущиеДанные.МинимальноеОтклонение);
	ЗаписатьСтраховойЗапасНаСервере(СтруктураПараметров);
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСтраховойЗапасНаСервере(Строка)
	Запись = РегистрыСведений.ПараметрыЗаказов.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Строка);
	Запись.Записать();
КонецПроцедуры

&НаКлиенте
Процедура Копировать(Команда)
	
	// Копировать данные с одной структурной единицы на другую.
	ПараметрыФормы = Новый Структура;
	Если Не Элементы.НаборЗаписей.ТекущиеДанные = Неопределено Тогда
		ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Элементы.НаборЗаписей.ТекущиеДанные.СтруктурнаяЕдиница);
	КонецЕсли;
	Если Элементы.Номенклатура.ВыделенныеСтроки.Количество() > 0 Тогда
		СписокЗн = Новый СписокЗначений;
		СписокЗн.ЗагрузитьЗначения(Элементы.Номенклатура.ВыделенныеСтроки);
		ПараметрыФормы.Вставить("Номенклатура", СписокЗн);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ПараметрыЗаказов.Форма.ФормаКопирования", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Копирование_ПараметрыЗаказов" Тогда
		Номенклатура1ПриАктивизацииСтроки(Элементы.Номенклатура);
	КонецЕсли;
КонецПроцедуры

//{БредовЮГ 2015-03-16 Наряд 000144173 от 05.03.2015 13:12:02
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтоЦентр = ПараметрыСеанса.ЭтоЦентр;
	Элементы.СтруктурнаяЕдиница.Видимость = ЭтоЦентр;
	Элементы.НаборЗаписей.ТолькоПросмотр = ЭтоЦентр;
	Элементы.НаборЗаписей.КоманднаяПанель.Видимость = НЕ ЭтоЦентр;
	Элементы.НаборЗаписей.КоманднаяПанель.Доступность = НЕ ЭтоЦентр;
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекДанные = Элементы.Номенклатура.ТекущаяСтрока;
	Если ЗначениеЗаполнено(ТекДанные) И ОбщегоНазначения.ПолучитьЗначениеРеквизита(ТекДанные,"ТипТовара") = ПредопределенноеЗначение("Перечисление.ТипыТоваров.Весовой") Тогда
		Элементы.НаборЗаписей.ПодчиненныеЭлементы.СтраховойЗапас.ФорматРедактирования = "ЧЦ=13; ЧДЦ=3";
	Иначе
		Элементы.НаборЗаписей.ПодчиненныеЭлементы.СтраховойЗапас.ФорматРедактирования = "ЧЦ=13; ЧДЦ=0";
	КонецЕсли;
КонецПроцедуры
//БредовЮГ 2015-03-16 Наряд 000144173 от 05.03.2015 13:12:02}

