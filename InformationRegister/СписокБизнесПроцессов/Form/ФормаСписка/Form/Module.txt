////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗадачаВыполнена" ИЛИ ИмяСобытия = "ЗадачаИзменена" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		ОткрытьЗначение(Элементы.Список.ТекущиеДанные.БизнесПроцесс);
 	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ОткрытьЗначение(Элемент.ТекущиеДанные.БизнесПроцесс);
		Отказ = Истина;
 	КонецЕсли;       
КонецПроцедуры

&НаКлиенте
Процедура ПометкаУдаления(Команда)
	УправлениеБизнесПроцессамиКлиент.СписокБизнесПроцессовПометкаУдаления(Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЗавершенныеПриИзменении(Элемент)
	УстановитьОтбор(Новый Структура("ПоказыватьЗавершенные", ПоказыватьЗавершенные));
КонецПроцедуры

&НаСервере
Процедура УстановитьОтбор(ПараметрыОтбора)
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(Список.Отбор, "Завершен");
	
	Если НЕ ПараметрыОтбора["ПоказыватьЗавершенные"] Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			Список.Отбор, "Завершен", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьОтбор(Новый Структура("ПоказыватьЗавершенные", ПоказыватьЗавершенные));
КонецПроцедуры


&НаКлиенте
Процедура ЗавершитьБизнесПроцесс(Команда)
	ТекстВопроса = "Данный режим приводит к пометке бизнес-процесса и задач как выполненных, но при этом происходит не штатное завершение бизнес-процесса, а фактически, прерывание выполнения выбранного бизнес-процесса.
					|Вы уверены, что хотите прервать выбранный бизнес-процесс?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, "Завершить бизнес-процесс?", КодВозвратаДиалога.Нет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ТекстОшибки = "";
		Если Не УправлениеБизнесПроцессамиСервер.ЗавершитьБизнесПроцессНаСервере(Элементы.Список.ТекущиеДанные.БизнесПроцесс, ТекстОшибки) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "При аварийном завершении бизнес-процесса произошла ошибка, поэтому завершение отменено.
							|Описание ошибки: " + ТекстОшибки;
			Сообщение.Сообщить();
		Иначе
			Элементы.Список.Обновить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

