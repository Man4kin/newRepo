
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") = Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	ДеревоСкладов = РеквизитФормыВЗначение("ТаблицаСкладов");
	
	СтруктурнаяЕдиница = Параметры.Отбор.СтруктурнаяЕдиница;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Склады.Владелец.Наименование КАК НаименованиеСЕ,
	|	Склады.Ссылка.Наименование КАК НаименованиеСклада,
	|	Склады.Владелец КАК СтруктурнаяЕдиница,
	|   Склады.ВидСклада КАК ВидСклада,
	|	Склады.Ссылка КАК Склад,
	|	ВЫБОР
	|		КОГДА Склады.Ссылка = СкладыАвтозаказа.Склад
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Флаг
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СкладыАвтозаказа КАК СкладыАвтозаказа
	|		ПО (СкладыАвтозаказа.Склад = Склады.Ссылка)
	|       И (СкладыАвтозаказа.СтруктурнаяЕдиница = Склады.Владелец)
	|	ГДЕ Склады.Владелец В ИЕРАРХИИ (&Владелец)
	|УПОРЯДОЧИТЬ ПО
	|   НаименованиеСЕ, НаименованиеСклада
	|ИТОГИ ПО
	|   СтруктурнаяЕдиница";
	
	Запрос.УстановитьПараметр("Владелец", СтруктурнаяЕдиница);
	Запрос.Текст = ТекстЗапроса;		
	Результат = Запрос.Выполнить();
	ВыборкаСтруктурнаяЕдиница = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСтруктурнаяЕдиница.Следующий() Цикл
		НоваяСтрокаСЕ = ДеревоСкладов.Строки.Добавить();
		НоваяСтрокаСЕ.Картинка = 1;
		НоваяСтрокаСЕ.СтруктурнаяЕдиница = ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница;
		НоваяСтрокаСЕ.Представление      = Строка(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница);
		ВыборкаДетальныеЗаписи = ВыборкаСтруктурнаяЕдиница.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрокаСклад = НоваяСтрокаСЕ.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСклад, ВыборкаДетальныеЗаписи);
			НоваяСтрокаСклад.СтруктурнаяЕдиница = ВыборкаДетальныеЗаписи.Склад;
			НоваяСтрокаСклад.Представление      = Строка(ВыборкаДетальныеЗаписи.Склад)+" ("+Строка(ВыборкаДетальныеЗаписи.ВидСклада)+")";
			НоваяСтрокаСклад.Картинка = 0;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоСкладов, "ТаблицаСкладов");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПроизводствПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ТаблицаСкладов.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Склад) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
	
	Коллекция = ТаблицаСкладов.ПолучитьЭлементы();
	Для Каждого Строка Из Коллекция Цикл
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		Элементы.ТаблицаСкладов.Развернуть(ИдентификаторСтроки);
	КонецЦикла;
	
	Оповестить("ЗаписьСкладыАвтозаказа");
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	ДеревоСкладов = РеквизитФормыВЗначение("ТаблицаСкладов");
	
	ТаблицаСкладовАвтозаказа = Новый ТаблицаЗначений;
	ТаблицаСкладовАвтозаказа.Колонки.Добавить("Склад");
	ТаблицаСкладовАвтозаказа.Колонки.Добавить("СтруктурнаяЕдиница");
	
	Для Каждого СтрокаСЕ Из ДеревоСкладов.Строки Цикл
		Для Каждого Строка Из СтрокаСЕ.Строки Цикл
			Если Строка.Флаг Тогда
				Запись = ТаблицаСкладовАвтозаказа.Добавить();
				Запись.Склад = Строка.Склад;
				Запись.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
			КонецЕсли;
		КонецЦикла;	
	КонецЦикла;	
	
	НаборЗаписей = РегистрыСведений.СкладыАвтозаказа.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(СтруктурнаяЕдиница);
	НаборЗаписей.Загрузить(ТаблицаСкладовАвтозаказа);
	НаборЗаписей.Записать(ИСТИНА);
	
	ЗначениеВРеквизитФормы(ДеревоСкладов, "ТаблицаСкладов");
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	СнятьФлажкиНаСервере();
	
	Коллекция = ТаблицаСкладов.ПолучитьЭлементы();
	Для Каждого Строка Из Коллекция Цикл
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		Элементы.ТаблицаСкладов.Развернуть(ИдентификаторСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СнятьФлажкиНаСервере()
	
	ДеревоСкладов = РеквизитФормыВЗначение("ТаблицаСкладов");
	Для Каждого СтрокаСЕ Из ДеревоСкладов.Строки Цикл
		Для Каждого Строка Из СтрокаСЕ.Строки Цикл
			Строка.Флаг = ЛОЖЬ;
		КонецЦикла;	
	КонецЦикла;	
	ЗначениеВРеквизитФормы(ДеревоСкладов, "ТаблицаСкладов");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьФлажкиНаСервере();
	
	Коллекция = ТаблицаСкладов.ПолучитьЭлементы();
	Для Каждого Строка Из Коллекция Цикл
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		Элементы.ТаблицаСкладов.Развернуть(ИдентификаторСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФлажкиНаСервере()
	
	ДеревоСкладов = РеквизитФормыВЗначение("ТаблицаСкладов");
	Для Каждого СтрокаСЕ Из ДеревоСкладов.Строки Цикл
		Для Каждого Строка Из СтрокаСЕ.Строки Цикл
			Строка.Флаг = ИСТИНА;
		КонецЦикла;	
	КонецЦикла;	
	ЗначениеВРеквизитФормы(ДеревоСкладов, "ТаблицаСкладов");
	
КонецПроцедуры



