
Процедура ПриЗаписи(Отказ, Замещение)
	УстановитьПривилегированныйРежим(Истина);
	Если ЭтотОбъект.Отбор.СтруктурнаяЕдиница.Значение = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница Тогда //V
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаRMSОтправка Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменRIB","ОтправкаДанныхRIB",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""),"РегламентноеЗаданиеОтправки");
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаRMSПолучение Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменRIB","ПолучениеДанныхRIB",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""),"РегламентноеЗаданиеПолучения");
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаЦВД Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменЦВД","ПередачаДанныхЦВД",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""));
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаФинансы Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменТД6","ПередачаДанныхФинансы",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""));
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаORDM Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменORDM","ПередачаДанныхORDM",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""));
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаSMSОтправка Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменSMS","ОтправкаДанныхSMS",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""),"РегламентноеЗаданиеОтправки");
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаSMSПолучение Тогда
				УстановитьРасписаниеРегламентногоЗадания("ОбменSMS","ПолучениеДанныхSMS",?(ЗначениеЗаполнено(Запись.Значение),ЗначениеИзСтрокиВнутр(Запись.Значение),""),"РегламентноеЗаданиеПолучения");
			ИначеЕсли Запись.Свойство = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.РасписаниеОбменаExtSystem Тогда
				ТекущееРегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ПолучениеДанныхExtSystem);
				Если ПустаяСтрока(Запись.Значение) Тогда
					ТекущееРегламентноеЗадание.Использование = Ложь;
				Иначе	
					ТекущееРегламентноеЗадание.Расписание = ЗначениеИзСтрокиВнутр(Запись.Значение);
					ТекущееРегламентноеЗадание.Использование = Истина;
				КонецЕсли;	
				ТекущееРегламентноеЗадание.Записать();
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	Если ПараметрыСеанса.ЭтоЦентр И ЭтотОбъект.Отбор.Свойство.Значение.Родитель  = ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.ЛокальныеВебСервисы Тогда
		Выборка = ПланыОбмена.ОбменRIB.Выбрать();
		ЭтотУзел = ПланыОбмена.ОбменRIB.ЭтотУзел();
		Пока Выборка.Следующий() Цикл
			Узел = Выборка.Ссылка;
			Если Узел = ЭтотУзел Тогда
				Продолжить;
			КонецЕсли;
			ПланыОбмена.ЗарегистрироватьИзменения(Узел,ЭтотОбъект);	
		КонецЦикла;	
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьРасписаниеРегламентногоЗадания(ПланОбмена,Идентификатор,Расписание,РеквизитЗадания = "РегламентноеЗадание")
	Выборка = ПланыОбмена[ПланОбмена].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Ссылка = ПланыОбмена[ПланОбмена].ЭтотУзел() Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ПустаяСтрока(Выборка[РеквизитЗадания]) Тогда
			ТекущееРегламентноеЗадание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(Новый УникальныйИдентификатор(Выборка[РеквизитЗадания]));
		Иначе
			ТекущееРегламентноеЗадание = Неопределено;
		КонецЕсли;
		
		Если ТекущееРегламентноеЗадание = Неопределено Тогда
			ТекущееРегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Идентификатор);
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(Выборка.Ссылка);
			ТекущееРегламентноеЗадание.Параметры = МассивПараметров;
			Если ПустаяСтрока(Расписание) Тогда
				ТекущееРегламентноеЗадание.Использование = Ложь;
			Иначе	
				ТекущееРегламентноеЗадание.Расписание = Расписание;
				ТекущееРегламентноеЗадание.Использование = Истина;
			КонецЕсли;	
			ТекущееРегламентноеЗадание.Записать();
			ОбъектПланОбмена = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектПланОбмена[РеквизитЗадания] = Строка(ТекущееРегламентноеЗадание.УникальныйИдентификатор);
			ОбъектПланОбмена.Записать();
		Иначе
			Если Расписание = "" Тогда
				ТекущееРегламентноеЗадание.Использование = Ложь;
			Иначе	
				ТекущееРегламентноеЗадание.Расписание = Расписание;
				ТекущееРегламентноеЗадание.Использование = Истина;
			КонецЕсли;	
			ТекущееРегламентноеЗадание.Записать();
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры