
Процедура СоздатьВТОграниченияПоВозвратамИСписаниям(МенеджерВТ, Знач СписокНоменклатуры, СтруктурнаяЕдиница, Знач ДатаСреза = Неопределено, Отбор = Неопределено) Экспорт 
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Состав.Номенклатура,
		|	Состав.ЗапрещенВозвратНаРЦ,
		|	Состав.ЗапрещеноСписаниеВМагазине,
		|	Состав.ЗапрещеноПеремещениеМеждуМагазинами
		|ПОМЕСТИТЬ ВТ_ПравилаПерепродажи
		|ИЗ
		|	(ВЫБРАТЬ
		|		СписокНоменклатуры.Ссылка КАК Номенклатура,
		|		&ЗапрещенВозвратНаРЦ КАК ЗапрещенВозвратНаРЦ,
		|		&ЗапрещеноСписаниеВМагазине КАК ЗапрещеноСписаниеВМагазине,
		|		&ЗапрещеноПеремещениеМеждуМагазинами КАК ЗапрещеноПеремещениеМеждуМагазинами
		|	ИЗ
		|		Справочник.Номенклатура КАК СписокНоменклатуры,
		|		&ОграниченияПоВозвратам КАК ОграниченияПоВозвратам
		|	ГДЕ
		|		СписокНоменклатуры.Ссылка В(&Номенклатура)
		|		И &Условие) КАК Состав";
		
	ШаблонОграниченияПоВозвратам = "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Izh_ОграниченияПоВозвратамИСписаниям.СрезПоследних(
		|				{&ДатаСреза},
		|				СтруктурнаяЕдиница = &СЕ
		|					) КАК ОграниченияПоВозвратам%1";
	ШаблонОбычноеСоединение = "
		|		ПО СписокНоменклатуры.Ссылка%2 = ОграниченияПоВозвратам%1.Номенклатура";
	ШаблонКорень = "
		|		ПО (ОграниченияПоВозвратам%1.Номенклатура = """")";
	ШаблонВыбор = 
		"		ВЫБОР %1
		|		КОНЕЦ";
	ШаблонПоле = "
		|			КОГДА НЕ ОграниченияПоВозвратам%1.Номенклатура ЕСТЬ NULL 
		|				ТОГДА ОграниченияПоВозвратам%1.%2";
	ШаблонУсловиеОбертка = "НЕ (%1)";
	ШаблонУсловие = "ОграниченияПоВозвратам%1.Номенклатура ЕСТЬ NULL";
	ШаблонСоединительУсловий = "
		|		И ";
		
	Таблицы = "";
	Поля = Новый Структура("ЗапрещенВозвратНаРЦ, ЗапрещеноСписаниеВМагазине, ЗапрещеноПеремещениеМеждуМагазинами");
	Условие = Новый Массив();
	
	УровеньВложенности = 7;
	Для Сч = 1 По УровеньВложенности Цикл
		
		Нпп = XMLСтрока(Сч);
		
		ОграниченияПоВозвратам = ШаблонОграниченияПоВозвратам + ?(Сч = УровеньВложенности, ШаблонКорень, ШаблонОбычноеСоединение);
		Родитель = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов(".Родитель", Сч - 1);
		Таблицы = Таблицы + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОграниченияПоВозвратам, Нпп, Родитель);
		
		Условие.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУсловие, Нпп));
		
		Для каждого Поле Из Поля Цикл
			Значение = Поле.Значение;
			Если Значение = Неопределено Тогда
				Значение = Новый Массив();
				Поля[Поле.Ключ] = Значение;
			КонецЕсли;
			Значение.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПоле, Нпп, Поле.Ключ)
			);
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КАК СписокНоменклатуры,", "КАК СписокНоменклатуры");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОграниченияПоВозвратам КАК ОграниченияПоВозвратам", Таблицы);
	
	Условие = СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(Условие, ШаблонСоединительУсловий);
	Условие = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУсловиеОбертка, Условие);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", Условие);
	
	Для каждого Поле Из Поля Цикл
		ТекстПоля = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонВыбор, 
			СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(Поле.Значение, "")
		);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&" + Поле.Ключ, ТекстПоля);
	КонецЦикла;
	
	Построитель = Новый ПостроительЗапроса();
	Построитель.Текст = ТекстЗапроса;
	Построитель.ЗаполнитьНастройки();
	
	Построитель.Параметры.Вставить("СЕ", СтруктурнаяЕдиница);
	Построитель.Параметры.Вставить("Номенклатура", СписокНоменклатуры);
	
	Если ЗначениеЗаполнено(ДатаСреза) Тогда
		Построитель.Отбор.Добавить("ДатаСреза").Установить(ДатаСреза);
	КонецЕсли;
	
	Izh_ОбщегоНазначения.УстановитьОтборПостроителя(Построитель, Отбор);
	
	Запрос = Построитель.ПолучитьЗапрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ОграниченияПоВозвратамИСписаниям(Знач СписокНоменклатуры, СтруктурнаяЕдиница, Знач ДатаСреза = Неопределено, Отбор = Неопределено) Экспорт 
	
	ВТ = Новый МенеджерВременныхТаблиц();
	СоздатьВТОграниченияПоВозвратамИСписаниям(ВТ, СписокНоменклатуры, СтруктурнаяЕдиница, ДатаСреза, Отбор);
	
	Возврат Izh_ОбщегоНазначения.ПолучитьВТ(ВТ, "ВТ_ПравилаПерепродажи");
	
КонецФункции

