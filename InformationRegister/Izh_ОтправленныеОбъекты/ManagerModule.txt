
Процедура ДобавитьОбъект(Знач Объект) Экспорт 
	
	Если ТипЗнч(Объект) = Тип("УдалениеОбъекта") Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = СсылкаНаОбъект(Объект);
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Запись = РегистрыСведений.Izh_ОтправленныеОбъекты.СоздатьМенеджерЗаписи();
	Запись.Объект = Ссылка;
	Запись.ДатаОтправки = ТекущаяДата();
	Запись.Записать(Истина);
	
КонецПроцедуры

Функция ОбъектОтправлен(Знач Объект) Экспорт 
	
	Ссылка = СсылкаНаОбъект(Объект);
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запись = РегистрыСведений.Izh_ОтправленныеОбъекты.СоздатьМенеджерЗаписи();
	Запись.Объект = Ссылка;
	Запись.Прочитать();
	
	Возврат Запись.Выбран();
	
КонецФункции

Функция СсылкаНаОбъект(Знач Объект)
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Объект) Тогда
		Ссылка = Объект;
	Иначе
		Попытка
			Ссылка = Объект.Ссылка;
		Исключение
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ Метаданные.РегистрыСведений.Izh_ОтправленныеОбъекты.Измерения.Объект.Тип.СодержитТип(ТипЗнч(Ссылка)) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции
