
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Отбор") = Ложь Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	ДеревоПроизводств = РеквизитФормыВЗначение("ТаблицаПроизводств");
	
	Если Параметры.Отбор.Свойство("Продукция") Тогда
		Продукция = Параметры.Отбор.Продукция;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|   ПродукцияПоЦехам.Склад.Владелец.Наименование КАК НаименованиеСЕ,
		|	ПродукцияПоЦехам.Склад.Ссылка.Наименование КАК НаименованиеСклада,
		|	ПродукцияПоЦехам.Склад.Владелец КАК СтруктурнаяЕдиница,
		|   ПродукцияПоЦехам.Склад.ВидСклада КАК ВидСклада,
		|	ПродукцияПоЦехам.Склад КАК Склад,
		|	ПродукцияПоЦехам.Склад КАК СкладВРегистре,
		|	ПродукцияПоЦехам.ФлагАктивности КАК Флаг,
		|	ПродукцияПоЦехам.ФлагАктивности КАК ФлагАктивности
		|ИЗ
		|	РегистрСведений.ПродукцияПоЦехам КАК ПродукцияПоЦехам
		|ГДЕ
		|	ПродукцияПоЦехам.Продукция = &Продукция
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродукцияПоЦехам.Склад.Владелец,
		|   ПродукцияПоЦехам.Склад.ВидСклада,
		|	ПродукцияПоЦехам.Склад,
		|	ПродукцияПоЦехам.ФлагАктивности
		|УПОРЯДОЧИТЬ ПО
		|   НаименованиеСЕ, НаименованиеСклада
		|ИТОГИ ПО
		|   СтруктурнаяЕдиница";
		
		Запрос.УстановитьПараметр("Продукция", Продукция);
		
		Результат = Запрос.Выполнить();
		ВыборкаСтруктурнаяЕдиница = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтруктурнаяЕдиница.Следующий() Цикл
			НоваяСтрокаСЕ = ДеревоПроизводств.Строки.Добавить();
			НоваяСтрокаСЕ.Картинка = 1;
			НоваяСтрокаСЕ.СтруктурнаяЕдиница = ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница;
			НоваяСтрокаСЕ.Представление      = Строка(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница);
			ВыборкаДетальныеЗаписи = ВыборкаСтруктурнаяЕдиница.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				НоваяСтрокаСклад = НоваяСтрокаСЕ.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСклад, ВыборкаДетальныеЗаписи);
				НоваяСтрокаСклад.СтруктурнаяЕдиница = ВыборкаДетальныеЗаписи.Склад;
				НоваяСтрокаСклад.Представление      = Строка(ВыборкаДетальныеЗаписи.Склад)+" ("+Строка(ВыборкаДетальныеЗаписи.ВидСклада)+")";
				НоваяСтрокаСклад.Картинка = 0;
			КонецЦикла;
		КонецЦикла;
		
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.ФормаСохранить.Видимость = Ложь;
		Элементы.ФормаКоманднаяПанель.ПодчиненныеЭлементы.СнятьФлажки.Видимость = Ложь;
		Элементы.ТаблицаПроизводств.ПодчиненныеЭлементы.ТаблицаПроизводствФлаг.Видимость = Ложь;
	ИначеЕсли Параметры.Отбор.Свойство("ТехнологическаяКарта") Тогда	
		ТехнологическаяКарта = Параметры.Отбор.ТехнологическаяКарта;
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Склады.Владелец.Наименование КАК НаименованиеСЕ,
		|	Склады.Ссылка.Наименование КАК НаименованиеСклада,
		|	Склады.Владелец КАК СтруктурнаяЕдиница,
		|   Склады.ВидСклада КАК ВидСклада,
		|	Склады.Ссылка КАК Склад,
		|	ПродукцияПоЦехам.Склад КАК СкладВРегистре,
		|	ПродукцияПоЦехам.ФлагАктивности КАК ФлагАктивности,
		|	ВЫБОР
		|		КОГДА Склады.Ссылка = ПродукцияПоЦехам.Склад
		|			ТОГДА ПродукцияПоЦехам.ФлагАктивности
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Флаг
		|ИЗ
		|	Справочник.Склады КАК Склады
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПродукцияПоЦехам КАК ПродукцияПоЦехам
		|		ПО (ПродукцияПоЦехам.Склад = Склады.Ссылка)
		|			И (ПродукцияПоЦехам.ТехнологическаяКарта = &ТехнологическаяКарта)
		|ГДЕ
		|	Склады.ВидСклада = &ВидСкладаПроизводство";
		
		Если НЕ СтруктурныеЕдиницы.ЕстьВГруппеТипСЕ(Перечисления.ТипыСтруктурныхЕдиниц.Производство) Тогда
			ТекстЗапроса = ТекстЗапроса + " 
			|	И Склады.Владелец В ИЕРАРХИИ (&Владелец)";
			Запрос.УстановитьПараметр("Владелец", ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница); //V
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса +
		"
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Склады.Владелец.Наименование КАК НаименованиеСЕ,
		|	Склады.Ссылка.Наименование КАК НаименованиеСклада,
		|	Склады.Владелец КАК СтруктурнаяЕдиница,
		|   Склады.ВидСклада КАК ВидСклада,
		|	Склады.Ссылка КАК Склад,
		|	ПродукцияПоЦехам.Склад КАК СкладВРегистре,
		|	ПродукцияПоЦехам.ФлагАктивности КАК ФлагАктивности,
		|	ВЫБОР
		|		КОГДА Склады.Ссылка = ПродукцияПоЦехам.Склад
		|			ТОГДА ПродукцияПоЦехам.ФлагАктивности
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Флаг
		|ИЗ
		|	Справочник.Склады КАК Склады
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПродукцияПоЦехам КАК ПродукцияПоЦехам
		|		ПО (ПродукцияПоЦехам.Склад = Склады.Ссылка)
		|			И (ПродукцияПоЦехам.ТехнологическаяКарта = &ТехнологическаяКарта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыСтруктурныхЕдиниц КАК ПараметрыСтруктурныхЕдиниц
		|		ПО (ПараметрыСтруктурныхЕдиниц.СтруктурнаяЕдиница = Склады.Владелец)
		|			И (ПараметрыСтруктурныхЕдиниц.Свойство = &РазрешитьПроизводствоРозница)
		|ГДЕ
		|	Склады.ВидСклада = &ВидСкладаРозничный
		|	И ПараметрыСтруктурныхЕдиниц.Значение = ИСТИНА";
		
		Если НЕ СтруктурныеЕдиницы.ЕстьВГруппеТипСЕ(Перечисления.ТипыСтруктурныхЕдиниц.Производство) Тогда
			ТекстЗапроса = ТекстЗапроса + " 
			|	И Склады.Владелец В ИЕРАРХИИ (&Владелец)";
			Запрос.УстановитьПараметр("Владелец", ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница); //V
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + 
		"
		|УПОРЯДОЧИТЬ ПО
		|   НаименованиеСЕ, НаименованиеСклада
		|ИТОГИ ПО
		|   СтруктурнаяЕдиница";
		
		Запрос.Текст = ТекстЗапроса;		
		
		Запрос.УстановитьПараметр("ТехнологическаяКарта", ТехнологическаяКарта);
		Запрос.УстановитьПараметр("ВидСкладаПроизводство", Перечисления.ВидыСкладов.Производство);
		Запрос.УстановитьПараметр("ВидСкладаРозничный", Перечисления.ВидыСкладов.Розничный);
		Запрос.УстановитьПараметр("РазрешитьПроизводствоРозница", ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.ИспользоватьРозничныеСкладыВПроизводстве);
		
		Результат = Запрос.Выполнить();
		ВыборкаСтруктурнаяЕдиница = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСтруктурнаяЕдиница.Следующий() Цикл
			НоваяСтрокаСЕ = ДеревоПроизводств.Строки.Добавить();
			НоваяСтрокаСЕ.Картинка = 1;
			НоваяСтрокаСЕ.СтруктурнаяЕдиница = ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница;
			НоваяСтрокаСЕ.Представление      = Строка(ВыборкаСтруктурнаяЕдиница.СтруктурнаяЕдиница);
			ВыборкаДетальныеЗаписи = ВыборкаСтруктурнаяЕдиница.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				НоваяСтрокаСклад = НоваяСтрокаСЕ.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСклад, ВыборкаДетальныеЗаписи);
				НоваяСтрокаСклад.СтруктурнаяЕдиница = ВыборкаДетальныеЗаписи.Склад;
				НоваяСтрокаСклад.Представление      = Строка(ВыборкаДетальныеЗаписи.Склад)+" ("+Строка(ВыборкаДетальныеЗаписи.ВидСклада)+")";
				НоваяСтрокаСклад.Картинка = 0;
			КонецЦикла;
		КонецЦикла;
		
	Иначе	
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	Если НЕ СтруктурныеЕдиницы.ЕстьВГруппеТипСЕ(Перечисления.ТипыСтруктурныхЕдиниц.Производство) Тогда
		Элементы.ТаблицаПроизводств.ТолькоПросмотр = Истина;
		Элементы.ФормаСохранить.Видимость = Ложь;
		Элементы.СнятьФлажки.Видимость = Ложь;
	КонецЕсли;	
	
	ЗначениеВРеквизитФормы(ДеревоПроизводств, "ТаблицаПроизводств");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПроизводствПередНачаломИзменения(Элемент, Отказ)
	СписокТоваров = Новый СписокЗначений;
	
	ТекущиеДанные = Элементы.ТаблицаПроизводств.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Склад) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ПроверкаСтатусовНоменклатуры(Элементы.ТаблицаПроизводств.ТекущиеДанные.Склад,СписокТоваров) = Ложь Тогда
		ТекстСообщения = "Технологическая карта не может быть отправлена в выбранный цех!
		|Следующая номенклатура не имеет активный статус:";
		Для Каждого Строка Из СписокТоваров Цикл
			ТекстСообщения = ТекстСообщения + "
			|"+СокрЛП(Строка.Значение);
		КонецЦикла;	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
	
	Коллекция = ТаблицаПроизводств.ПолучитьЭлементы();
	Для Каждого Строка Из Коллекция Цикл
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		Элементы.ТаблицаПроизводств.Развернуть(ИдентификаторСтроки);
	КонецЦикла;
	
	Оповестить("ЗаписьПродукцииПоЦехам");
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	ДеревоПроизводств = РеквизитФормыВЗначение("ТаблицаПроизводств");
	Для Каждого СтрокаСЕ Из ДеревоПроизводств.Строки Цикл
		Для Каждого Строка Из СтрокаСЕ.Строки Цикл
			Если Строка.Флаг <> Строка.ФлагАктивности Тогда
				Запись = РегистрыСведений.ПродукцияПоЦехам.СоздатьМенеджерЗаписи();
				Запись.Склад = Строка.Склад;
				Запись.ТехнологическаяКарта = ТехнологическаяКарта;
				Запись.Продукция = ТехнологическаяКарта.Продукция;
				Запись.ФлагАктивности = Строка.Флаг;
				Запись.Записать();
				Строка.СкладВРегистре = Строка.Склад;
				Строка.ФлагАктивности = Строка.Флаг;
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	ЗначениеВРеквизитФормы(ДеревоПроизводств, "ТаблицаПроизводств");
	
КонецПроцедуры

&НаСервере
Функция ПроверкаСтатусовНоменклатуры(Склад,СписокТоваров)
	Если Склад.Владелец = Справочники.СтруктурныеЕдиницы.Центр Тогда
		Возврат Истина;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА СтатусыНоменклатуры.Статус = ЗНАЧЕНИЕ(перечисление.статусытоваров.активный)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Статус,
	|	ВложенныйЗапрос.Состав КАК Состав
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Ингредиенты.Номенклатура ЕСТЬ NULL 
	|				ТОГДА ТехнологическаяКартаСостав.Номенклатура
	|			ИНАЧЕ Ингредиенты.Номенклатура
	|		КОНЕЦ КАК Номенклатура,
	|		ТехнологическаяКартаСостав.Номенклатура КАК Состав
	|	ИЗ
	|		Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК Ингредиенты
	|			ПО ТехнологическаяКартаСостав.Номенклатура = Ингредиенты.Ссылка
	|	ГДЕ
	|		ТехнологическаяКартаСостав.Ссылка = &Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВЫБОР
	|			КОГДА Ингредиенты.Номенклатура ЕСТЬ NULL 
	|				ТОГДА ТехнологическаяКартаСостав.Номенклатура
	|			ИНАЧЕ Ингредиенты.Номенклатура
	|		КОНЕЦ,
	|		ТехнологическаяКартаСостав.Номенклатура
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТехнологическаяКарта.Продукция,
	|		ТехнологическаяКарта.Продукция
	|	ИЗ
	|		Документ.ТехнологическаяКарта КАК ТехнологическаяКарта
	|	ГДЕ
	|		ТехнологическаяКарта.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыНоменклатуры КАК СтатусыНоменклатуры
	|		ПО ВложенныйЗапрос.Номенклатура = СтатусыНоменклатуры.Номенклатура
	|			И (СтатусыНоменклатуры.СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Состав";
	
	
	Запрос.УстановитьПараметр("Ссылка", ТехнологическаяКарта);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Склад.Владелец);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	ИтоговыйСтатус = 0;
	Пока Выборка.Следующий() Цикл
		Если Выборка.Статус <> 1 Тогда
			СписокТоваров.Добавить(СокрЛП(Выборка.Состав)+" ("+СокрЛП(Выборка.Состав.Код)+")");	
		КонецЕсли;
		ИтоговыйСтатус = ИтоговыйСтатус + Выборка.Статус;
	КонецЦикла;	
	Если ИтоговыйСтатус < ТехнологическаяКарта.Состав.Количество()+1 Тогда
		Возврат Ложь;	
	Иначе
		Возврат Истина;	
	КонецЕсли;	
КонецФункции

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	СнятьФлажкиНаСервере();
	
	Коллекция = ТаблицаПроизводств.ПолучитьЭлементы();
	Для Каждого Строка Из Коллекция Цикл
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		Элементы.ТаблицаПроизводств.Развернуть(ИдентификаторСтроки);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СнятьФлажкиНаСервере()
	
	ДеревоПроизводств = РеквизитФормыВЗначение("ТаблицаПроизводств");
	Для Каждого СтрокаСЕ Из ДеревоПроизводств.Строки Цикл
		Для Каждого Строка Из СтрокаСЕ.Строки Цикл
	         Строка.Флаг = ЛОЖЬ;
		КонецЦикла;	
	КонецЦикла;	
	ЗначениеВРеквизитФормы(ДеревоПроизводств, "ТаблицаПроизводств");
	
КонецПроцедуры


