
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ПараметрыФормы = Новый Структура("АдресВхранилище",ПоместитьВХранилище(ПараметрКоманды));
	ОткрытьФорму("Обработка.ПечатьЦенниковИЭтикеток.Форма.ПечатьЦенниковИЭтикеток", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
КонецПроцедуры

&НаСервере
Функция ПоместитьВХранилище(Источник)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокНоменклатуры", Источник);
	//в запросе ВложенныйЗапрос для того, чтобы подтянуть транзакционный товар, который может находится выше уровнем нежели выделенный
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	тНоменклатура.Ссылка КАК Номенклатура,
	               |	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения
	               |ИЗ
	               |	Справочник.Номенклатура КАК тНоменклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	               |		ПО тНоменклатура.БазоваяЕдиницаИзмерения = ЕдиницыИзмерения.ЕдиницаПоКлассификатору
	               |			И тНоменклатура.Ссылка = ЕдиницыИзмерения.Владелец
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			Номенклатура.Ссылка КАК Ссылка
	               |		ИЗ
	               |			Справочник.Номенклатура КАК Номенклатура
	               |		ГДЕ
	               |			Номенклатура.Ссылка В ИЕРАРХИИ(&СписокНоменклатуры)) КАК ВложенныйЗапрос
	               |		ПО (тНоменклатура.Ссылка = ВложенныйЗапрос.Ссылка
	               |				ИЛИ тНоменклатура.Ссылка = ВложенныйЗапрос.Ссылка.Родитель
	               |					И ВложенныйЗапрос.Ссылка.Родитель.Транзакционный
	               |				ИЛИ тНоменклатура.Ссылка = ВложенныйЗапрос.Ссылка.Родитель.Родитель
	               |					И ВложенныйЗапрос.Ссылка.Родитель.Родитель.Транзакционный)
	               |ГДЕ
	               |	тНоменклатура.Транзакционный";
	Результат = Запрос.Выполнить();
	СтруктураПараметров = Новый Структура;
	Если НЕ Результат.Пустой() тогда
		СтруктураПараметров.Вставить("Состав", Результат.Выгрузить());
	КонецЕсли;
	Возврат ПоместитьВоВременноеХранилище(СтруктураПараметров);	 
КОнецФункции
