
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.ОтборПоЗонеПеремещения Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	СтруктурныеЕдиницы.Ссылка
		               |ИЗ
		               |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		               |ГДЕ
		               |	(СтруктурныеЕдиницы.ЗонаПеремещения В
		               |					(ВЫБРАТЬ
		               |						СтруктурныеЕдиницы.ЗонаПеремещения
		               |					ИЗ
		               |						Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		               |					ГДЕ
		               |						СтруктурныеЕдиницы.Ссылка В ИЕРАРХИИ (&ТекущаяСЕ)
		               |						И СтруктурныеЕдиницы.ЗонаПеремещения <> &ПустаяЗона)
		               |				И НЕ СтруктурныеЕдиницы.Ссылка В ИЕРАРХИИ (&ТекущаяСЕ)
		               |				И СтруктурныеЕдиницы.Ссылка <> &СЕЦентр
		               |				И СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы <> &ТипСтруктурнойЕдиницы
		               |			ИЛИ СтруктурныеЕдиницы.Ссылка В
		               |				(ВЫБРАТЬ
		               |					СтруктурныеЕдиницы.РЦПоУмолчанию
		               |				ИЗ
		               |					Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		               |				ГДЕ
		               |					СтруктурныеЕдиницы.Ссылка В ИЕРАРХИИ (&ТекущаяСЕ)
		               |					И СтруктурныеЕдиницы.РЦПоУмолчанию <> &ПустаяСЕ))
		               |	И ВЫБОР
		               |			КОГДА СтруктурныеЕдиницы.ДатаОткрытия = &ПустаяДата
		               |				ТОГДА ИСТИНА
		               |			ИНАЧЕ &ТекущаяДата >= ДОБАВИТЬКДАТЕ(СтруктурныеЕдиницы.ДатаОткрытия, ДЕНЬ, -СтруктурныеЕдиницы.КоличествоДнейДоОткрытия)
		               |		КОНЕЦ
		               |	И ВЫБОР
		               |			КОГДА СтруктурныеЕдиницы.ДатаЗакрытия = &ПустаяДата
		               |				ТОГДА ИСТИНА
		               |			ИНАЧЕ &ТекущаяДата <= ДОБАВИТЬКДАТЕ(СтруктурныеЕдиницы.ДатаЗакрытия, ДЕНЬ, СтруктурныеЕдиницы.КоличествоДнейДоЗакрытия)
		               |		КОНЕЦ";
		Запрос.УстановитьПараметр("ПустаяЗона",Справочники.ЗоныПеремещений.ПустаяСсылка());	
		
		Если ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница.ЭтоГруппа Тогда 
			Запрос.УстановитьПараметр("ТекущаяСЕ", Параметры.ТекущаяСЕ);
		Иначе		
			Запрос.УстановитьПараметр("ТекущаяСЕ", ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница);
		КонецЕсли;	
		
		Запрос.УстановитьПараметр("СЕЦентр",Справочники.СтруктурныеЕдиницы.Центр);	
		Запрос.УстановитьПараметр("ПустаяСЕ",Справочники.СтруктурныеЕдиницы.ПустаяСсылка());	
		Запрос.УстановитьПараметр("ТекущаяДата",ТекущаяДата());	
		Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
		Запрос.УстановитьПараметр("ТипСтруктурнойЕдиницы", Перечисления.ТипыСтруктурныхЕдиниц.Производство);
		СписокСЕ.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование    = Истина;
		ЭлементОтбора.ПравоеЗначение   = СписокСЕ;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	ВидСклада = Параметры.ВидСклада;
	Если ЗначениеЗаполнено(ВидСклада) Тогда
		Список.ПроизвольныйЗапрос = Истина;
		Список.ТекстЗапроса = "ВЫБРАТЬ
		                      |	СтруктурныеЕдиницы.Ссылка,
		                      |	СтруктурныеЕдиницы.ВерсияДанных,
		                      |	СтруктурныеЕдиницы.ПометкаУдаления,
		                      |	СтруктурныеЕдиницы.Предопределенный,
		                      |	СтруктурныеЕдиницы.Родитель,
		                      |	СтруктурныеЕдиницы.ЭтоГруппа,
		                      |	СтруктурныеЕдиницы.Код,
		                      |	СтруктурныеЕдиницы.Наименование,
		                      |	СтруктурныеЕдиницы.Наименование10,
		                      |	СтруктурныеЕдиницы.Наименование3,
		                      |	СтруктурныеЕдиницы.СкладТорговогоЗала,
		                      |	СтруктурныеЕдиницы.Формат,
		                      |	СтруктурныеЕдиницы.РЦПоУмолчанию,
		                      |	СтруктурныеЕдиницы.Валюта,
		                      |	СтруктурныеЕдиницы.ОбщаяПлощадь,
		                      |	СтруктурныеЕдиницы.Класс,
		                      |	СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы,
		                      |	СтруктурныеЕдиницы.ДатаОткрытия,
		                      |	СтруктурныеЕдиницы.ДатаЗакрытия,
		                      |	СтруктурныеЕдиницы.ТорговаяПлощадь,
		                      |	СтруктурныеЕдиницы.ЗонаПеремещения,
		                      |	СтруктурныеЕдиницы.КоличествоДнейДоЗакрытия,
		                      |	СтруктурныеЕдиницы.КоличествоДнейДоОткрытия,
		                      |	СтруктурныеЕдиницы.Страна,
		                      |	СтруктурныеЕдиницы.РежимРаботы
		                      |ИЗ
		                      |	Справочник.Склады КАК Склады
		                      |		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		                      |		ПО (СтруктурныеЕдиницы.Ссылка = Склады.Владелец)
		                      |ГДЕ
		                      |	Склады.ВидСклада = &ВидСклада
		                      |	И СтруктурныеЕдиницы.ТипСтруктурнойЕдиницы <> &ТипСтруктурнойЕдиницы";
		Список.Параметры.УстановитьЗначениеПараметра("ВидСклада",ВидСклада);										  
		Список.Параметры.УстановитьЗначениеПараметра("ТипСтруктурнойЕдиницы",Перечисления.ТипыСтруктурныхЕдиниц.Производство);										  
	КонецЕсли;									  
КонецПроцедуры
