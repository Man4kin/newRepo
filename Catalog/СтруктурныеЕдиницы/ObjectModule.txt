

Процедура ПриЗаписи(Отказ)
	Если ПараметрыСеанса.ЭтоЦентр Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ОбменРБД.Ссылка КАК Узел
			|ИЗ
			|	ПланОбмена.ОбменРБД КАК ОбменРБД
			|ГДЕ
			|	ОбменРБД.Ссылка <> &Центр";

		Запрос.УстановитьПараметр("Центр", ПланыОбмена.ОбменРБД.ЭтотУзел());

		Результат = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = Результат.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ПланыОбмена.ЗарегистрироватьИзменения(ВыборкаДетальныеЗаписи.Узел,ЭтотОбъект);	
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Склад Тогда
		ПроверяемыеРеквизиты.Добавить("ОптоваяЛокация");
	КонецЕсли;	
КонецПроцедуры


Процедура ПередЗаписью(Отказ)
	Если ПараметрыСеанса.ЭтоЦентр Тогда
		Если Ссылка = Справочники.СтруктурныеЕдиницы.Центр И НЕ Родитель = Ссылка.Родитель Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Запрещено изменение родителя структурной единицы Центр!'"),Ссылка);
			Отказ = Истина;
		КонецЕсли;	
		//Если ЗонаПеремещения <> Родитель.ЗонаПеремещения Тогда
		//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		//	НСтр("ru = 'Запрещено изменение! Разные зоны перемещения!'"),Ссылка);
		//	Отказ = Истина;
		//КонецЕсли;	
		Если Ссылка.Родитель <> Родитель И ЗначениеЗаполнено(Родитель) Тогда
			Если СтруктурныеЕдиницы.ЕстьЭлементыВГруппе(Родитель) Тогда
				СтруктурныеЕдиницы.ПеренестиПараметры(Родитель,Ссылка);
			Иначе
				СтруктурныеЕдиницы.ПеренестиПараметры(Ссылка,Родитель);
			КонецЕсли;
		КонецЕсли;	
		//+Зернятко А.В.@13.03.2013 17:38:04 - Изменение стратегии нумерации
		Если Не ЭтоГруппа Тогда
			
			Если Не ЗначениеЗаполнено(Префикс) ИЛИ СтрДлина(СокрЛП(Префикс))<2 ТОгда
				Если Предопределенный И Ссылка = Справочники.СтруктурныеЕдиницы.Центр Тогда //Центральный офис имеет префикс "00" 
					Префикс = "00";
				Иначе
					Отказ = НЕ УстановитьПрефиксСЕ(Код);
				КонецЕсли
			ИначеЕсли Код<=1688 Тогда
				Запрос = Новый Запрос();
				Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				|	СтруктурныеЕдиницы.Ссылка
				|ИЗ
				|	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
				|ГДЕ
				|	СтруктурныеЕдиницы.Ссылка <> &Ссылка
				|	И СтруктурныеЕдиницы.Префикс = &Префикс";
				Запрос.УстановитьПараметр("Ссылка", Ссылка);					   
				Запрос.УстановитьПараметр("Префикс", Префикс);					   
				ВыборкаСЕПоПрефиксу = Запрос.Выполнить().Выбрать();
				
				Если ВыборкаСЕПоПрефиксу.Следующий() Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Не удалось записать структурную единицу "+Ссылка+". Указанный префикс не уникален, уже существует структурная единица с префиксом "+Префикс+" ("+ВыборкаСЕПоПрефиксу.Ссылка+").'"),Ссылка,,"Префикс");
				КонецЕсли;	
			КОнецЕсли;
		КонецЕсли;
		///Зернятко А.В.@ - конец блока 
	КонецЕсли;
КонецПроцедуры

//+Зернятко А.В.@13.03.2013 17:38:04 - Изменение стратегии нумерации
Функция УстановитьПрефиксСЕ(Знач ЧисловойКод) Экспорт
	ЧисловойКод = Число(ЧисловойКод); // возможна ошибка преобразования типов
	Если Код>1688 Тогда //максимально обсуживаемый код
		Префикс = "@@"; //Префикс для всех "тестовых" и т.п. структурные единиц с большими кодами
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Для структурной единицы "+Ссылка+" указан код превышающий предусмотренную систему префиксации. Присвоен временный префикс `@@`.'"),Ссылка);
		Возврат Истина;
	КонецЕсли;
	Результат = "";
	
	СтаршийРазряд = Новый Массив;
	СтаршийРазряд.Добавить("0123456789"); 
	СтаршийРазряд.Добавить("АБВГДЕЁЖИЙКЛМНПРСТУФХЦЧШЩЭЮЯ"); // - Ь,Ъ,Ы,О,З 
	СтаршийРазряд.Добавить("DFGIJLNQRSUVWYZ"); // - A,B,C,E,H,K,M,O,P,T,X
	
	МладшийРазряд = Новый Массив;
	МладшийРазряд.Добавить("0123456789");
	МладшийРазряд.Добавить("123456789АБВГДЕЁЖИЙКЛМНОПРСТУФХЦЧШЩЭЮЯ");  // - 0, Ь,Ъ,Ы,З
	МладшийРазряд.Добавить("123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ");    // - 0

	Для Индекс = 1 По 3 Цикл
	
		Размерность = СтрДлина(СтаршийРазряд[Индекс-1]) * СтрДлина(МладшийРазряд[Индекс-1]);
		
		Если ЧисловойКод < Размерность Тогда
			
			Основание = СтрДлина(МладшийРазряд[Индекс-1]); 
			
			ИндексПервый = Цел(ЧисловойКод / Основание);
			ИндексВторой = ЧисловойКод % Основание;
			
			ПервыйСимвол = Сред(СтаршийРазряд[Индекс-1], ИндексПервый+1, 1);
			ВторойСимвол = Сред(МладшийРазряд[Индекс-1], ИндексВторой+1, 1);
			
			Результат = ПервыйСимвол + ВторойСимвол;
			
			Прервать;
		Иначе
			ЧисловойКод = ЧисловойКод - Размерность;
		КонецЕсли;
	КонецЦикла;
	
	СЕПоПрефиксу = Справочники.СтруктурныеЕдиницы.НайтиПоРеквизиту("Префикс", Результат);
	Если не СЕПоПрефиксу = Справочники.СтруктурныеЕдиницы.ПустаяСсылка() и не СЕПоПрефиксу = Ссылка Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Для структурной единицы "+Ссылка+" не удалось автоматически присвоить уникальный префикс! Уже сущесвтует структурная единица с префиксом "+Результат+" ("+СЕПоПрефиксу+").'"),Ссылка);
		Префикс = "";
		Возврат Ложь;
	Иначе
		Префикс = Результат;
	КонецЕсли;	
	Возврат Истина;
КонецФункции
///Зернятко А.В.@ - конец блока 
