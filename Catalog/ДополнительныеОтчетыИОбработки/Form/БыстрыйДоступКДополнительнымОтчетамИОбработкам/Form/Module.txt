////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Быстрый доступ к команде ""%1""'"),
		Параметры.ПредставлениеКоманды);
		
	//popn+
	//Для Каждого ПользовательСтрока Из Параметры.ПользователиСБыстрымДоступом Цикл
	//	НоваяСтрока = ПользователиКороткогоСписка.Добавить();
	//	НоваяСтрока.Пользователь = ПользовательСтрока.Значение;
	//КонецЦикла;
	//
	Для Каждого ПользовательСтрока Из Параметры.ПользователиСБыстрымДоступом Цикл
		НоваяСтрока = ПользователиКороткогоСписка.Добавить();
		Если ТипЗнч(ПользовательСтрока.Значение) = Тип("СправочникСсылка.Пользователи") Тогда
			НоваяСтрока.Картинка = 2;
		ИначеЕсли ТипЗнч(ПользовательСтрока.Значение) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
			НоваяСтрока.Картинка = 0;
		КонецЕсли;
		НоваяСтрока.Пользователь = ПользовательСтрока.Значение;
	КонецЦикла;
	
	ЗаполнитьТаблицуПользователейСКороткимСписком();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ВсеПользователи

&НаКлиенте
Процедура ВсеПользователиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
		Возврат;
	КонецЕсли;
	
	ПеренестиПользователей(ВсеПользователи, ПользователиКороткогоСписка, ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеПользователиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ПользователиКороткогоСписка

&НаКлиенте
Процедура ПользователиКороткогоСпискаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
		Возврат;
	КонецЕсли;
	
	ПеренестиПользователей(ПользователиКороткогоСписка, ВсеПользователи, ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПользователиКороткогоСпискаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УбратьДоступККомандеУВсехПользователей(Команда)
	
	МассивПеретаскиваемыхЭлементов = Новый Массив;
	
	Для Каждого ОписаниеСтроки Из ПользователиКороткогоСписка Цикл
		МассивПеретаскиваемыхЭлементов.Добавить(ОписаниеСтроки);
	КонецЦикла;
	
	ПеренестиПользователей(ВсеПользователи, ПользователиКороткогоСписка, МассивПеретаскиваемыхЭлементов);
	СортироватьИСвернуть();
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьДоступККомандеУВыделенныхПользователей(Команда)
	
	МассивПеретаскиваемыхЭлементов = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.ПользователиКороткогоСписка.ВыделенныеСтроки Цикл
		МассивПеретаскиваемыхЭлементов.Добавить(Элементы.ПользователиКороткогоСписка.ДанныеСтроки(ВыделеннаяСтрока));
	КонецЦикла;
	
	ПеренестиПользователей(ВсеПользователи, ПользователиКороткогоСписка, МассивПеретаскиваемыхЭлементов);
	СортироватьИСвернуть();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступДляВсехПользователей(Команда)
	
	МассивПеретаскиваемыхЭлементов = Новый Массив;
	
	Для Каждого ОписаниеСтроки Из ВсеПользователи Цикл
		МассивПеретаскиваемыхЭлементов.Добавить(ОписаниеСтроки);
	КонецЦикла;
	
	ПеренестиПользователей(ПользователиКороткогоСписка, ВсеПользователи, МассивПеретаскиваемыхЭлементов);
	СортироватьИСвернуть();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомандуДляВыделенныхПользователей(Команда)
	
	МассивПеретаскиваемыхЭлементов = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.ВсеПользователи.ВыделенныеСтроки Цикл
		МассивПеретаскиваемыхЭлементов.Добавить(Элементы.ВсеПользователи.ДанныеСтроки(ВыделеннаяСтрока));
	КонецЦикла;
	
	ПеренестиПользователей(ПользователиКороткогоСписка, ВсеПользователи, МассивПеретаскиваемыхЭлементов);
	СортироватьИСвернуть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	РезультатВыбора = Новый СписокЗначений;
	
	Для Каждого ЭлементКоллекции Из ПользователиКороткогоСписка Цикл
		РезультатВыбора.Добавить(ЭлементКоллекции.Пользователь);
	КонецЦикла;
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьТаблицуПользователейСКороткимСпискомНов()
	
	МассивПользователей = ПользователиКороткогоСписка.Выгрузить().ВыгрузитьКолонку("Пользователь");
	
	ТекстЗапроса = "ВЫБРАТЬ
					|	Истина	КАК Используется,
					|	Ссылка	КАК Пользователь
					|ИЗ
					|	Справочник.Пользователи
					|ГДЕ
					|	НЕ ПометкаУдаления
					|	И НЕ Ссылка В (&МассивПользователей)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.Параметры.Вставить("МассивПользователей", МассивПользователей);
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "ВсеПользователи");
	
	ВсеПользователи.Сортировать("Пользователь Возр");
	ПользователиКороткогоСписка.Сортировать("Пользователь Возр");
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьТаблицуПользователейСКороткимСписком()
	
	МассивПользователей = ПользователиКороткогоСписка.Выгрузить().ВыгрузитьКолонку("Пользователь");
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ВЗ.Картинка КАК Картинка,
	               |	ИСТИНА КАК Используется,
	               |	ВЗ.Пользователь КАК Пользователь
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		2 КАК Картинка,
	               |		Пользователи.Ссылка КАК Пользователь
	               |	ИЗ
	               |		Справочник.Пользователи КАК Пользователи
	               |	ГДЕ
	               |		НЕ Пользователи.ПометкаУдаления
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		0,
	               |		ГруппыПользователей.Ссылка
	               |	ИЗ
	               |		Справочник.ГруппыПользователей КАК ГруппыПользователей
	               |	ГДЕ
	               |		НЕ ГруппыПользователей.ПометкаУдаления
				   |		И НЕ ГруппыПользователей.Предопределенный) КАК ВЗ
	               |ГДЕ
	               |	НЕ ВЗ.Пользователь В (&МассивПользователей)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ВЗ.Картинка";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.Параметры.Вставить("МассивПользователей", МассивПользователей);
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "ВсеПользователи");
	
	ВсеПользователи.Сортировать("Картинка Возр, Пользователь Возр");
	ПользователиКороткогоСписка.Сортировать("Картинка Возр, Пользователь Возр");
	
КонецПроцедуры


&НаКлиенте
Процедура ПеренестиПользователей(Приемник, Источник, МассивПеретаскиваемыхЭлементов)
	
	Для Каждого ПеретаскиваемыйЭлемент Из МассивПеретаскиваемыхЭлементов Цикл
		Если ТипЗнч(ПеретаскиваемыйЭлемент.Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
			НовыйПользователь = Приемник.Добавить();
			НовыйПользователь.Картинка = 2;
			НовыйПользователь.Пользователь = ПеретаскиваемыйЭлемент.Пользователь;
			Источник.Удалить(ПеретаскиваемыйЭлемент);
		ИначеЕсли ТипЗнч(ПеретаскиваемыйЭлемент.Пользователь) = Тип("СправочникСсылка.ГруппыПользователей") Тогда
			НовыйПользователь = Приемник.Добавить();
			НовыйПользователь.Картинка = 0;
			НовыйПользователь.Пользователь = ПеретаскиваемыйЭлемент.Пользователь;
			Источник.Удалить(ПеретаскиваемыйЭлемент);
		КонецЕсли;
 	КонецЦикла;
	
	Приемник.Сортировать("Пользователь Возр");
	
КонецПроцедуры

&НаСервере
Процедура СортироватьИСвернуть()
	
	ВремТаблица1 = ВсеПользователи.Выгрузить();
	ВремТаблица1.Свернуть("Пользователь,Картинка");
	ВремТаблица1.Сортировать("Картинка Возр, Пользователь Возр");
	ЗначениеВРеквизитФормы(ВремТаблица1,"ВсеПользователи");

	ВремТаблица2 = ПользователиКороткогоСписка.Выгрузить();
	ВремТаблица2.Свернуть("Пользователь,Картинка");
	ВремТаблица2.Сортировать("Картинка Возр, Пользователь Возр");
	ЗначениеВРеквизитФормы(ВремТаблица2,"ПользователиКороткогоСписка");
	
КонецПроцедуры

