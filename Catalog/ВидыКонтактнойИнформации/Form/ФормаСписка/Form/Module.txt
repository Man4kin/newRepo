&НаКлиенте
Процедура ПереместитьВверхВыполнить()
	Переместить(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВнизВыполнить()
	Переместить(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Переместить(Вверх)
	
	Ссылка = Элементы.Список.ТекущаяСтрока;
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ВыполнитьПеремещение(Ссылка, Вверх);
	ЭтаФорма.Элементы.Список.Обновить();
	Если Результат <> "" Тогда
		Предупреждение(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПеремещение(Ссылка, Вверх)
	
	Если Ссылка.ЭтоГруппа Тогда
		Возврат "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб2.Ссылка КАК Ссылка2
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК Таб1
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыКонтактнойИнформации КАК Таб2
	|		ПО Таб1.Родитель = Таб2.Родитель
	|			И Таб1.Порядок < Таб2.Порядок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыКонтактнойИнформации КАК Таб3
	|		ПО Таб1.Родитель = Таб3.Родитель
	|			И Таб1.Порядок < Таб3.Порядок
	|			И (Таб3.Порядок < Таб2.Порядок)
	|ГДЕ
	|	Таб1.Ссылка = &Ссылка
	|	И Таб3.Ссылка ЕСТЬ NULL";
	
	Если Вверх Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "<", ">");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат "";
	КонецЕсли;
	
	// Выполним перемещение
	Попытка
		НачатьТранзакцию();
		
		Объект1 = Ссылка.ПолучитьОбъект();
		Объект2 = Выборка.Ссылка2.ПолучитьОбъект();
		Порядок1 = Объект1.Порядок;
		Порядок2 = Объект2.Порядок;
		Объект1.Порядок = Порядок2;
		Объект2.Порядок = Порядок1;
		Объект1.Записать();
		Объект2.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		
		Результат = ОписаниеОшибки();
		ОтменитьТранзакцию();
		Возврат Результат;
		
	КонецПопытки;
	
	Возврат "";
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияСервер.ЗапретМодификацииОбъектов(ЭтаФорма);
КонецПроцедуры


