
&НаКлиенте
Процедура СтруктурныеЕдиницыПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивСкладов = Новый Массив;
	Если ЗначениеЗаполнено(ВидСклада) Тогда
		МассивСкладов.Добавить(ВидСклада);
		МассивСкладов.Добавить(ПредопределенноеЗначение("Перечисление.ВидыСкладов.Розничный"));
	КонецЕсли;
	
	Список.Отбор.Элементы.Очистить();
	Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.Использование = Истина;
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
	Отбор.ПравоеЗначение = Элемент.ТекущаяСтрока;
	
	Если ЗначениеЗаполнено(МассивСкладов) Тогда
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.Использование = Истина;
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидСклада");
		Отбор.ПравоеЗначение = МассивСкладов;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВидСклада = Параметры.ВидСклада;
	
	МассивСкладов = Новый Массив;
	Если ЗначениеЗаполнено(ВидСклада) Тогда
		МассивСкладов.Добавить(ВидСклада);
		Если ИспользоватьРозничныеСкладыВПроизводстве() Тогда
			МассивСкладов.Добавить(Перечисления.ВидыСкладов.Розничный);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.ТолькоТекущаяСЕ Тогда
		Если ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница.ЭтоГруппа Тогда
			СтруктурныеЕдиницы.ПроизвольныйЗапрос = Истина;
			СтруктурныеЕдиницы.ТекстЗапроса = "ВЫБРАТЬ
			                                  |	СтруктурныеЕдиницы.Ссылка,
			                                  |	СтруктурныеЕдиницы.Наименование,
			                                  |	СтруктурныеЕдиницы.Код
			                                  |ИЗ
			                                  |	Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
			                                  |ГДЕ
			                                  |	СтруктурныеЕдиницы.Ссылка В ИЕРАРХИИ(&Ссылка)
			                                  |
			                                  |СГРУППИРОВАТЬ ПО
			                                  |	СтруктурныеЕдиницы.Ссылка,
			                                  |	СтруктурныеЕдиницы.Наименование,
			                                  |	СтруктурныеЕдиницы.Код";
			
			СтруктурныеЕдиницы.Параметры.УстановитьЗначениеПараметра("Ссылка",ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница);//V
		Иначе
			Список.Отбор.Элементы.Очистить();
			Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Отбор.Использование = Истина;
			Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
			Отбор.ПравоеЗначение = ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница; //V
			Отбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			
			Если ЗначениеЗаполнено(МассивСкладов) Тогда
				Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				Отбор.Использование = Истина;
				Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
				Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидСклада");
				Отбор.ПравоеЗначение = МассивСкладов;
				Отбор.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			КонецЕсли;
			Элементы.СтруктурныеЕдиницы.Видимость = Ложь;
		КонецЕсли;	
		Возврат;
	КонецЕсли;									  
	
	Если ЗначениеЗаполнено(МассивСкладов) Тогда
		СтруктурныеЕдиницы.ПроизвольныйЗапрос = Истина;
		СтруктурныеЕдиницы.ТекстЗапроса = "ВЫБРАТЬ
		                                  |	СтруктурныеЕдиницы.Ссылка,
		                                  |	СтруктурныеЕдиницы.Наименование,
		                                  |	СтруктурныеЕдиницы.Код
		                                  |ИЗ
		                                  |	Справочник.Склады КАК Склады
		                                  |		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.СтруктурныеЕдиницы КАК СтруктурныеЕдиницы
		                                  |		ПО (СтруктурныеЕдиницы.Ссылка = Склады.Владелец)
		                                  |ГДЕ
		                                  |	Склады.ВидСклада В (&ВидСклада)
		                                  |
		                                  |СГРУППИРОВАТЬ ПО
		                                  |	СтруктурныеЕдиницы.Ссылка,
		                                  |	СтруктурныеЕдиницы.Наименование,
		                                  |	СтруктурныеЕдиницы.Код";
		СтруктурныеЕдиницы.Параметры.УстановитьЗначениеПараметра("ВидСклада",МассивСкладов);										  
	КонецЕсли;									  
КонецПроцедуры

 &НаСервереБезКонтекста
Функция ИспользоватьРозничныеСкладыВПроизводстве()
	
	Возврат (НЕ ПараметрыСеанса.ЭтоЦентр) И УправлениеПараметрамиСЕСервер.ПолучитьЗначениеСвойства(ПланыВидовХарактеристик.ПараметрыСтруктурныхЕдиниц.ИспользоватьРозничныеСкладыВПроизводстве, ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница);
	
КонецФункции


