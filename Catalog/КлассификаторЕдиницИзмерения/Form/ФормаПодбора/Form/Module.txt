
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Макет = Справочники.КлассификаторЕдиницИзмерения.ПолучитьМакет("КлассификаторЕдиницИзмерения");
	//Макет.Параметры.Расшифровка = Истина;
	ПолеДокумента.Вывести(Макет);
	ПолеДокумента.ФиксацияСверху = ПолеДокумента.Области.ОбластьРасшифровки.Верх - 1;
КонецПроцедуры

&НаКлиенте
Процедура ПолеДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТабличныйДокумент = ПолеДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодЧисловой         = ТабличныйДокумент.Области.КодЧисловой;
	ОбластьНаименованиеКраткое = ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное  = ТабличныйДокумент.Области.НаименованиеПолное;

	КодЧисловой         = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.        Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.        Право).Текст;
	НаименованиеКраткое = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
	НаименованиеПолное  = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное. Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст;

	Ссылка = НайтиСсылкуПоКоду(КодЧисловой);
	Если НЕ Ссылка.Пустая() Тогда
		Вопрос = "В справочнике ""Классификатор единиц измерения"" уже существует элемент с кодом """ + КодЧисловой + """! Открыть существующий?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, );

		Если Ответ = КодВозвратаДиалога.Да Тогда
			СтруктураПараметров = Новый Структура("Ключ", Ссылка);
			ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаОбъекта",СтруктураПараметров,ВладелецФормы);
			//Ссылка.ПолучитьФорму( ,ВладелецФормы, ).Открыть();
			Возврат;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ФормаНовогоЭлемента = ПолучитьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаОбъекта", ,ВладелецФормы);
	ФормаНовогоЭлемента.Объект.Код                = КодЧисловой;
	ФормаНовогоЭлемента.Объект.Наименование       = СтрПолучитьСтроку(НаименованиеКраткое, 1);
	ФормаНовогоЭлемента.Объект.ПолноеНаименование = СтрПолучитьСтроку(НаименованиеПолное, 1);

	ФормаНовогоЭлемента.Открыть();
КонецПроцедуры


&НаСервере
Функция НайтиСсылкуПоКоду(Код)
	Возврат Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(Код);
КонецФункции
