
//{ СИТЕК БрыляковЕЮ 2015-04-16  
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Izh_Спецификация") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Izh_Спецификация",
			"Спецификация",
			ПечатьСпецификации(МассивОбъектов, ОбъектыПечати)
		);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьСпецификации(МассивОбъектов, ОбъектыПечати)
	
	СКД = Отчеты.Izh_Спецификация.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД));
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	
	Настройки = КомпоновщикНастроек.ФиксированныеНастройки;
	
	Контрагенты = Новый СписокЗначений();
	Контрагенты.Добавить(МассивОбъектов);
	Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Контрагент", Контрагенты);
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	Макет = КомпоновщикМакета.Выполнить(СКД, Настройки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновки.Инициализировать(Макет,,, Истина);
	
	ТабДокумент = Новый ТабличныйДокумент();
	тд = ТабДокумент;
	тд.ПолеСверху = 0;
	тд.ПолеСлева = 0;
	тд.ПолеСнизу = 0;
	тд.ПолеСправа = 0;
	тд.АвтоМасштаб = Истина;
	тд.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Контрагенты_Спецификация";
	тд.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ТабДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат ТабДокумент;
	
КонецФункции
// СИТЕК БрыляковЕЮ 2015-04-16 }