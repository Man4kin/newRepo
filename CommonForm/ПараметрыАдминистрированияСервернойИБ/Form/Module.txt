
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ВызватьИсключение НСтр("ru = 'Настройка параметров серверной ИБ доступна только в клиент-серверном режиме работы.'");
		Возврат;
	КонецЕсли;
		
	Элементы.ГруппаАдминистрирование.Доступность = ?(ПользователиИнформационнойБазы.ПолучитьПользователей().Количество() > 0, Истина, Ложь);
	
	ПараметрыАдминистрирования = СоединенияИБ.ПолучитьПараметрыАдминистрированияИБ();
	
	АдминистраторИБ				= Справочники.Пользователи.НайтиПоНаименованию(ПараметрыАдминистрирования.ИмяАдминистратораИБ);
	ПарольАдминистратораИБ		= ПараметрыАдминистрирования.ПарольАдминистратораИБ;
	ИмяАдминистратораКластера	= ПараметрыАдминистрирования.ИмяАдминистратораКластера;
	ПарольАдминистратораКластера= ПараметрыАдминистрирования.ПарольАдминистратораКластера;
	ПортАгентаСервера			= ПараметрыАдминистрирования.ПортАгентаСервера;
	ПортКластераСерверов		= ПараметрыАдминистрирования.ПортКластераСерверов;
	
	НестандартныеПорты			= ПортКластераСерверов <> 0 ИЛИ ПортАгентаСервера <> 0;
	КластерТребуетАвторизации	= НЕ ПустаяСтрока(ИмяАдминистратораКластера);
		
	Элементы.ГруппаПорты.Доступность = НестандартныеПорты;
	Элементы.ГруппаАвторизацияКластера.Доступность = КластерТребуетАвторизации;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеПолей()
	
	Элементы.ГруппаПорты.Доступность = НестандартныеПорты;
	Элементы.ГруппаАвторизацияКластера.Доступность = КластерТребуетАвторизации;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ПараметрыПодключения = СохранитьПараметрыПодключения(АдминистраторИБ, ПарольАдминистратораИБ, КластерТребуетАвторизации,
								ИмяАдминистратораКластера, ПарольАдминистратораКластера, НестандартныеПорты,
								ПортАгентаСервера, ПортКластераСерверов);
	
	Оповестить("ОбновленыПараметрыПодключенияКИнформационнойБазе", ПараметрыПодключения);
	
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СохранитьПараметрыПодключения(АдминистраторИБ, ПарольАдминистратораИБ, КластерТребуетАвторизации,
										ИмяАдминистратораКластера, ПарольАдминистратораКластера, НестандартныеПорты,
										ПортАгентаСервера, ПортКластераСерверов)
	
	ПараметрыАдминистрирования = СоединенияИБ.ПолучитьПараметрыАдминистрированияИБ();
	
	ИдентификаторПользователяИБ = Пользователи.ПолучитьУникальныйИдентификаторПользователяИБ(АдминистраторИБ);

	
	ПараметрыАдминистрирования.ИмяАдминистратораИБ = 
			ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователяИБ).Имя;
			
	ПараметрыАдминистрирования.ПарольАдминистратораИБ = ПарольАдминистратораИБ;
	
	Если КластерТребуетАвторизации Тогда
		ПараметрыАдминистрирования.ИмяАдминистратораКластера = ИмяАдминистратораКластера;
		ПараметрыАдминистрирования.ПарольАдминистратораКластера = ПарольАдминистратораКластера;
	Иначе	
		ПараметрыАдминистрирования.ИмяАдминистратораКластера = "";
		ПараметрыАдминистрирования.ПарольАдминистратораКластера = "";
	КонецЕсли;
	
	Если НестандартныеПорты Тогда
		ПараметрыАдминистрирования.ПортКластераСерверов = ПортКластераСерверов;
		ПараметрыАдминистрирования.ПортАгентаСервера = ПортАгентаСервера;
	Иначе	
		ПараметрыАдминистрирования.ПортКластераСерверов = 0;
		ПараметрыАдминистрирования.ПортАгентаСервера = 0;
	КонецЕсли;	
	
	Константы.ПараметрыАдминистрированияИБ.Установить(Новый ХранилищеЗначения(ПараметрыАдминистрирования));
	
	СоединенияИБ.ЗаписатьПараметрыАдминистрированияИБ(ПараметрыАдминистрирования);
	
	Возврат ПараметрыАдминистрирования;
	
КонецФункции

&НаКлиенте
Процедура КластерТребуетАвторизацииПриИзменении(Элемент)
	
	УстановитьСостояниеПолей()
	
КонецПроцедуры

&НаКлиенте
Процедура НестандартныеПортыПриИзменении(Элемент)
	
	УстановитьСостояниеПолей();
	
КонецПроцедуры
