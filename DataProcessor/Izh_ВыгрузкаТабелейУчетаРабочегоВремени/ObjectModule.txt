Функция ПолучитьМассивПриставок() Экспорт
	
	МассивПриставок = Новый Массив(4);
	МассивПриставок[0] = "Первый";
	МассивПриставок[1] = "Второй";
	МассивПриставок[2] = "Третий";
	МассивПриставок[3] = "Четвертый";
	
	Возврат МассивПриставок;
	
КонецФункции

Функция ПолучитьТабелиЗаПериод() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	|	Izh_ОтработанноеВремяСотрудников.Регистратор КАК Документ
	|ИЗ
	|	РегистрСведений.Izh_ОтработанноеВремяСотрудников КАК Izh_ОтработанноеВремяСотрудников
	|ГДЕ
	|	Izh_ОтработанноеВремяСотрудников.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Izh_ОтработанноеВремяСотрудников.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|
	|СГРУППИРОВАТЬ ПО
	|	Izh_ОтработанноеВремяСотрудников.Регистратор";	
	Запрос.УстановитьПараметр("НачалоПериода"		, НачалоПериода);// Дата
	Запрос.УстановитьПараметр("КонецПериода"		, КонецПериода);// Дата
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница"	, СтруктурнаяЕдиница);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Документ");
	
КонецФункции

Функция ПолучитьТекстВыгрузки(МассивДокументовТабель, ТекстДокумента = "") Экспорт
			
	МассивПриставок = ПолучитьМассивПриставок();	
	ОтборПоПериоду  = ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода);	
	
	Для Каждого ДокументТабель Из МассивДокументовТабель Цикл
		ТабельСсылка = ДокументТабель;
		
		Для Каждого СтрокаТЧ Из ТабельСсылка.ОтработанноеВремя Цикл			
			Месяц = НачалоМесяца(ТабельСсылка.ДатаНачалаПериода);
			
			Для ИндДня = 0 По 30 Цикл				
																
				ДатаДня = НачалоДня(Месяц) + (ИндДня * (86400));
				ДатаДняЧислом = ИндДня + 1;
												
				Если ДатаДня > ТабельСсылка.ДатаОкончанияПериода 
					ИЛИ ДатаДня < ТабельСсылка.ДатаНачалаПериода Тогда
					
					Продолжить;				
				КонецЕсли;
				
				//Если текст выгрузки формируется программено, то дополнительная проверка
				Если ОтборПоПериоду И (ДатаДня < НачалоПериода ИЛИ ДатаДня > КонецПериода) Тогда
					Продолжить;
				КонецЕсли;
				
				ЧасовЯвка	= 0;
				ЧасовНочные	= 0;
				
				Для Каждого Приставка Из МассивПриставок Цикл	
					ТекВидВремени	= СтрокаТЧ[Приставка + "ВидВремени" + СокрЛП(ДатаДняЧислом)];
					ТекЧасов		= СтрокаТЧ[Приставка + "Часов" + СокрЛП(ДатаДняЧислом)];
					
					Если НЕ ЗначениеЗаполнено(ТекВидВремени) Тогда						
						Продолжить;
					КонецЕсли;
					
					Если ТекВидВремени.РабочееВремя И НЕ ТекВидВремени.НочныеЧасы Тогда						
						ЧасовЯвка = ЧасовЯвка + ТекЧасов;
					КонецЕсли;
					
					Если ТекВидВремени.НочныеЧасы Тогда						
						ЧасовНочные = ЧасовНочные + ТекЧасов;
					КонецЕсли;
				КонецЦикла;
				
				Если ЧасовЯвка + ЧасовНочные = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ДатаСтрокой = Формат(ДатаДня, "ДФ=yyyyMMdd");
				КодВыгрузкиСотрудника = СокрЛП(СтрокаТЧ.КодСотрудника);
				
				ОтработаноЧасов = ЧасовЯвка;
				ОтработаноЧасовСтрокой = Формат(ОтработаноЧасов, "ЧЦ=3; ЧДЦ=1; ЧРД=.; ЧН=.0");				
				
				ОтработаноНочных = ЧасовНочные;
				ОтработаноНочныхСтрокой = Формат(ОтработаноНочных, "ЧЦ=3; ЧДЦ=1; ЧРД=.; ЧН=.0");				
				
				ДолжностьСтрокой	= "";
				Должность			= СтрокаТЧ["ДолжностьДень" + СокрЛП(ДатаДняЧислом)];
				ВидЗанятости		= СтрокаТЧ["ВидЗанятостиДень" + СокрЛП(ДатаДняЧислом)];
				
				Если ЗначениеЗаполнено(ВидЗанятости) Тогда					
					Если ВидЗанятости = Перечисления.Izh_ВидыЗанятостиНаТекущемМестеРаботы.БольничныйОтпуск Тогда
						ДолжностьСтрокой = "БО";
					КонецЕсли;
					
					Если ВидЗанятости = Перечисления.Izh_ВидыЗанятостиНаТекущемМестеРаботы.ИсполняющийОбязанности Тогда
						ДолжностьСтрокой = "ИО";
					КонецЕсли;					
				КонецЕсли;				
				
				Если ЗначениеЗаполнено(Должность) Тогда
					Если ВидЗанятости = Перечисления.Izh_ВидыЗанятостиНаТекущемМестеРаботы.ИсполняющийОбязанности Тогда
						ДолжностьСтрокой = ДолжностьСтрокой + " " + СокрЛП(Должность.НаименованиеСинхронизации);
					КонецЕсли;					
				КонецЕсли;				
				
				КодМагазина = СтрокаТЧ.КодПодразделения;
				
				СтрокаВставки = ДатаСтрокой + ","
								+ КодВыгрузкиСотрудника + ","
								+ ОтработаноЧасовСтрокой + ","
								+ ОтработаноНочныхСтрокой + ","
								+ ДолжностьСтрокой + ","
								+ КодМагазина;
								
				ТекстДокумента = ТекстДокумента + СтрокаВставки + Символы.ПС;				
			КонецЦикла;						
		КонецЦикла;		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
