
Процедура ЗаполнитьПродукциюПриготовление() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Регистратор КАК КалькуляционнаяКарта,
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Продукция,
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Себестоимость,
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.СебестоимостьНДС
	|ПОМЕСТИТЬ АктивныеКалькуляционныеКарты
	|ИЗ
	|	РегистрСведений.ПлановаяСебестоимостьПродукции.СрезПоследних(&Момент, ) КАК ПлановаяСебестоимостьПродукцииСрезПоследних
	|ГДЕ
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Регистратор.Фирма = &Фирма
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Регистратор,
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Продукция,
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Себестоимость,
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.СебестоимостьНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалькуляционнаяКартаСостав.Ссылка,
	|	КалькуляционнаяКартаСостав.НомерСтроки,
	|	КалькуляционнаяКартаСостав.Номенклатура,
	|	КалькуляционнаяКартаСостав.Цена
	|ПОМЕСТИТЬ СтрокиАктивныхКалькуляционныхКарт
	|ИЗ
	|	Документ.КалькуляционнаяКарта.Состав КАК КалькуляционнаяКартаСостав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктивныеКалькуляционныеКарты КАК АктивныеКалькуляционныеКарты
	|		ПО КалькуляционнаяКартаСостав.Ссылка = АктивныеКалькуляционныеКарты.КалькуляционнаяКарта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТехнологическаяКарта.Ссылка,
	|	ТехнологическаяКарта.Продукция
	|ПОМЕСТИТЬ ТехнологическиеКартыПриготовление
	|ИЗ
	|	Документ.ТехнологическаяКарта КАК ТехнологическаяКарта
	|ГДЕ
	|	ТехнологическаяКарта.Проведен
	|	И ТехнологическаяКарта.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Приготовление)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктивныеКалькуляционныеКарты.КалькуляционнаяКарта,
	|	ТехнологическиеКартыПриготовление.Ссылка КАК ТехнологическаяКарта,
	|	ТехнологическиеКартыПриготовление.Продукция КАК Продукция
	|ПОМЕСТИТЬ АктивныеТехнологическиеКарты
	|ИЗ
	|	АктивныеКалькуляционныеКарты КАК АктивныеКалькуляционныеКарты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТехнологическиеКартыПриготовление КАК ТехнологическиеКартыПриготовление
	|		ПО АктивныеКалькуляционныеКарты.КалькуляционнаяКарта.ДокументОснование = ТехнологическиеКартыПриготовление.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Ингредиент,
	|	ИнгредиентыСостав.КоэффициентЗамены
	|ПОМЕСТИТЬ НоменклатураИнгредиентов
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ВложенныйЗапрос.Номенклатура) КАК Номенклатура,
	|		ВложенныйЗапрос.Ингредиент КАК Ингредиент
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ИнгредиентыСостав.Номенклатура КАК Номенклатура,
	|			ИнгредиентыСостав.Ссылка КАК Ингредиент,
	|			НоменклатураКонтрагента.Цена КАК Цена,
	|			ИнгредиентыСостав.КоэффициентЗамены КАК КоэффициентЗамены
	|		ИЗ
	|			Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента
	|					ПО ИнгредиентыСостав.Номенклатура = НоменклатураКонтрагента.Номенклатура
	|						И (НоменклатураКонтрагента.ОсновнойПоставщик)
	|				ПО ТехнологическаяКартаСостав.Номенклатура = ИнгредиентыСостав.Ссылка
	|		ГДЕ
	|			ТехнологическаяКартаСостав.Ссылка В
	|					(ВЫБРАТЬ
	|						АктивныеТехнологическиеКарты.ТехнологическаяКарта
	|					ИЗ
	|						АктивныеТехнологическиеКарты)) КАК ВложенныйЗапрос
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				ВложенныйЗапрос.Ингредиент КАК Ингредиент,
	|				МАКСИМУМ(ВложенныйЗапрос.Цена) КАК Цена
	|			ИЗ
	|				(ВЫБРАТЬ
	|					ИнгредиентыСостав.Номенклатура КАК Номенклатура,
	|					ИнгредиентыСостав.Ссылка КАК Ингредиент,
	|					НоменклатураКонтрагента.Цена КАК Цена
	|				ИЗ
	|					Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
	|							ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента
	|							ПО ИнгредиентыСостав.Номенклатура = НоменклатураКонтрагента.Номенклатура
	|								И (НоменклатураКонтрагента.ОсновнойПоставщик)
	|						ПО ТехнологическаяКартаСостав.Номенклатура = ИнгредиентыСостав.Ссылка
	|				ГДЕ
	|					ТехнологическаяКартаСостав.Ссылка В
	|							(ВЫБРАТЬ
	|								АктивныеТехнологическиеКарты.ТехнологическаяКарта
	|							ИЗ
	|								АктивныеТехнологическиеКарты)) КАК ВложенныйЗапрос
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ВложенныйЗапрос.Ингредиент) КАК ВложенныйЗапрос1
	|			ПО ВложенныйЗапрос.Ингредиент = ВложенныйЗапрос1.Ингредиент
	|				И ВложенныйЗапрос.Цена = ВложенныйЗапрос1.Цена
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Ингредиент) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ингредиенты.Состав КАК ИнгредиентыСостав
	|		ПО ВложенныйЗапрос.Номенклатура = ИнгредиентыСостав.Номенклатура
	|			И ВложенныйЗапрос.Ингредиент = ИнгредиентыСостав.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТехнологическаяКартаСостав.Ссылка,
	|	ТехнологическаяКартаСостав.Ссылка.Продукция КАК Продукция,
	|	ТехнологическаяКартаСостав.Ссылка.КоличествоПорций КАК КоличествоПорций,
	|	ВЫБОР
	|		КОГДА ИнгредиентыСостав.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ТехнологическаяКартаСостав.Номенклатура
	|		ИНАЧЕ ИнгредиентыСостав.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ИнгредиентыСостав.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ТехнологическаяКартаСостав.Количество
	|		ИНАЧЕ ТехнологическаяКартаСостав.Количество * ИнгредиентыСостав.КоэффициентЗамены
	|	КОНЕЦ КАК Количество,
	|	ТехнологическаяКартаСостав.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТехнологическаяКартаСостав.Коэффициент КАК Коэффициент,
	|	ТехнологическаяКартаСостав.КоэффициентРаспределенияЦены КАК КоэффициентРаспределенияЦены,
	|	ТехнологическаяКартаСостав.НомерСтроки КАК НомерСтроки,
	|	ИнгредиентыСостав.Ингредиент КАК Ингредиент
	|ПОМЕСТИТЬ СоставТехКарты
	|ИЗ
	|	Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИнгредиентов КАК ИнгредиентыСостав
	|		ПО ТехнологическаяКартаСостав.Номенклатура = ИнгредиентыСостав.Ингредиент
	|ГДЕ
	|	ТехнологическаяКартаСостав.Ссылка В
	|			(ВЫБРАТЬ
	|				АктивныеТехнологическиеКарты.ТехнологическаяКарта
	|			ИЗ
	|				АктивныеТехнологическиеКарты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТК.ТехнологическаяКарта) КАК ТехнологическаяКарта,
	|	ТК.Продукция
	|ПОМЕСТИТЬ ТехКарты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТехнологическаяКарта.Ссылка КАК ТехнологическаяКарта,
	|		ТехнологическаяКарта.Продукция КАК Продукция
	|	ИЗ
	|		СоставТехКарты КАК СоставТехКарты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТехнологическаяКарта КАК ТехнологическаяКарта
	|			ПО СоставТехКарты.Номенклатура = ТехнологическаяКарта.Продукция
	|	ГДЕ
	|		ТехнологическаяКарта.Проведен = ИСТИНА
	|		И ТехнологическаяКарта.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Приготовление)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТехнологическаяКартаСостав.Ссылка,
	|		ТехнологическаяКартаСостав.Номенклатура
	|	ИЗ
	|		СоставТехКарты КАК СоставТехКарты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТехнологическаяКарта.Состав КАК ТехнологическаяКартаСостав
	|			ПО СоставТехКарты.Номенклатура = ТехнологическаяКартаСостав.Номенклатура
	|	ГДЕ
	|		ТехнологическаяКартаСостав.Ссылка.Проведен = ИСТИНА
	|		И ТехнологическаяКартаСостав.Ссылка.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Разделка)) КАК ТК
	|
	|СГРУППИРОВАТЬ ПО
	|	ТК.Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставТехКарты.Ссылка,
	|	СоставТехКарты.Продукция,
	|	СоставТехКарты.КоличествоПорций,
	|	СоставТехКарты.Номенклатура,
	|	СоставТехКарты.Количество,
	|	СоставТехКарты.ЕдиницаИзмерения,
	|	СоставТехКарты.НомерСтроки,
	|	СоставТехКарты.Коэффициент,
	|	СоставТехКарты.КоэффициентРаспределенияЦены,
	|	СоставТехКарты.Ингредиент,
	|	СоставТехКарты.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТехКарты.ТехнологическаяКарта ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьТехКарта,
	|	ТехКарты.ТехнологическаяКарта.ВидПроизводства КАК ВидТехКарты
	|ПОМЕСТИТЬ СоставСТехКартами
	|ИЗ
	|	СоставТехКарты КАК СоставТехКарты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТехКарты КАК ТехКарты
	|		ПО СоставТехКарты.Номенклатура = ТехКарты.Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА СоставСТехКартами.ЕстьТехКарта = ИСТИНА
	|				ТОГДА ЕСТЬNULL(ПлановаяСебестоимостьПродукции.Себестоимость, 0)
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
	|						ТОГДА ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0)
	|					ИНАЧЕ ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0) * (100 + СоставСТехКартами.СтавкаНДС.Ставка) / 100
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА СоставСТехКартами.ЕстьТехКарта = ИСТИНА
	|				ТОГДА ЕСТЬNULL(ПлановаяСебестоимостьПродукции.Себестоимость, 0)
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
	|						ТОГДА ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0)
	|					ИНАЧЕ ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0) * (100 + СоставСТехКартами.СтавкаНДС.Ставка) / 100
	|				КОНЕЦ
	|		КОНЕЦ * СоставСТехКартами.Количество / СоставСТехКартами.КоличествоПорций КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА СоставСТехКартами.ЕстьТехКарта = ИСТИНА
	|				ТОГДА ЕСТЬNULL(ПлановаяСебестоимостьПродукции.Себестоимость, 0)
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
	|						ТОГДА ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0)
	|					ИНАЧЕ ЕСТЬNULL(НоменклатураКонтрагента.Цена, 0) * (100 + СоставСТехКартами.СтавкаНДС.Ставка) / 100
	|				КОНЕЦ
	|		КОНЕЦ * СоставСТехКартами.Количество * СоставСТехКартами.СтавкаНДС.Ставка / (100 + СоставСТехКартами.СтавкаНДС.Ставка) / СоставСТехКартами.КоличествоПорций КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	СоставСТехКартами.Ссылка КАК ТехнологическаяКарта,
	|	СоставСТехКартами.Продукция,
	|	СоставСТехКартами.КоличествоПорций,
	|	СоставСТехКартами.Номенклатура,
	|	СоставСТехКартами.Количество,
	|	СоставСТехКартами.ЕдиницаИзмерения,
	|	СоставСТехКартами.Коэффициент,
	|	СоставСТехКартами.КоэффициентРаспределенияЦены,
	|	СоставСТехКартами.НомерСтроки,
	|	СоставСТехКартами.Ингредиент,
	|	СоставСТехКартами.СтавкаНДС,
	|	СоставСТехКартами.ЕстьТехКарта,
	|	СоставСТехКартами.ВидТехКарты
	|ПОМЕСТИТЬ СтоимостьСоставляющих
	|ИЗ
	|	СоставСТехКартами КАК СоставСТехКартами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановаяСебестоимостьПродукции.СрезПоследних(&Момент, ) КАК ПлановаяСебестоимостьПродукции
	|		ПО СоставСТехКартами.Номенклатура = ПлановаяСебестоимостьПродукции.Продукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента
	|		ПО СоставСТехКартами.Номенклатура = НоменклатураКонтрагента.Номенклатура
	|			И (НоменклатураКонтрагента.ОсновнойПоставщик = ИСТИНА),
	|	Константа.ВключатьНДСВЦеныКонтрагентов КАК ВключатьНДСВЦеныКонтрагентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктивныеКалькуляционныеКарты.КалькуляционнаяКарта КАК КалькуляционнаяКарта,
	|	СтоимостьСоставляющих.ТехнологическаяКарта КАК ТехнологическаяКарта,
	|	АктивныеКалькуляционныеКарты.Продукция КАК Продукция,
	|	АктивныеКалькуляционныеКарты.Себестоимость КАК Себестоимость,
	|	АктивныеКалькуляционныеКарты.СебестоимостьНДС КАК СебестоимостьНДС,
	|	СУММА(СтоимостьСоставляющих.Сумма) КАК Сумма,
	|	СУММА(СтоимостьСоставляющих.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ СуммыПродукции
	|ИЗ
	|	АктивныеКалькуляционныеКарты КАК АктивныеКалькуляционныеКарты,
	|	СтоимостьСоставляющих КАК СтоимостьСоставляющих
	|
	|СГРУППИРОВАТЬ ПО
	|	АктивныеКалькуляционныеКарты.КалькуляционнаяКарта,
	|	СтоимостьСоставляющих.ТехнологическаяКарта,
	|	АктивныеКалькуляционныеКарты.Продукция,
	|	АктивныеКалькуляционныеКарты.Себестоимость,
	|	АктивныеКалькуляционныеКарты.СебестоимостьНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыПродукции.Продукция,
	|	СуммыПродукции.КалькуляционнаяКарта,
	|	СуммыПродукции.ТехнологическаяКарта,
	|	СуммыПродукции.Себестоимость,
	|	СуммыПродукции.СебестоимостьНДС,
	|	СуммыПродукции.Сумма,
	|	СуммыПродукции.СуммаНДС
	|ПОМЕСТИТЬ ПродукцияДляОбновленияКалькуляции
	|ИЗ
	|	СуммыПродукции КАК СуммыПродукции
	|ГДЕ
	|	(СуммыПродукции.Себестоимость <> СуммыПродукции.Сумма
	|			ИЛИ СуммыПродукции.СебестоимостьНДС <> СуммыПродукции.СуммаНДС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтоимостьСоставляющих.ТехнологическаяКарта,
	|	СтоимостьСоставляющих.Продукция,
	|	СтоимостьСоставляющих.КоличествоПорций,
	|	СтоимостьСоставляющих.Номенклатура,
	|	СтоимостьСоставляющих.Количество,
	|	СтоимостьСоставляющих.ЕдиницаИзмерения,
	|	СтоимостьСоставляющих.Коэффициент,
	|	СтоимостьСоставляющих.КоэффициентРаспределенияЦены,
	|	СтоимостьСоставляющих.НомерСтроки,
	|	СтоимостьСоставляющих.Ингредиент,
	|	СтоимостьСоставляющих.СтавкаНДС,
	|	СтоимостьСоставляющих.ЕстьТехКарта,
	|	СтоимостьСоставляющих.ВидТехКарты,
	|	СтоимостьСоставляющих.Цена,
	|	СтоимостьСоставляющих.Сумма * СтоимостьСоставляющих.КоличествоПорций КАК Сумма,
	|	СтоимостьСоставляющих.СуммаНДС * СтоимостьСоставляющих.КоличествоПорций КАК СуммаНДС,
	|	ПродукцияДляОбновленияКалькуляции.КалькуляционнаяКарта,
	|	ПродукцияДляОбновленияКалькуляции.Себестоимость,
	|	ПродукцияДляОбновленияКалькуляции.СебестоимостьНДС,
	|	ПродукцияДляОбновленияКалькуляции.Сумма КАК НоваяСебестоимость,
	|	ПродукцияДляОбновленияКалькуляции.СуммаНДС КАК НоваяСебестоимостьНДС
	|ПОМЕСТИТЬ ИтоговыеДанные
	|ИЗ
	|	СтоимостьСоставляющих КАК СтоимостьСоставляющих
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродукцияДляОбновленияКалькуляции КАК ПродукцияДляОбновленияКалькуляции
	|		ПО СтоимостьСоставляющих.ТехнологическаяКарта = ПродукцияДляОбновленияКалькуляции.ТехнологическаяКарта
	|			И СтоимостьСоставляющих.Продукция = ПродукцияДляОбновленияКалькуляции.Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	ИтоговыеДанные.ТехнологическаяКарта КАК ТехнологическаяКарта,
	|	ИтоговыеДанные.Продукция КАК Продукция,
	|	ИтоговыеДанные.КоличествоПорций,
	|	ИтоговыеДанные.Номенклатура,
	|	ИтоговыеДанные.Количество,
	|	ИтоговыеДанные.ЕдиницаИзмерения,
	|	ИтоговыеДанные.Коэффициент,
	|	ИтоговыеДанные.КоэффициентРаспределенияЦены,
	|	ИтоговыеДанные.НомерСтроки КАК НомерСтроки,
	|	ИтоговыеДанные.Ингредиент,
	|	ИтоговыеДанные.СтавкаНДС,
	|	ИтоговыеДанные.ЕстьТехКарта КАК ЭтоПродукция,
	|	ИтоговыеДанные.ВидТехКарты КАК ВидПроизводства,
	|	ИтоговыеДанные.Цена КАК Цена,
	|	СтрокиАктивныхКалькуляционныхКарт.Цена КАК СтараяЦена,
	|	ИтоговыеДанные.Цена - СтрокиАктивныхКалькуляционныхКарт.Цена КАК Разница,
	|	ИтоговыеДанные.Сумма,
	|	ИтоговыеДанные.СуммаНДС,
	|	ИтоговыеДанные.КалькуляционнаяКарта КАК КалькуляционнаяКарта,
	|	ИтоговыеДанные.Себестоимость КАК Себестоимость,
	|	ИтоговыеДанные.СебестоимостьНДС КАК СебестоимостьНДС,
	|	ИтоговыеДанные.НоваяСебестоимость КАК НоваяСебестоимость,
	|	ИтоговыеДанные.НоваяСебестоимостьНДС КАК НоваяСебестоимостьНДС
	|ИЗ
	|	ИтоговыеДанные КАК ИтоговыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтрокиАктивныхКалькуляционныхКарт КАК СтрокиАктивныхКалькуляционныхКарт
	|		ПО ИтоговыеДанные.КалькуляционнаяКарта = СтрокиАктивныхКалькуляционныхКарт.Ссылка
	|			И ИтоговыеДанные.НомерСтроки = СтрокиАктивныхКалькуляционныхКарт.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИтоговыеДанные.Продукция.Наименование,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Пометка),
	|	МАКСИМУМ(ТехнологическаяКарта),
	|	МАКСИМУМ(Продукция),
	|	МАКСИМУМ(Себестоимость),
	|	МАКСИМУМ(СебестоимостьНДС),
	|	МАКСИМУМ(НоваяСебестоимость),
	|	МАКСИМУМ(НоваяСебестоимостьНДС)
	|ПО
	|	КалькуляционнаяКарта";
	
	Запрос.УстановитьПараметр("Момент", ТекущаяДата());
	Запрос.УстановитьПараметр("Фирма",  Фирма);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаКалькуляционнаяКарта = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКалькуляционнаяКарта.Следующий() Цикл
		
		НоваяСтрокаПродукция = Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПродукция, ВыборкаКалькуляционнаяКарта);
		
		ВыборкаДетальныеЗаписи = ВыборкаКалькуляционнаяКарта.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НоваяСтрокаСостав = Состав.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСостав, ВыборкаДетальныеЗаписи);
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПродукциюРазделка() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Регистратор КАК КалькуляционнаяКарта
	|ПОМЕСТИТЬ АктивныеКалькуляционныеКарты
	|ИЗ
	|	РегистрСведений.ПлановаяСебестоимостьПродукции.СрезПоследних(&Момент, ) КАК ПлановаяСебестоимостьПродукцииСрезПоследних
	|ГДЕ
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Регистратор.Фирма = &Фирма
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановаяСебестоимостьПродукцииСрезПоследних.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТехнологическаяКарта.Ссылка,
	|	ТехнологическаяКарта.Продукция
	|ПОМЕСТИТЬ ТехнологическиеКартыРазделка
	|ИЗ
	|	Документ.ТехнологическаяКарта КАК ТехнологическаяКарта
	|ГДЕ
	|	ТехнологическаяКарта.Проведен
	|	И ТехнологическаяКарта.ВидПроизводства = ЗНАЧЕНИЕ(Перечисление.ВидыПроизводства.Разделка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктивныеКалькуляционныеКарты.КалькуляционнаяКарта,
	|	АктивныеКалькуляционныеКарты.КалькуляционнаяКарта.СебестоимостьПродукции КАК СебестоимостьПродукции,
	|	ТехнологическиеКартыРазделка.Ссылка КАК ТехнологическаяКарта,
	|	ТехнологическиеКартыРазделка.Продукция КАК Продукция
	|ПОМЕСТИТЬ АктивныеТехнологическиеКарты
	|ИЗ
	|	АктивныеКалькуляционныеКарты КАК АктивныеКалькуляционныеКарты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТехнологическиеКартыРазделка КАК ТехнологическиеКартыРазделка
	|		ПО АктивныеКалькуляционныеКарты.КалькуляционнаяКарта.ДокументОснование = ТехнологическиеКартыРазделка.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалькуляционнаяКартаСостав.Ссылка,
	|	КалькуляционнаяКартаСостав.Ссылка.Продукция КАК Продукция,
	|	КалькуляционнаяКартаСостав.Ссылка.МассаПорции КАК МассаПорции,
	|	КалькуляционнаяКартаСостав.НомерСтроки,
	|	КалькуляционнаяКартаСостав.Номенклатура,
	|	КалькуляционнаяКартаСостав.Количество,
	|	КалькуляционнаяКартаСостав.ЕдиницаИзмерения,
	|	КалькуляционнаяКартаСостав.Коэффициент,
	|	КалькуляционнаяКартаСостав.КоэффициентРаспределенияЦены,
	|	КалькуляционнаяКартаСостав.СтавкаНДС,
	|	КалькуляционнаяКартаСостав.Цена,
	|	КалькуляционнаяКартаСостав.Сумма,
	|	КалькуляционнаяКартаСостав.СуммаНДС,
	|	КалькуляционнаяКартаСостав.Ингредиент
	|ПОМЕСТИТЬ СтрокиАктивныхКалькуляционныхКарт
	|ИЗ
	|	Документ.КалькуляционнаяКарта.Состав КАК КалькуляционнаяКартаСостав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктивныеТехнологическиеКарты КАК АктивныеТехнологическиеКарты
	|		ПО КалькуляционнаяКартаСостав.Ссылка = АктивныеТехнологическиеКарты.КалькуляционнаяКарта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураКонтрагента.Номенклатура,
	|	ВЫБОР
	|		КОГДА ВключатьНДСВЦеныКонтрагентов.Значение = ИСТИНА
	|			ТОГДА НоменклатураКонтрагента.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(НоменклатураКонтрагента.Цена * (100 + НоменклатураКонтрагента.Номенклатура.СтавкаНДС.Ставка) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Цена
	|ПОМЕСТИТЬ ЦеныПродукции
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагента КАК НоменклатураКонтрагента,
	|	Константа.ВключатьНДСВЦеныКонтрагентов КАК ВключатьНДСВЦеныКонтрагентов
	|ГДЕ
	|	НоменклатураКонтрагента.Номенклатура В
	|			(ВЫБРАТЬ
	|				АктивныеТехнологическиеКарты.Продукция
	|			ИЗ
	|				АктивныеТехнологическиеКарты КАК АктивныеТехнологическиеКарты)
	|	И НоменклатураКонтрагента.ОсновнойПоставщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктивныеТехнологическиеКарты.КалькуляционнаяКарта,
	|	АктивныеТехнологическиеКарты.ТехнологическаяКарта,
	|	АктивныеТехнологическиеКарты.Продукция,
	|	АктивныеТехнологическиеКарты.СебестоимостьПродукции КАК Себестоимость,
	|	ЦеныПродукции.Цена КАК НоваяСебестоимость
	|ПОМЕСТИТЬ ПродукцияДляОбновленияКалькуляции
	|ИЗ
	|	АктивныеТехнологическиеКарты КАК АктивныеТехнологическиеКарты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЦеныПродукции КАК ЦеныПродукции
	|		ПО АктивныеТехнологическиеКарты.Продукция = ЦеныПродукции.Номенклатура
	|ГДЕ
	|	АктивныеТехнологическиеКарты.СебестоимостьПродукции <> ЦеныПродукции.Цена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	ПродукцияДляОбновленияКалькуляции.КалькуляционнаяКарта КАК КалькуляционнаяКарта,
	|	ПродукцияДляОбновленияКалькуляции.ТехнологическаяКарта КАК ТехнологическаяКарта,
	|	ПродукцияДляОбновленияКалькуляции.Продукция КАК Продукция,
	|	ПродукцияДляОбновленияКалькуляции.Себестоимость КАК Себестоимость,
	|	ПродукцияДляОбновленияКалькуляции.НоваяСебестоимость КАК НоваяСебестоимость,
	|	СтрокиАктивныхКалькуляционныхКарт.НомерСтроки,
	|	СтрокиАктивныхКалькуляционныхКарт.Количество,
	|	СтрокиАктивныхКалькуляционныхКарт.ЕдиницаИзмерения,
	|	СтрокиАктивныхКалькуляционныхКарт.Коэффициент,
	|	СтрокиАктивныхКалькуляционныхКарт.КоэффициентРаспределенияЦены,
	|	СтрокиАктивныхКалькуляционныхКарт.Ингредиент,
	|	СтрокиАктивныхКалькуляционныхКарт.СтавкаНДС,
	|	СтрокиАктивныхКалькуляционныхКарт.Номенклатура,
	|	СтрокиАктивныхКалькуляционныхКарт.Цена КАК СтараяЦена,
	|	СтрокиАктивныхКалькуляционныхКарт.КоэффициентРаспределенияЦены * ПродукцияДляОбновленияКалькуляции.НоваяСебестоимость / (СтрокиАктивныхКалькуляционныхКарт.МассаПорции * СтрокиАктивныхКалькуляционныхКарт.Количество * СтрокиАктивныхКалькуляционныхКарт.Коэффициент) КАК Цена,
	|	СтрокиАктивныхКалькуляционныхКарт.КоэффициентРаспределенияЦены * ПродукцияДляОбновленияКалькуляции.НоваяСебестоимость / (СтрокиАктивныхКалькуляционныхКарт.МассаПорции * СтрокиАктивныхКалькуляционныхКарт.Количество * СтрокиАктивныхКалькуляционныхКарт.Коэффициент) - СтрокиАктивныхКалькуляционныхКарт.Цена КАК Разница
	|ИЗ
	|	ПродукцияДляОбновленияКалькуляции КАК ПродукцияДляОбновленияКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтрокиАктивныхКалькуляционныхКарт КАК СтрокиАктивныхКалькуляционныхКарт
	|		ПО ПродукцияДляОбновленияКалькуляции.КалькуляционнаяКарта = СтрокиАктивныхКалькуляционныхКарт.Ссылка
	|			И ПродукцияДляОбновленияКалькуляции.Продукция = СтрокиАктивныхКалькуляционныхКарт.Продукция
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПродукцияДляОбновленияКалькуляции.Продукция.Наименование,
	|	СтрокиАктивныхКалькуляционныхКарт.НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(Пометка),
	|	МАКСИМУМ(ТехнологическаяКарта),
	|	МАКСИМУМ(Продукция),
	|	МАКСИМУМ(Себестоимость),
	|	МАКСИМУМ(НоваяСебестоимость)
	|ПО
	|	КалькуляционнаяКарта";
	
	Запрос.УстановитьПараметр("Момент", ТекущаяДата());
	Запрос.УстановитьПараметр("Фирма",  Фирма);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаКалькуляционнаяКарта = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКалькуляционнаяКарта.Следующий() Цикл
		
		НоваяСтрокаПродукция = Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПродукция, ВыборкаКалькуляционнаяКарта);
		
		ВыборкаДетальныеЗаписи = ВыборкаКалькуляционнаяКарта.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НоваяСтрокаСостав = Состав.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСостав, ВыборкаДетальныеЗаписи);
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьКалькуляционныеКарты() Экспорт
	
	Для Каждого СтрокаПродукции Из Продукция Цикл
		Если СтрокаПродукции.Пометка Тогда
			
			НоваяКалькуляционнаяКарта                           = Документы.КалькуляционнаяКарта.СоздатьДокумент();
			НоваяКалькуляционнаяКарта.Дата                      = ТекущаяДата();
			НоваяКалькуляционнаяКарта.ДокументОснование         = СтрокаПродукции.ТехнологическаяКарта;
			НоваяКалькуляционнаяКарта.Продукция                 = СтрокаПродукции.Продукция;
			НоваяКалькуляционнаяКарта.Фирма                     = Фирма;
			НоваяКалькуляционнаяКарта.Автор                     = ПараметрыСеанса.ТекущийПользователь;
			НоваяКалькуляционнаяКарта.КоличествоПорций          = СтрокаПродукции.ТехнологическаяКарта.КоличествоПорций;
			НоваяКалькуляционнаяКарта.МассаПорции               = СтрокаПродукции.ТехнологическаяКарта.МассаПорции;
			НоваяКалькуляционнаяКарта.ВидПроизводства           = СтрокаПродукции.ТехнологическаяКарта.ВидПроизводства;
			НоваяКалькуляционнаяКарта.НомерПоСправочнику        = СтрокаПродукции.ТехнологическаяКарта.НомерПоСправочнику;
			НоваяКалькуляционнаяКарта.СебестоимостьПродукции    = СтрокаПродукции.НоваяСебестоимость;
			НоваяКалькуляционнаяКарта.СебестоимостьНДСПродукции = СтрокаПродукции.НоваяСебестоимостьНДС;
			
			МассивСтрокСостава = Состав.НайтиСтроки(Новый Структура("Продукция, КалькуляционнаяКарта", СтрокаПродукции.Продукция, СтрокаПродукции.КалькуляционнаяКарта));
			Для Каждого СтрокаСостава Из МассивСтрокСостава Цикл
				НоваяСтрокаНовойКалькуляционнойКарты = НоваяКалькуляционнаяКарта.Состав.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаНовойКалькуляционнойКарты, СтрокаСостава);
				Если ВидПроизводства = Перечисления.ВидыПроизводства.Разделка Тогда
					НоваяСтрокаНовойКалькуляционнойКарты.Сумма = Окр(НоваяСтрокаНовойКалькуляционнойКарты.Количество * НоваяСтрокаНовойКалькуляционнойКарты.Цена / 100,2);
					НоваяСтрокаНовойКалькуляционнойКарты.СуммаНДС = РассчитатьСуммуНДС(НоваяСтрокаНовойКалькуляционнойКарты.Сумма, Истина, ОбщегоНазначенияСервер.ПолучитьСтавкуНДС(НоваяСтрокаНовойКалькуляционнойКарты.СтавкаНДС));
				КонецЕсли;
			КонецЦикла;	
			
			Если ВидПроизводства = Перечисления.ВидыПроизводства.Разделка Тогда
				НоваяКалькуляционнаяКарта.СебестоимостьНДСПродукции = НоваяКалькуляционнаяКарта.Состав.Итог("СуммаНДС");
			КонецЕсли;
			
			НоваяКалькуляционнаяКарта.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Для продукции """+СокрЛП(СтрокаПродукции.Продукция)+""" создан документ "+СокрЛП(НоваяКалькуляционнаяКарта));
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция РассчитатьСуммуНДС(Сумма, СуммаВключаетНДС,СтавкаНДС)
	
	Возврат Окр(СтавкаНДС * Сумма / (100 + СтавкаНДС),2);
	
КонецФункции
