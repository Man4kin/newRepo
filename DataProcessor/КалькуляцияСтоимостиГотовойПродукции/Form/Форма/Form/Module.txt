
&НаКлиенте
Процедура ЗаполнитьПродукцию(Команда)
	
	Если Объект.Фирма.Пустая() Тогда
		Сообщить("Не выбрана фирма.");
		Возврат;
	КонецЕсли;
	
	Если Объект.ВидПроизводства.Пустая() Тогда
		Сообщить("Не выбран вид производства.");
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос(НСтр("ru = 'Табличная часть ""Продукция"" будет перезаполнена. Продолжить?'"), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПродукциюНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПродукциюНаСервере()
	
	Объект.Продукция.Очистить();
	Объект.Состав.Очистить();
	ЭтотОбъект = РеквизитФормыВЗначение("Объект");
	Если Объект.ВидПроизводства = Перечисления.ВидыПроизводства.Приготовление Тогда
		ЭтотОбъект.ЗаполнитьПродукциюПриготовление();
	ИначеЕсли Объект.ВидПроизводства = Перечисления.ВидыПроизводства.Разделка Тогда
		ЭтотОбъект.ЗаполнитьПродукциюРазделка();
	КонецЕсли;
	ЗначениеВРеквизитФормы(ЭтотОбъект,"Объект");
	Элементы.Продукция.ТекущаяСтрока = 0;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	Для Каждого Строка из Объект.Продукция Цикл
		Строка.Пометка = 1;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометки(Команда)
	Для Каждого Строка из Объект.Продукция Цикл
		Строка.Пометка = 0;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Продукция.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	УстановитьОтборПоПродукции(Элементы.Продукция.ТекущиеДанные.Продукция, Элементы.Продукция.ТекущиеДанные.КалькуляционнаяКарта);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоПродукции(текПродукция, текКалькуляционнаяКарта)
	
	Элементы.Состав.ОтборСтрок = Новый ФиксированнаяСтруктура("Продукция, КалькуляционнаяКарта", текПродукция, текКалькуляционнаяКарта);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКалькуляционныеКарты(Команда)

	Если Объект.Фирма.Пустая() Тогда
		Сообщить("Не выбрана фирма.");
		Возврат;
	КонецЕсли;
	
	Если Объект.ВидПроизводства.Пустая() Тогда
		Сообщить("Не выбран вид производства.");
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос(НСтр("ru = 'Будут созданы обновленные калькуляционные карты для выбранной продукции. Продолжить?'"), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СоздатьКалькуляционныеКартыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКалькуляционныеКартыНаСервере()
	
	ЭтотОбъект = РеквизитФормыВЗначение("Объект");
	ЭтотОбъект.СоздатьКалькуляционныеКарты();
	ЗначениеВРеквизитФормы(ЭтотОбъект,"Объект");	
	
	ЗаполнитьПродукциюНаСервере();

КонецПроцедуры	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ПараметрыСеанса.ЭтоЦентр Тогда
		ВызватьИсключение("Запуск обработки разрешен только в центре.");
	КонецЕсли;
	Объект.ВидПроизводства = Перечисления.ВидыПроизводства.Приготовление;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПродукцияОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОЖЬ;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияТехнологическаяКартаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОЖЬ;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКалькуляционнаяКартаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОЖЬ;
КонецПроцедуры

&НаКлиенте
Процедура ВидПроизводстваНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СЗ = Новый Массив();
    СЗ.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Приготовление"));
    СЗ.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПроизводства.Разделка"));
    Элемент.СписокВыбора.ЗагрузитьЗначения(СЗ);
	
КонецПроцедуры



