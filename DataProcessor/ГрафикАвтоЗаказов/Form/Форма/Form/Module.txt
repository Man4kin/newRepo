
&НаСервере
Процедура ФормированиеДиаграммы(Парам = Неопределено)
	
	ДиаграммаГанта = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет").Рисунки[0].Объект;
	
	Если НЕ ЗначениеЗаполнено(Период.ДатаНачала) Тогда
		Период.Вариант = ВариантСтандартногоПериода.ЭтаНеделя;
	КонецЕсли;
	
	ДиаграммаГанта.Обновление = Ложь;
	ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДиаграммаГанта.УстановитьПолныйИнтервал(Период.ДатаНачала,Период.ДатаОкончания);
	ДиаграммаГанта.Очистить();
	Серия = ДиаграммаГанта.Серии.Добавить();
	УстановитьПривилегированныйРежим(ИСТИНА);
	ТекущиеЗадания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Новый Структура("Метаданные","Автозаказ"));
	УстановитьПривилегированныйРежим(ЛОЖЬ);
	ДатаНачала = КонецДня(Период.ДатаНачала); 
	ДатаКонца = Период.ДатаОкончания; 
	//ДатаНачала = НачалоДня(ТекущаяДата()); 
	//ДатаКонца = ДатаНачала+86400*7; 
	Для Каждого ТекЗадание Из ТекущиеЗадания Цикл
		Попытка
			БизнесПроцесс = ТекЗадание.Параметры[0].Ссылка;
		Исключение
			Продолжить;
		КонецПопытки;	
		Поставщик = ТекЗадание.Параметры[0].Поставщик;
		СтруктурнаяЕдиница = ТекЗадание.Параметры[0].СтруктурнаяЕдиница;
		//++БИТ БВО
		Пропустить = Ложь;
		Если Парам <> Неопределено Тогда
			Если Парам.Свойство("СтруктурнаяЕдиница") И СтруктурнаяЕдиница <> Парам.СтруктурнаяЕдиница  Тогда
				Пропустить = Истина;
			КонецЕсли;
			Если Парам.Свойство("Контрагент") И Поставщик <> Парам.Контрагент Тогда
				Пропустить = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Пропустить Тогда
			Продолжить;
		КонецЕсли;
		//--БИТ БВО				
		ТекДата = ДатаНачала;    
		Пока ТекДата < ДатаКонца Цикл
			Результат = ТекЗадание.Расписание.ТребуетсяВыполнение(ТекДата);
			Если Результат Тогда
				Точка = ДиаграммаГанта.УстановитьТочку(Поставщик);
				Точка.Текст = Поставщик.Наименование;
				Если ТекДата = ДатаНачала Тогда
					Точка.Шрифт = Новый Шрифт(,,Истина);
				КонецЕсли;	
				
				Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);   
				Значение.Расшифровка = БизнесПроцесс;
				Интервал = Значение.Добавить(); 
				Интервал.Начало = НачалоДня(ТекДата);
				Интервал.Конец = Интервал.Начало+7200;
			КонецЕсли;	
			ТекДата = ТекДата + 86400;
		КонецЦикла;
	КонецЦикла;	
	ДиаграммаГанта.Обновление = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	
	ФормаПериода = Новый ДиалогРедактированияСтандартногоПериода(); 
	ФормаПериода.Период = Период;
	Если ФормаПериода.Редактировать() Тогда
		Период = ФормаПериода.Период;
		ФормированиеДиаграммы();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ЭтоЦентр()
	
	Возврат ПараметрыСеанса.ЭтоЦентр;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ГруппаОтборы.Видимость = ЭтоЦентр();
	Если НЕ ЭтоЦентр() Тогда
		ФормированиеДиаграммы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если ЭтоЦентр() Тогда		
		Парам = Новый Структура;
		Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда						
			Парам.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		КонецЕсли;
		Если ЗначениеЗаполнено(Контрагент) Тогда		
			Парам.Вставить("Контрагент", Контрагент);
		КонецЕсли;
	Иначе
		Парам = Неопределено;
	КонецЕсли;
		
	ФормированиеДиаграммы(Парам);	
	
КонецПроцедуры

